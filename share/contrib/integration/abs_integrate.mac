/*
  Copyright 2008 by Barton Willis
  Maxima code for integration of signum, abs, max, min, and unit_step.

  This is free software; you can redistribute it and/or
  modify it under the terms of the GNU General Public License,
  http://www.gnu.org/copyleft/gpl.html.

 This software has NO WARRANTY, not even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

I gratefully thank Robert Dodier and Richard Fateman for their suggestions, advice, assistance, and bug fixes.

*/
qput(abs_integrate, 1, version);

eval_when(translate,declare_translated(signum_int,signum_int_helper))$

load("opsubst");
load("unwind_protect");

matchdeclare(x, symbolp, q, true, a, numberp, b, numberp);
block([simp : false], tellsimpafter('integrate(q,x), signum_int(q,x)),
                      tellsimpafter('integrate(q,x,a,b), abs_defint(q,x,a,b)));
                                        
gradef(signum(x),0);
gradef(abs(x), abs(x) / x);

safe_op(e) := block([inflag : true], if mapatom(e) then false else op(e));

/* If e factors into a product of linear factors, return the list of factors; otherwise return false */
real_linear_factors(e,x) := (
  e : factor(e),
  e : if (safe_op(e) = "*") then args(e) else [e],
  if every(lambda([w], polynomialp(w, [x], lambda([s], freeof(x,s)),lambda([k],is(k=0 or k=1)))),e) then (
     e : sublist(args(e), lambda([s], is(equal(s, conjugate(s))) and not freeof(x,s))),
     map(lambda([s], rhs(first(linsolve(s,x)))), e))
  else false);	
 
/* Convert abs, max, min, and unit_step to signum expressions */
convert_to_signum(e) := block([opsubst : true],
  e : subst('unit_step = lambda([s], (signum(s) + 1) / 2), e),
  e : subst('max = lambda([[x]], xreduce(lambda([a,b], (a + b +abs(a-b))/2),x)),e),
  e : subst('min = lambda([[x]], xreduce(lambda([a,b], (a + b -abs(a-b))/2),x)),e),
  subst('abs = lambda([s], s * signum(s)), e));

/* Do signum(a) * signum(b) --> signum(a * b). Maybe it should, but when n is a positive integer,
   apply_signum_mult doesn't do signum(a)^n --> signum(a^n).*/
   
apply_signum_mult_id (e) := block([p : 1, q : 1, inflag : true],
 if mapatom(e) then e
 else if (safe_op(e) = "*") then (
   for ek in e do (
     if (safe_op(ek) = 'signum) then p : p * first(ek) else q : q * ek),
   signum(p) * q)
 else map('apply_signum_mult_id, e));

/* Do signum(a) * signum(b) --> signum(a * b) and e * signum(e) --> abs(e) */
signum_to_abs(e) := block([l],
  e : apply_signum_mult_id(e),
  l : flatten(gatherargs(e,'signum)),
  for lk in l do (
     e : ratsubst(abs(lk), lk * signum(lk),e)),
  e);

signum_int(q,x) := block([p : 1, w : 1, l, v, inflag : true, f, qsave : q, listconstvars : true,
  noun_int : nounify('integrate)],
  
  q : convert_to_signum(q),

  /* maybe expand */
  v : listofvars(q),
  if safe_op(q) = "*" and not every(lambda([s], polynomialp(s,v) or safe_op(s) = 'signum), args(q)) then (
    q : ratexpand(q)),
  
  if (safe_op(q) = 'signum) or (safe_op(q) = "*" and not freeof('signum, q)) then (
    if op(q) = 'signum then (p : 1, w : first(q))
    else (
      for qk in q do (
        if mapatom(qk) then p : p * qk else if (op(qk)='signum) then w : w * first(qk) else p : p * qk)),
    l : real_linear_factors(w,x),
    if (l = false) or (not (freeof('signum, p))) or emptyp(l) then (
      funmake(noun_int, [qsave,x]))
    else signum_int_helper(p, l, x))
  else if (safe_op(q) = "+") then map(lambda([s], signum_int(s,x)), q)
  else if (p : abs_int_extra(q,x)) # false then p
  else integrate(qsave,x));

make_dummy(e,x) := block([listconstvars : true, v : listofvars(e)],
 while member(x, v) do x : concat(x,"%"),
 x);

/* integrate(q(x) * signum(x - l[1]) * signum(x - l[2]) * ...,x). The list l is nonempty. */

signum_int_helper(q,l,x) := block([v, xo, f, noun_int : nounify('integrate)],
  if emptyp(rest(l)) then (
    f : integrate(q,x),
    if (nounify(safe_op(f)) = noun_int) then (
      v : make_dummy(q,x),
      f : funmake(noun_int, [subst(x = v, q), v, first(l), x])),
    signum(x - first(l)) * (f - subst(x = first(l), f)))
  else (
    xo : first(l),
    f : signum_int_helper(q, rest(l), x),
    signum(x - xo) * (f - subst(x = xo, f))));

/* The function abs_defint is based on code I wrote and placed in the public domain.*/

dint(e,x,l) := block([xo, acc : 0],
  xo : first(l),
  l : rest(l),
  for xi in l do (
    acc : acc + integrate(e,x,xo,xi),
    xo : xi),
  acc);

abs_defint(e,x,lo,hi) := block([l, knots, noun_int : nounify('integrate)],
  if numberp(lo) and numberp(hi) then (
    if (hi < lo) then abs_defint(-e,x,hi,lo)
    else if (lo = hi) then 0
    else (
      e : convert_to_signum(e),
      l : flatten(gatherargs(e, 'signum)),
      knots : flatten(map(lambda([s], (real_linear_factors(s,x))), l)),
      if every('numberp, knots) then (
        knots : listify(setify(knots)),
        knots : sublist(knots, lambda([s], is(s > lo) and is(s < hi))),
        knots : append([lo, hi], knots),
        dint(e,x, sort(knots, lambda([a,b], is(compare(a,b) = "<")))))
      else funmake(noun_int,[e,x,lo,hi])))
  else funmake(noun_int,[e,x,lo,hi]));

abs_int_extra(q,x) := block([cntx,q1,q2,k,xo],
  unwind_protect((
      cntx : supcontext(cntx),
      q : convert_to_signum(q),
      k : gatherargs(q,'signum),
      if k # [] and emptyp(rest(k)) then (
        k : first(first(k)),
        if polynomialp(k, [x], lambda([s], freeof(x,s)),lambda([k],is(k=0 or k=1))) then (
          assume(k < 0),
          q1 : errcatch(integrate(q,x)),
          forget(k < 0),
          assume(k > 0),
          q2 : errcatch(integrate(q,x)),
          forget(k > 0),
          if freeof('integrate, 'limit, 'diff, q1) and freeof('integrate, 'limit, 'diff, q2) and
          q1 # [] and q2 # [] then (
            q1 : first(q1),
            q2 : first(q2),
            xo : linsolve(k,x),
            q2 : q2 + subst(xo, q1 - q2),
            xo : first(xo),
            xo : rhs(xo) - lhs(xo),
            (1 + signum(xo)) * q1 / 2 + (1 - signum(xo)) * q2 / 2)
          else false)
        else false)),
    killcontext(cntx)));