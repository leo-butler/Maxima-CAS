
showtime:all$
/* This file proves that the Schwarzschild line element satisfies
the Einstein vacuum equations as well as computing other tensors
and invariants */
/* .mc version is presently available in DOE MACSYMA */
load("ctensor")$
/* the following allows the batch program to run by presetting flags */
setflags()$
/* this calls for the rational simplification of geometrical objects */
ratfac:true$
/* the dimension of the manifold */
dim:4$
/* the coordinate labels */
ct_coords:[x,y,z,t]$
/* THIS IS THE SCHWARZSCHILD METRIC IN STANDARD COORDINATES */
lg:
matrix([1/(1-2*m/x),0,0,0],[0,x^2,0,0],[0,0,x^2*sin(y)^2,0],[0,0,0,2*m/x-1]);
/* computes metric inverse and determines diagonality */
setmetric()$
/* computes and displays mixed Christoffel symbols */
christof(mcs)$
/* computes and ratsimps Ricci tensor */
uricci(true)$
/* computes scalar curvature */
scurvature();
/* computes Riemann tensor */
riemann(true)$
/* computes contravariant Riemann tensor */
uriemann(false)$
/* computes the Kretchmann invariant Rijkl^2 */
rinvariant();
