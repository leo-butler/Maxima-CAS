("Deriving the Kaluza-Klein equation of motion.")$
("For reference, see http://www.vttoth.com/KK/kk.htm")$
derivabbrev:true;
("KALUZA.MAC contains definitions and contraction properties for G4 and G5")$
load("kaluza.mac");
("Predeclaring some 4d indices")$
assume(k<=4,l<=4,m<=4)$

("Equation of motion in empty 5-space")$
ishow('diff(x([],[a]),t,2)+'ichr2([b,c],[a])*'diff(x([],[b]),t)*'diff(x([],[c]),t)=0)$
ishow(part(first(%),1))$
ishow(subst(m,c,%)+subst(5,c,%))$
ishow(subst(l,b,%)+subst(5,b,%)+part(first(%th(3)),2)=last(%th(3)))$

("We are only interested in the case where A is a 4D index")$
ishow(subst(k,a,%th(2)))$

("We protect one of the Christoffel-symbols from expansion")$
ishow(subst(chr2klm,'ichr2([l,m],[k]),%th(2)))$
%,ichr2$
ishow(rename(%))$

("Now we break this up into two parts depending on whether %1=5")$
map(lambda([u],block(if freeof(%1,u) then u else u+subst(5,%1,u))),first(%th(2)))=last(%th(2))$
assume(%1<=4)$
%th(2),g5$
%,nouns$
ishow(%)$

("Now we're ready to isolate the electromagnetic field tensor")$
map(lambda([u],factorout(u,g55)),%th(2))$
ishow(apply1(%,evpot))$

("Contracting and rearranging yields the equation in the usual form")$
contract(%th(2))$
%,nouns$
ishow(rename(%))$

%-part(first(%),1)$
ishow(subst('ichr2([l,m],[k]),chr2klm,%))$

("But what about the 5D Christoffel-symbol?")$
ishow(ichr2([k,l],[m]))$
rename(%)$
forget(%1<=4)$
subst(5,%1,%th(2))$
%,g5,g4$
ishow(%)$

assume(%1<=4)$
%th(6),g5$
scanmap(lambda([u],apply1(  ratsimp(u,g55,g4([],[%1,m]),a([k],[]))  ,evpot)),%)$
ishow(%)$

%+%th(5)$
%,nouns$
ratsubst(chr42([k,l],[m]),g4([],[%1,m])*(g4([l,%1],[],k)+g4([k,%1],[],l)-g4([k,l],[],%1))/2,%)$
ishow(%)$

contract(%)$
("The extra term is presumably the curvature caused by the EM field.")$
ishow(map(factor,combine(distrib(%th(2)))))$
