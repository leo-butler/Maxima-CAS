/* Copyright (C) 2003 Valerij Pipin <pip@iszf.irk.ru>
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License as
 * published by the Free Software Foundation; either version 2 of
 * the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be
 * useful, but WITHOUT ANY WARRANTY; without even the implied
 * warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
 * PURPOSE.  See the GNU General Public License for more details.
 *
 * Demonstrating exterior algebra operations
 *
*/
("the anticommutative algebra's operations on indexed objects")$
("the operations are allowed only on the covariant tensors")$
load(itensor);
dummyx:z;
("The exterior product is denoted by ~. Take it on two 1-forms
  a([i])~b([j])")$

ishow(a([i])~b([j]))$

("The exterior product of the three  1-forms
  a([i])~b([j])~c([k])")$

ishow(a([i])~b([j])~c([k]))$

("Take a sum of two 3 forms")$
ishow( factor(a([i])~(bk*b([j]))~c([k]) +(ak*a([i]))~c([j])~b([k])))$

("declare 2-form (don't forget to put allsym:false) and take ~ with 1-form")$
allsym:false;
decsym(p,2,0,[anti(all)],[]);

ishow(q([i])~p([j,k]))$
("the exterior (anticommutative) derivative
  is denoted by extdiff(x,ind), where ind denotes the component")$

("So actually d_k ~ form means in our notation extdiff(form, k), e.g.")$

ishow(extdiff(p([j,k]),i))$
ishow(extdiff((a([j])~b([k])),i))$
ishow(extdiff((a([j])~b([k])),k))$

("the interior product is denoted by |, say form | vector, where
  form is indexed tensor and vector is vector's name")$
ishow(a([i])|a)$

ishow((a([i])~b([j]))|a)$
("to avoid the mistake please use the literally sorted indices
  when you apply the | ")$

ishow(factor((a([i2])~b([i1]))|a+ (a([i1])~b([i2]))|a))$

("the Lie derivative is denoted by liediff(vector,x)")$
("it is currently not applicable to functions.")$

ishow(liediff(v,a([i1])))$
("tests the consequence of the
  Cartan identity for 1-forms.  The Lie derivative
  has to commute with the exterior one ")$

ishow(canform(liediff(v,extdiff(a([i1]),i2))-extdiff(liediff(v,a([i1])),i2)))$

("where the liediff(v,extdiff(a([i1]),i2)) is")$
ishow(liediff(v,extdiff(a([i1]),i2)))$

("the verifications of this consequence of the Cartan identity
  for the higher order forms are in car_iden.dem")$
