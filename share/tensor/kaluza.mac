LOAD("itensor");

DIM:5;
METRIC:G5;
DEFCON(G4);
DEFCON(G5);
DEFCON(G4,G4,KDELTA);
DEFCON(G5,G5,KDELTA);

PREDVAL(prd):=BLOCK([RETVAL,SAVED_PREDERROR:PREDERROR],
	PREDERROR:FALSE,
	RETVAL:EV(prd,PRED)=TRUE OR EV(prd,PRED)=FALSE,
	PREDERROR:SAVED_PREDERROR,
	RETVAL
);

DIFFLIST(exp,lst):=IF LENGTH(lst)=0 THEN exp ELSE DIFFLIST(DIFF(exp,lst[1]),REST(lst));

A(L1,L2,[L3]):=IF MEMBER(5,L3) THEN 0 ELSE FUNMAKE('A,APPEND([L1,L2],L3));

G4(L1,L2,[L3]):=IF MEMBER(5,L3) THEN 0 ELSE FUNMAKE('G4,APPEND([L1,L2],L3));

G5(L1,L2,[L3]):=
	IF MEMBER(5,L3) THEN 0
	ELSE IF L1#[] THEN
	(
		IF NOT (PREDVAL(L1[1]<=4) AND PREDVAL(L1[2]<=4)) THEN FUNMAKE('G5,APPEND([L1,L2],L3))
		ELSE IF L1[1]<=4 AND L1[2]<=4 THEN APPLY('G4,APPEND([L1,L2],L3))+G55*DIFFLIST(A([L1[1]],[])*A([L1[2]],[]),L3)
		ELSE IF L1[1]<=4 THEN G55*APPLY('A,APPEND([[L1[1]],[]],L3))
		ELSE IF L1[2]<=4 THEN G55*APPLY('A,APPEND([[L1[2]],[]],L3))
		ELSE IF L3#[] THEN 0 ELSE G55
	)
	ELSE IF L2#[] THEN
	(
		IF NOT (PREDVAL(L2[1]<=4) AND PREDVAL(L2[2]<=4)) THEN FUNMAKE('G5,APPEND([L1,L2],L3))
		ELSE IF L2[1]<=4 AND L2[2]<=4 THEN APPLY('G4,APPEND([L1,L2],L3))
		ELSE IF L2[1]<=4 THEN -APPLY('A,APPEND([[],[L2[1]]],L3))
		ELSE IF L2[2]<=4 THEN -APPLY('A,APPEND([[],[L2[2]]],L3))
		ELSE IF L3#[] THEN SUM(DIFFLIST(A([I],[])*A([],[I]),L3),I,1,4)
		ELSE 1/G55+SUM(A([I],[])*A([],[I]),I,1,4)
	)
	ELSE FUNMAKE('G5,APPEND([L1,L2],L3));

MATCHDECLARE([DUMMY1,DUMMY2],TRUE);
DEFRULE(EVPOT,A([DUMMY1],[],DUMMY2)-A([DUMMY2],[],DUMMY1),-F([DUMMY1,DUMMY2],[]));
