
showtime:all$
/* This file finds the Schwarzschild solution of the
Einstein vacuum equations */
/* .mc version currently available in DOE MACSYMA */
load("ctensor")$
/* the following allows the batch program to run by presetting flags */
setflags()$
/* this calls for the rational simplification of geometrical objects */
ratfac:true$
/* the dimension of the manifold */
dim:4$
/* the coordinate labels */
ct_coords:[x,y,z,t]$
/* This is the standard spherically symmetric metric */
lg:matrix([a,0,0,0],[0,x^2,0,0],[0,0,x^2*sin(y)^2,0],[0,0,0,-d]);
/* functional dependencies */
depends([a,d],x);
/* computes inverse metric and specifies diagonality */
cmetric()$
/* computes the mixed Christoffel symbols */
christof(false)$
/* computes and ratsimps Ricci tensor */
uricci(false)$
/* computes and displays the Einstein tensor */
einstein(true);
/* makes a list of the non-zero components of the Einstein tensor (EIN)
where the 2 indicates the order of the array EIN */
exp:findde(ein,2);
/* now begins to solve the field equations */
ode2(last(exp),a,x);
/* a kludge to get the solution (the 1,1 component) explicitly */
solve(%,x);
resultlist:solve(%,a)$
h:ev(part(resultlist,1),eval);
/* to cast the solution into standard form */
h1:h,exp(%c) = 1/(2*m),factor;
/* now to find the 4,4 component */
ev(first(exp),h1,diff,factor);
ode2(num(%),d,x);
expand(radcan(%));
h2:ev(%,%c = 1);
/* H1 and H2 should be the solution and to check */
sol:[h1,h2];
exp,sol,diff,ratsimp;
