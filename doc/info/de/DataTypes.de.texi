@c -----------------------------------------------------------------------------
@c File        : DataTypes.de.texi
@c License     : GNU General Public License (GPL)
@c Language    : German
@c Original    : New file
@c Translation : Dr. Dieter Kaiser
@c Date        : 15.10.2010
@c Revision    : 13.02.2011
@c 
@c This file is part of Maxima -- GPL CAS based on DOE-MACSYMA
@c -----------------------------------------------------------------------------

@menu
* Einf@"uhrung in Maxima Zahlen::
* Zeichenketten::
* Funktionen und Variablen f@"ur Zahlen::
* Funktionen und Variablen f@"ur Zeichenketten::
* Funktionen und Variablen f@"ur Konstante::
* Funktionen und Variablen f@"ur Arrays::
* Einf@"uhrung in Listen::
* Funktionen und Variablen f@"ur Listen::
@end menu

@c -----------------------------------------------------------------------------
@node Einf@"uhrung in Maxima Zahlen, Zeichenketten, Datentypen und -strukturen, Datentypen und -strukturen
@section Einf@"uhrung in Maxima Zahlen
@c -----------------------------------------------------------------------------

@subheading Ganze und rationale Zahlen

Arithmetische Rechnungen mit ganzen oder rationalen Zahlen sind exakt.
Prinzipell k@"onnen die ganzen und rationalen Zahlen eine beliebige Anzahl an 
Stellen haben.  Eine Obergrenze ist allein der zur Verf@"ugung stehende
Speicherplatz.

@example
(%i1) 1/3+5/4+3;
                               55
(%o1)                          --
                               12
(%i2) 100!;
(%o2) 9332621544394415268169923885626670049071596826438162146859\
2963895217599993229915608941463976156518286253697920827223758251\
185210916864000000000000000000000000
(%i3) 100!/101!;
                                1
(%o3)                          ---
                               101
@end example

Funktionen f@"ur ganze und rationale Zahlen:

@verbatim
   integerp       numberp       nonnegintegerp     
   oddp           evenp
   ratnump        rationalize
@end verbatim

@subheading Gleitkommazahlen

Maxima rechnet mit Gleitkommazahlen in doppelter Genauigkeit.  Weiterhin kann
Maxima mit gro@ss{}en Gleitkommazahlen rechnen, die prinzipell eine beliebige
Genauigkeit haben.

Gleitkommazahlen werden mit einem Dezimalpunkt eingegeben.  Der Exponent kann 
mit "f", "e" oder "d" angegeben werden.  Intern rechnet Maxima
ausschlie@ss{}lich mit Gleitkommazahlen in doppelter Genauigkeit, die immer mit
"e" f@"ur den Exponenten angezeigt werden.  Gro@ss{}e Gleitkommazahlen werden
mit dem Exponenten "b" bezeichnet.  Gro@ss{}- und Kleinschreibung werden bei der
Schreibweise des Exponenten nicht unterschieden.

@example
(%i1) [2.0,1f10,1,e10,1d10,1d300];
(%o1)        [2.0, 1.e+10, 1, e10, 1.e+10, 1.e+300]
(%i2) [2.0b0,1b10,1b300];
(%o2)               [2.0b0, 1.0b10, 1.0b300]
@end example

@noindent
Ist mindestens eine Zahl in einer Rechnung eine Gleitkommazahl, werden die 
Argumente in Gleitkommazahlen umgewandelt und eine Gleitkommazahl als Ergebnis
zur@"uckgegeben.  Dies wird auch f@"ur gro@ss{}e Gleitkommazahlen ausgef@"uhrt.

@example
(%i1) 2.0+1/2+3;
(%o1)                                 5.5
(%i2) 2.0b0+1/2+3;
(%o2)                                5.5b0
@end example

@noindent
Mit den Funktionen @mref{float} und @mref{bfloat} werden Zahlen in
Gleitkommazahlen und gro@ss{}e Gleitkommazahlen umgewandelt:

@example
(%i1) float([2,1/2,1/3,2.0b0]);
(%o1)          [2.0, 0.5, .3333333333333333, 2.0]
(%i2) bfloat([2,1/2,1/3,2.0b0]);
(%o2)     [2.0b0, 5.0b-1, 3.333333333333333b-1, 2.0b0]
@end example

@noindent
Funktionen und Variablen f@"ur Gleitkommazahlen:

@verbatim
   float        floatnump     
   bfloat       bfloatp       fpprec
   float2bf     bftorat       ratepsilon

   number_pbranch 
   m1pbranch
@end verbatim

@subheading Komplexe Zahlen

@c A complex expression is specified in Maxima by adding the real part of the
@c expression to @code{%i} times the imaginary part.  Thus the roots of the 
@c equation @code{x^2 - 4*x + 13 = 0} are @code{2 + 3*%i} and @code{2 - 3*%i}.
@c Note that simplification of products of complex expressions can be effected 
@c by expanding the product.  Simplification of quotients, roots, and other
@c functions of complex expressions can usually be accomplished by using the 
@c @code{realpart}, @code{imagpart}, @code{rectform}, @code{polarform}, 
@c @code{abs}, @code{carg} functions.

Maxima kennt keinen eigenen Typ f@"ur komplexe Zahlen.  Komplexe Zahlen werden 
von Maxima intern als die Addition von Realteil und dem mit der Imagin@"aren 
Einheit @mref{%i} multiplizierten Imagin@"arteil dargestellt.  Zum Beispiel sind
die komplexen Zahlen @code{2 + 3*%i} und @code{2 - 3*%i} die Wurzeln der 
Gleichung @code{x^2 - 4*x + 13 = 0}.

Maxima vereinfacht Produkte, Quotienten, Wurzeln und andere Ausdr@"ucke mit 
komplexen Zahlen nicht automatisch zu einer komplexen Zahl.  Um Produkte mit
komplexen Zahlen zu vereinfachen, kann der Ausdruck mit der Funktion 
@mref{expand} expandiert werden.

Funktionen f@"ur komplexe Zahlen:

@verbatim
   realpart     imagpart      rectform     polarform
   cabs         carg          conjugate    csign
@end verbatim

@c -----------------------------------------------------------------------------
@node Zeichenketten, Funktionen und Variablen f@"ur Zahlen, Einf@"uhrung in Maxima Zahlen, Datentypen und -strukturen
@section Zeichenketten
@c -----------------------------------------------------------------------------

@c Strings (quoted character sequences) are enclosed in double quote marks 
@c @code{"} for input, and displayed with or without the quote marks, depending
@c on the global variable @code{stringdisp}.

Zeichenketten werden bei der Eingabe in Anf@"uhrungszeichen gesetzt.  Sie werden 
standardm@"a@ss{}ig ohne Anf@"uhrungszeichen ausgegeben.  Hat die 
Optionsvariable @mref{stringdisp} den Wert @code{true}, werden Zeichenketten mit
Anf@"uhrungszeichen dargestellt.

@c Strings may contain any characters, including embedded tab, newline, and 
@c carriage return characters. The sequence @code{\"} is recognized as a literal
@c double quote, and @code{\\} as a literal backslash. When backslash appears at
@c the end of a line, the backslash and the line termination (either newline or
@c carriage return and newline) are ignored, so that the string continues with 
@c the next line. No other special combinations of backslash with another 
@c character are recognized; when backslash appears before any character other 
@c than @code{"}, @code{\}, or a line termination, the backslash is ignored.
@c There is no way to represent a special character (such as tab, newline, or
@c carriage return) except by embedding the literal character in the string.

Zeichenketten k@"onnen jedes Zeichen einschlie@ss{}lich Tabulator-, 
Zeilenvorschub- oder Wagenr@"ucklauf-Zeichen enthalten.  Das Anf@"uhrungszeichen 
wird innerhalb einer Zeichenkette durch @code{\"} und der Backslash durch 
@code{\\} dargestellt.  Ein Backslash am Ende einer Eingabezeile erlaubt die
Fortsetzung einer Zeichenkette in der n@"achsten Zeile.  Maxima kennt keine 
weiteren Kombinationen mit einem Backslash.  Daher wird der Backslash an anderer
Stelle ignoriert.  Maxima kennt keine andere M@"oglichkeit, als spezielle
Zeichen wie ein Tabulator-, Zeilenvorschub- oder Wagenr@"ucklaufzeichen in einer
Zeichenkette darzustellen.

@c There is no character type in Maxima; a single character is represented as a 
@c one-character string.
@c The @code{stringproc} add-on package contains many functions for working with
@c strings.

Maxima hat keinen Typ f@"ur ein einzelnes Zeichen.  Einzelne Zeichen werden daher
als eine Zeichenkette mit einem Zeichen dargestellt.  Folgende Funktionen und 
Variablen arbeiten mit Zeichenketten:

@verbatim
   concat   sconcat   string   stringdisp
@end verbatim

@noindent
Das Zusatzpaket @nref{stringproc} enth@"alt eine umfangreiche Bibliothek an 
Funktionen f@"ur Zeichenketten.

Beispiele:

@c ===beg===
@c s_1 : "This is a string.";
@c s_2 : "Embedded \"double quotes\" and backslash \\ characters.";
@c s_3 : "Embedded line termination
@c in this string.";
@c s_4 : "Ignore the \
@c line termination \
@c characters in \
@c this string.";
@c stringdisp : false;
@c s_1;
@c stringdisp : true;
@c s_1;
@c ===end===
@example
(%i1) s_1 : "This is a string.";
(%o1)               This is a string.
(%i2) s_2 : "Embedded \"double quotes\" and backslash \\ characters.";
(%o2) Embedded "double quotes" and backslash \ characters.
(%i3) s_3 : "Embedded line termination
in this string.";
(%o3) Embedded line termination
in this string.
(%i4) s_4 : "Ignore the \
line termination \
characters in \
this string.";
(%o4) Ignore the line termination characters in this string.
(%i5) stringdisp : false;
(%o5)                         false
(%i6) s_1;
(%o6)                   This is a string.
(%i7) stringdisp : true;
(%o7)                         true
(%i8) s_1;
(%o8)                  "This is a string."
@end example

@c -----------------------------------------------------------------------------
@node Funktionen und Variablen f@"ur Zahlen, Funktionen und Variablen f@"ur Zeichenketten, Zeichenketten, Datentypen und -strukturen
@section Funktionen und Variablen f@"ur Zahlen
@c -----------------------------------------------------------------------------

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{bfloat}
@deffn {Funktion} bfloat (@var{expr})

@c Converts all numbers and functions of numbers in @var{expr} to bigfloat 
@c numbers. The number of significant digits in the resulting bigfloats is 
@c specified by the global variable @code{fpprec}.

Konvertiert alle Zahlen im Ausdruck @var{expr} in gro@ss{}e Gleitkommazahlen.
Die Anzahl der Stellen wird durch die Optionsvariable 
@mref{fpprec} spezifiziert.

@c When @code{float2bf} is @code{false} a warning message is printed when a 
@c floating point number is converted into a bigfloat number (since this may 
@c lead to loss of precision).

Hat die Optionsvariable @mref{float2bf} den Wert @code{false}, gibt Maxima eine
Warnung aus, wenn eine Gleitkommazahl mit doppelter Genauigkeit in eine 
gro@ss{}e Gleitkommazahl umgewandelt wird.
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{bfloatp}
@deffn {Funktion} bfloatp (@var{expr})

@c Returns @code{true} if @var{expr} is a bigfloat number, otherwise 
@c @code{false}.

Gibt den Wert @code{true} zur@"uck, wenn @var{expr} eine gro@ss{}e 
Gleitkommazahl ist, ansonsten den Wert @code{false}.
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{bftorat}
@defvr {Optionsvariable} bftorat
Standardwert: @code{false}

@c @code{bftorat} controls the conversion of bfloats to rational numbers. When 
@c @code{bftorat} is @code{false}, @code{ratepsilon} will be used to control the
@c conversion (this results in relatively small rational numbers). When 
@c @code{bftorat} is @code{true}, the rational number generated will accurately
@c represent the bfloat.

Die Optionsvariable @code{bftorat} kontrolliert die Umwandlung von gro@ss{}en
Gleitkommazahlen in rationale Zahlen.  Hat @code{bftorat} den Wert @code{true},
wird die Genauigkeit der Umwandlung von der Optionsvariablen @mref{ratepsilon}@ 
kontrolliert.  In diesem Fall ist das Ergebnis der Umwandlung eine relativ kleine
rationale Zahl.  Hat dagegen @code{bftorat} den Wert @code{false}, wird die 
gro@ss{}e Gleitkommazahl exakt durch die rationale Zahl repr@"asentiert.
@end defvr

@c --- 15.10.2010 --------------------------------------------------------------
@anchor{bftrunc}
@defvr {Optionsvariable} bftrunc
Standardwert: @code{true}

@c @code{bftrunc} causes trailing zeroes in non-zero bigfloat numbers not to be 
@c displayed.  Thus, if @code{bftrunc} is @code{false}, @code{bfloat (1)} 
@c displays as @code{1.000000000000000B0}. Otherwise, this is displayed as
@c @code{1.0B0}.

Hat die Optionsvariable @code{bftrunc} den Wert @code{true}, werden bei der
Ausgabe einer gro@ss{}en Gleitkommazahl nachfolgende Nullen nicht angezeigt.  So 
wird @code{bfloat(1)} als @code{1.000000000000000b0} angezeigt, wenn 
@code{bftrunc} den Wert @code{false} hat, ansonsten wird @code{1.0b0} angezeigt.
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{evenp}
@deffn {Funktion} evenp (@var{expr})

@c Returns @code{true} if @var{expr} is an even integer.
@c @c THIS IS STRANGE -- SHOULD RETURN NOUN FORM IF INDETERMINATE
@c @code{false} is returned in all other cases.

Ist @var{expr} eine gerade ganze Zahl, wird @code{true} zur@"uckgegeben.  In 
allen anderen F@"allen wird @code{false} zur@"uckgegeben.

@code{evenp} gibt f@"ur Symbole oder Ausdr@"ucke immer den Wert @code{false} 
zur@"uck, auch wenn das Symbol als gerade ganze Zahl deklariert ist oder der 
Ausdruck eine gerade ganze Zahl repr@"asentiert.

Siehe die Funktion @mrefcomma{featurep} um zu testen, ob ein Symbol oder 
Ausdruck eine gerade ganze Zahl repr@"asentiert.
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{float}
@deffn  {Funktion} float (@var{expr})
@deffnx {Auswertungsschalter} float

@c Converts integers, rational numbers and bigfloats in @var{expr} to floating 
@c point numbers.  It is also an @code{evflag}, @code{float} causes non-integral
@c rational numbers and bigfloat numbers to be converted to floating point.

Konvertiert ganze, rationale und gro@ss{}e Gleitkommazahlen im Ausdruck 
@var{expr} in Gleitkommazahlen mit doppelter Genauigkeit.

@code{float} ist auch ein Auswertungsschalter f@"ur die Funktion @mrefdot{ev}@ 
In diesem Fall werden rationale Zahlen und gro@ss{}e Gleitkommazahlen, aber 
keine ganzen Zahlen in Gleitkommazahlen umgewandelt.  Daher haben die beiden
Kommandos @code{ev(expr),float} und @code{float(expr)} ein unterschiedliches
Ergebnis.

Beispiele:

In den ersten zwei Beispielen werden die Zahlen @code{1/2} und @code{1} in eine
Gleitkommazahl umgewandelt.  Die Sinusfunktion vereinfacht sodann zu einem
numerischen Wert.  Das Auswertungsflag @code{float} wandelt ganze Zahlen nicht
in eine Gleitkommazahl um.  Daher wird @code{sin(1)} nicht zu einem numerischen
Wert vereinfacht.

@c === beg ===
@c float(sin(1/2));
@c float(sin(1));
@c sin(1/2),float;
@c sin(1),float;
@c === end ===
@example
(%i1) float(sin(1/2));
(%o1)                   0.479425538604203
(%i2) float(sin(1));
(%o2)                   .8414709848078965
(%i3) sin(1/2),float;
(%o3)                   0.479425538604203
(%i4) sin(1),float;
(%o4)                        sin(1)
@end example
@end deffn

@c --- 08.10.2010 DK -----------------------------------------------------------
@anchor{float2bf}
@defvr {Optionsvariable} float2bf
Standardwert: @code{true}
 
@c When @code{float2bf} is @code{false}, a warning message is printed when a 
@c floating point number is converted into a bigfloat number (since this may 
@c lead to loss of precision).

Hat die Optionsvariable @code{float2bf} den Wert @code{false}, wird eine
Warnung ausgegeben, wenn eine Gleitkommazahl in eine gro@ss{}e Gleitkommazahl 
umgewandelt wird, da die Umwandlung zu einem Verlust an Genauigkeit f@"uhren 
kann.
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{floatnump}
@deffn {Funktion} floatnump (@var{number})

@c Returns @code{true} if @var{expr} is a floating point number, otherwise 
@c @code{false}.

Gibt den Wert @code{true} zur@"uck, wenn das Argument @var{number} eine 
Gleitkommazahl ist.  Ansonsten wird @code{false} zur@"uckgegeben.  Auch wenn 
@var{number} eine gro@ss{}e Gleitkommazahl ist, ist das Ergebnis @code{false}.
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{fpprec}
@defvr {Optionsvariable} fpprec
Standardwert: 16

@c @code{fpprec} is the number of significant digits for arithmetic on bigfloat
@c numbers. @code{fpprec} does not affect computations on ordinary floating 
@c point numbers.
@c See also @code{bfloat} and @code{fpprintprec}.

@code{ffprec} ist die Zahl der Stellen f@"ur das Rechnen mit gro@ss{}en 
Gleitkommazahlen.  @code{ffprec} hat keinen Einflu@ss{} auf das Rechnen mit
Gleitkommazahlen in doppelter Genauigkeit.  Siehe auch @mref{bfloat} und 
@mrefdot{fpprintprec}
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{fpprintprec}
@defvr {Optionsvariable} fpprintprec
Standardwert: 0

@c @code{fpprintprec} is the number of digits to print when printing an ordinary
@c float or bigfloat number.

@code{fpprintprec} ist die Anzahl der Stellen, die angezeigt wird, wenn eine
Gleitkommazahl ausgegeben wird.

@c For ordinary floating point numbers, when @code{fpprintprec} has a value 
@c between 2 and 16 (inclusive), the number of digits printed is equal to 
@c @code{fpprintprec}. Otherwise, @code{fpprintprec} is 0, or greater than 16,
@c and the number of digits printed is 16.

Hat @code{fpprintprec} einen Wert zwischen 2 und 16, ist die Anzahl der 
angezeigten Stellen f@"ur einfache Gleitkommazahlen gleich dem Wert von 
@code{fpprintprec}.  Hat @code{fpprintprec} den Wert 0 oder ist gr@"o@ss{}er als
16 werden 16 Stellen angezeigt.

@c For bigfloat numbers, when @code{fpprintprec} has a value between 2 and 
@c @code{fpprec} (inclusive), the number of digits printed is equal to 
@c @code{fpprintprec}. Otherwise, @code{fpprintprec} is 0, or greater than 
@c @code{fpprec}, and the number of digits printed is equal to @code{fpprec}.

Hat f@"ur gro@ss{}e Gleitkommazahlen @code{fpprintprec} einen Wert zwischen 2 
und @mrefcomma{fpprec} ist die Anzahl der angezeigten Stellen gleich
@code{fpprintprec}.  Ist der Wert von @code{fpprintprec} gleich 0 oder 
gr@"o@ss{}er als @code{fpprec} werden @code{fpprec} Stellen angezeigt.

@c @code{fpprintprec} cannot be 1.

@code{fpprintprec} kann nicht den Wert 1 erhalten.
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{integerp}
@deffn {Funktion} integerp (@var{number})

@c Returns @code{true} if @var{expr} is a literal numeric integer, otherwise 
@c @code{false}.

Hat den R@"uckgabewert @code{true}, wenn das Argument @var{number} eine ganze 
Zahl ist.  In allen anderen F@"allen gibt @code{integerp} den Wert @code{false} 
zur@"uck.

@c @code{integerp} returns @code{false} if its argument is a symbol, even if the
@c argument is declared integer.

@code{integerp} gibt f@"ur Symbole oder Ausdr@"ucke immer den Wert @code{false} 
zur@"uck, auch wenn das Symbol als ganze Zahl deklariert ist oder der Ausdruck 
eine ganze Zahl repr@"asentiert.  Siehe die Funktion @mrefcomma{featurep} um zu
testen, ob ein Symbol oder Ausdruck eine ganze Zahl repr@"asentiert.

Beispiele:

@c === beg ===
@c ingegerp(1);
@c integerp(1.0);
@c integerp(%pi);
@c declare(n, integer)$
@c integerp(n);
@c === end ===
@example
(%i1) integerp (1);
(%o1)                         true
(%i2) integerp (1.0);
(%o2)                         false
(%i3) integerp (%pi);
(%o3)                         false
(%i4) declare (n, integer)$
(%i5) integerp (n);
(%o5)                         false
@end example
@end deffn

@c --- 08.10.2010 --------------------------------------------------------------
@anchor{m1pbranch}
@defvr {Optionsvariable} m1pbranch
Standardwert: @code{false}

@c @code{m1pbranch} is the principal branch for @code{-1} to a power. Quantities
@c such as @code{(-1)^(1/3)} (that is, an "odd" rational exponent) and 
@c @code{(-1)^(1/4)} (that is, an "even" rational exponent) are handled as 
@c follows:

Die Optionsvariable @code{m1branch} kontrolliert die Vereinfachung der 
Exponentiation von @code{-1} f@"ur den Fall, dass die Optionsvariable 
@mref{domain} den Wert @code{complex} hat.  Hat @code{m1pbranch} f@"ur diesen 
Fall den Wert @code{true}, wird die Exponentiation von @code{-1} zu einem 
Ausdruck vereinfacht, der dem Hauptwert entspricht.  Die Auswirkung der 
Optionsvariable @code{m1pbranch} ist in der folgenden Tabelle gezeigt.

@example
              domain:real
                            
(-1)^(1/3):      -1         
(-1)^(1/4):   (-1)^(1/4)   

             domain:complex              
m1pbranch:false          m1pbranch:true
(-1)^(1/3)               1/2+%i*sqrt(3)/2
(-1)^(1/4)              sqrt(2)/2+%i*sqrt(2)/2
@end example

Siehe auch die Optionsvariable @mrefdot{number_pbranch}
@end defvr

@c --- 08.10.2010 DK -----------------------------------------------------------
@anchor{nonnegintegerp}
@deffn {Funktion} nonnegintegerp (@var{number})

@c Return @code{true} if and only if @code{@var{n} >= 0} and @var{n} is an 
@c integer.

Gibt den Wert @code{true} zur@"uck, wenn @code{number} eine ganze positive Zahl
oder Null ist.  Siehe auch @mrefdot{integerp}
@end deffn

@c --- 21.08.2010 --------------------------------------------------------------
@anchor{numberp}
@deffn {Funktion} numberp (@var{expr})

@c Returns @code{true} if @var{expr} is a literal integer, rational number, 
@c floating point number, or bigfloat, otherwise @code{false}.

Hat das Ergebnis @code{true}, wenn @var{expr} eine ganze, rationale, eine
Gleitkommazahl oder eine gro@ss{}e Gleitkommazahl ist.  Ansonsten ist das 
Ergebnis @code{false}.

@c @code{numberp} returns @code{false} if its argument is a symbol, even if the
@c argument is a symbolic number such as @code{%pi} or @code{%i}, or declared 
@c to be @code{even}, @code{odd}, @code{integer}, @code{rational}, 
@c @code{irrational}, @code{real}, @code{imaginary}, or @code{complex}.

@code{numberp} gibt f@"ur ein Symbol immer das Ergebnis @code{false} zur@"uck.
Dies ist auch dann der Fall, wenn das Symbol eine numerische Konstante wie 
@mref{%pi} ist oder wenn das Symbol mit der Funktion @mref{declare} eine 
Eigenschaft wie @mrefcomma{integer} @mref{real} oder @mref{complex} erhalten
hat.

Beispiele:

@c ===beg===
@c numberp (42);
@c numberp (-13/19);
@c numberp (3.14159);
@c numberp (-1729b-4);
@c map (numberp, [%e, %pi, %i, %phi, inf, minf]);
@c declare(a,even,b,odd,c,integer,d,rational,e,irrational,f,real);
@c map (numberp, [a, b, c, d, e, f]);
@c ===end===
@example
(%i1) numberp (42);
(%o1)                         true
(%i2) numberp (-13/19);
(%o2)                         true
(%i3) numberp (3.14159);
(%o3)                         true
(%i4) numberp (-1729b-4);
(%o4)                         true
(%i5) map (numberp, [%e, %pi, %i, %phi, inf, minf]);
(%o5)      [false, false, false, false, false, false]
(%i6) declare(a,even,b,odd,c,integer,d,rational,e,real);
(%o6)                         done
(%i7) map (numberp, [a, b, c, d, e]);
(%o7) [false, false, false, false, false]
@end example
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{number_pbranch}
@defvr {Optionsvariable} numer_pbranch
Standardwert: @code{false}

@c The option variable @code{numer_pbranch} controls the numerical evaluation of 
@c the power of a negative integer, rational, or floating point number. When
@c @code{numer_pbranch} is @code{true} and the exponent is a floating point 
@c number or the option variable @code{numer} is @code{true} too, Maxima 
@c evaluates the numerical result using the principal branch. Otherwise a
@c simplified, but not an evaluated result is returned.

Die Optionsvariable @code{numer_pbranch} kontrolliert die Vereinfachung der
Exponentiation einer negativen ganzen, rationalen oder Gleitkommazahl.
Hat @code{numer_pbranch} den Wert @code{true} und ist der Exponent eine 
Gleitkommazahl oder hat die Optionsvariable @mref{numer} den Wert @code{true}, 
dann berechnet Maxima den Hauptwert der Exponentiation.  Ansonsten wird ein 
vereinfachter Ausdruck, aber nicht numerischer Wert zur@"uckgegeben.  Siehe auch
die Optionsvariable @mrefdot{m1pbranch}

Beispiele:

@c ===beg===
@c (-2)^0.75;
@c (-2)^0.75,numer_pbranch:true;
@c (-2)^(3/4);
@c (-2)^(3/4),numer;
@c (-2)^(3/4),numer,numer_pbranch:true;
@c ===end===
@example
(%i1) (-2)^0.75;
(%o1) (-2)^0.75

(%i2) (-2)^0.75,numer_pbranch:true;
(%o2) 1.189207115002721*%i-1.189207115002721

(%i3) (-2)^(3/4);
(%o3) (-1)^(3/4)*2^(3/4)

(%i4) (-2)^(3/4),numer;
(%o4) 1.681792830507429*(-1)^0.75

(%i5) (-2)^(3/4),numer,numer_pbranch:true;
(%o5) 1.189207115002721*%i-1.189207115002721
@end example
@end defvr

@c --- 08.10.2010 DK -----------------------------------------------------------
@anchor{oddp}
@deffn {Funktion} oddp (@var{expr})

@c is @code{true} if @var{expr} is an odd integer.
@c @c THIS IS STRANGE -- SHOULD RETURN NOUN FORM IF INDETERMINATE
@c @code{false} is returned in all other cases.

Gibt @code{true} zur@"uck, wenn @var{expr} eine ungerade ganze Zahl ist.  In 
allen anderen F@"allen wird @code{false} zur@"uckgegeben.

@code{oddp} gibt f@"ur Symbole oder Ausdr@"ucke immer den Wert @code{false} 
zur@"uck, auch wenn das Symbol als ungerade ganze Zahl deklariert ist oder der 
Ausdruck eine ungerade ganze Zahl repr@"asentiert.  Siehe die Funktion 
@mrefcomma{featurep} um zu testen, ob ein Symbol oder Ausdruck eine ungerade 
ganze Zahl repr@"asentiert.
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{ratepsilon}
@defvr {Optionsvariable} ratepsilon
Standardwert: 2.0e-8

@c @code{ratepsilon} is the tolerance used in the conversion of floating point
@c numbers to rational numbers.

@code{ratepsilon} kontrolliert die Genauigkeit, mit der Gleitkommazahlen in
rationale Zahlen umgewandelt werden.  Siehe auch @mrefdot{bftorat}
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{rationalize}
@deffn {Funktion} rationalize (@var{expr})

@c Convert all double floats and big floats in the Maxima expression @var{expr} 
@c to their exact rational equivalents.

Konvertiert alle Gleitkommazahlen einschlie@ss{}lich gro@ss{}er 
Gleitkommazahlen, die in dem Ausdruck @var{expr} auftreten, in rationale Zahlen.

@c If you are not familiar with the binary representation of floating point 
@c numbers, you might be surprised that @code{rationalize (0.1)} does not equal 
@c 1/10.  This behavior isn't special to Maxima -- the number 1/10 has a 
@c repeating, not a terminating, binary representation.

Es mag @"uberraschend sein, dass @code{rationalize(0.1)} nicht das Ergebnis
@code{1/10} hat.  Dies ist nicht speziell f@"ur Maxima.  Ursache ist, dass die 
gebrochene Zahl @code{1/10} in der internen Darstellung als bin@"are Zahl keine
endliche Darstellung hat.

Siehe auch die Funktion @mref{float} und den Auswertungsschalter 
@mrefcomma{numer} um eine rationale Zahl in eine Gleitkommazahl umzuwandeln.

Beispiele:

@c ===beg===
@c rationalize (0.5);
@c rationalize (0.1);
@c fpprec : 5$
@c rationalize (0.1b0);
@c fpprec : 20$
@c rationalize (0.1b0);
@c rationalize (sin (0.1*x + 5.6));
@c float(%);
@c ===end===
@example
(%i1) rationalize (0.5);
                                1
(%o1)                           -
                                2
(%i2) rationalize (0.1);
                        3602879701896397
(%o2)                   -----------------
                        36028797018963968
(%i3) fpprec : 5$
(%i4) rationalize (0.1b0);
                             209715
(%o4)                        -------
                             2097152
(%i5) fpprec : 20$
@group
(%i6) rationalize (0.1b0);
                     236118324143482260685
(%o6)                ----------------------
                     2361183241434822606848
@end group
(%i7) rationalize (sin (0.1*x + 5.6));
               3602879701896397 x   3152519739159347
(%o7)      sin(------------------ + ----------------)
               36028797018963968    562949953421312

(%i8) float(%);
(%o8)                          sin(0.1 x + 5.6)
@end example
@end deffn

@c --- 08.10.2010 DK -----------------------------------------------------------
@anchor{ratnump}
@deffn {Funktion} ratnump (@var{number})

@c Returns @code{true} if @var{expr} is a literal integer or ratio of literal
@c integers, otherwise @code{false}.

Gibt @code{true} zur@"uck, wenn @var{number} eine ganze oder rationale Zahl ist.
In allen anderen F@"allen ist das Ergebnis @code{false}.

Beispiele:

@c === beg ===
@c ratnump(1/2);
@c ratnump(3);
@c ratnump(3.0);
@c === end ===
@example
(%i1) ratnump(1/2);
(%o1)                         true
(%i2) ratnump(3);
(%o2)                         true
(%i3) ratnump(3.0);
(%o3)                         false
@end example
@end deffn

@c -----------------------------------------------------------------------------
@node Funktionen und Variablen f@"ur Zeichenketten, Funktionen und Variablen f@"ur Konstante, Funktionen und Variablen f@"ur Zahlen, Datentypen und -strukturen
@section Funktionen und Variablen f@"ur Zeichenketten
@c -----------------------------------------------------------------------------

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{concat}
@deffn {Funktion} concat (@var{arg_1}, @var{arg_2}, @dots{})

@c Concatenates its arguments. The arguments must evaluate to atoms. The return 
@c value is a symbol if the first argument is a symbol and a string otherwise.

Verkettet die Argumente @var{arg_1}, @var{arg_2}, @dots{} zu einer Zeichenkette 
oder einem Symbol.  Die Argumente m@"ussen sich zu einem Atom auswerten lassen.
Der R@"uckgabewert ist ein Symbol, wenn das erste Argument ein Symbol ist.
Ansonsten wird eine Zeichenkette zur@"uckgegeben.

@c @code{concat} evaluates its arguments. The single quote @code{'} prevents 
@c evaluation.

@code{concat} wertet die Argumente aus.  Der Quote-Operator verhindert die
Auswertung.  Siehe auch die Funktion @mrefdot{sconcat}

Beispiele:

@c === beg ===
@c y: 7$
@c z: 88$
@c stringdisp:true$
@c concat(y, z/2);
@c concat('y, z/2);
@c === end ===
@example
(%i1) y: 7$
(%i2) z: 88$
(%i3) stringdisp:true$
(%i4) concat(y, z/2);
(%o4)                         "744"
(%i5) concat('y, z/2);
(%o5)                          y44
@end example

@c A symbol constructed by @code{concat} may be assigned a value and appear in 
@c expressions. The @code{::} (double colon) assignment operator evaluates its 
@c left-hand side.

Einem Symbol, das mit @code{concat} konstruiert wird, kann ein Wert zugewiesen
werden und es kann in Ausdr@"ucken auftreten.

@c === beg ===
@c a: concat ('y, z/2);
@c a:: 123;
@c y44;
@c b^a;
@c %, numer;
@c === end ===
@example
(%i6) a: concat ('y, z/2);
(%o6)                          y44
@group
(%i7) a:: 123;
(%o7)                          123
@end group
(%i8) y44;
(%o8)                          123
(%i9) b^a;
                               y44
(%o9)                         b
(%i10) %, numer;
                                123
(%o11)                         b
@end example

@c Note that although @code{concat (1, 2)} looks like a number, it is a string.

@code{concat(1, 2)} gibt eine Zeichenkette als Ergebnis zur@"uck.

@c === beg ===
@c stringdisp:true$
@c concat(1, 2) + 3;
@c === end ===
@example
(%i12) concat (1, 2) + 3;
(%o12)                       "12" + 3
@end example
@end deffn

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{sconcat}
@deffn {Funktion} sconcat (@var{arg_1}, @var{arg_2}, @dots{})

@c Concatenates its arguments into a string. Unlike @code{concat}, the arguments
@c do @i{not} need to be atoms.

Verkettet die Argumente @var{arg_1}, @var{arg_2}, @dots{} zu einer Zeichenkette.
Im Unterschied zu der Funktion @mref{concat} m@"ussen die Argumente @i{nicht} 
Atome sein.  Der R@"uckgabewert ist eine Zeichenkette.

Beispiel:

@c === beg ===
@c sconcat ("xx[", 3, "]:", expand ((x+y)^3));
@c === end ===
@example
(%i1) sconcat ("xx[", 3, "]:", expand ((x+y)^3));
(%o1)               xx[3]:y^3+3*x*y^2+3*x^2*y+x^3
@end example
@end deffn

@c --- 08.10.2010 DK -----------------------------------------------------------
@anchor{string}
@deffn {Funktion} string (@var{expr})

@c Converts @code{expr} to Maxima's linear notation just as if it had been typed
@c in.
@c The return value of @code{string} is a string, and thus it cannot be used in
@c a computation.

Konvertiert @code{expr} in eine linearer Darstellung, wie sie auch vom Parser
von der Eingabe eingelesen wird.  Die R@"uckgabe von @code{string} ist eine
Zeichenkette.  Diese kann nicht f@"ur eine Berechnung genutzt werden.
@end deffn

@c TODO: DIESE FUNKTION IN DAS KAPITEL KOMMANDOZEILE VERSCHIEBEN ???

@c --- 28.08.2010 DK -----------------------------------------------------------
@anchor{stringdisp}
@defvr {Optionsvariable} stringdisp
Standardwert: @code{false}

@c When @code{stringdisp} is @code{true}, strings are displayed enclosed in 
@c double quote marks. Otherwise, quote marks are not displayed.

Hat @code{stringdisp} den Wert @code{true}, werden Zeichenketten mit
Anf@"uhrungszeichen ausgegeben.  Ansonsten werden keine Anf@"uhrungszeichen
ausgegeben.

@c @code{stringdisp} is always @code{true} when displaying a function 
@c definition.

Wird die Definition einer Funktion ausgegeben, werden enthaltene Zeichenketten
unabh@"angig vom Wert der Optionsvariablen @code{stringdisp} immer in 
Anf@"uhrungszeichen ausgegeben.

Beispiele:

@c ===beg===
@c stringdisp: false$
@c "This is an example string.";
@c foo () := 
@c       print ("This is a string in a function definition.");
@c stringdisp: true$
@c "This is an example string.";
@c ===end===
@example
(%i1) stringdisp: false$
(%i2) "This is an example string.";
(%o2)              This is an example string.
(%i3) foo () :=
      print ("This is a string in a function definition.");
(%o3) foo() := 
              print("This is a string in a function definition.")
(%i4) stringdisp: true$
(%i5) "This is an example string.";
(%o5)             "This is an example string."
@end example
@end defvr

@c -----------------------------------------------------------------------------
@node Funktionen und Variablen f@"ur Konstante,  Funktionen und Variablen f@"ur Arrays, Funktionen und Variablen f@"ur Zeichenketten, Datentypen und -strukturen
@section Funktionen und Variablen f@"ur Konstante
@c -----------------------------------------------------------------------------

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{%e}
@defvr {Konstante} %e
@ifinfo
@vrindex e
@vrindex Eulersche Zahl
@vrindex Basis des nat@"urlichen Logarithmus
@end ifinfo

@c @code{%e} represents the base of the natural logarithm, also known as 
@c Euler's number. The numeric value of @code{%e} is the double-precision 
@c floating-point value 2.718281828459045d0.

@code{%e} ist die Basis des nat@"urlichen Logarithmus, auch Eulersche Zahl 
genannt.  Der numerische Wert der Konstanten als Gleitkommazahl mit doppelter
Genauigkeit ist 2.718281828459045d0.

Die Funktion @mref{bfloat} kann @code{%e} mit einer beliebigen Genauigkeit
berechnen.

Beispiel:

@c === beg ===
@c fpprec:48$
@c bfloat(%e);
@c === end ===
@example
(%i1) fpprec:48$
(%i2) bfloat(%e);
(%o2)  2.7182818284590452353602874713526624977572470937b0
@end example
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{%i}
@defvr {Konstante} %i
@ifinfo
@vrindex i
@vrindex Imagin@"are Einheit
@end ifinfo

@c @code{%i} represents the imaginary unit, @math{sqrt(- 1)}.

@code{%i} ist die imagin@"are Einheit.
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{false}
@defvr {Konstante} false

@c @code{false} represents the Boolean constant of the same name. Maxima 
@c implements @code{false} by the value @code{NIL} in Lisp.

Repr@"asentiert den logischen Wert @code{falsch}.  @code{false} wird intern von 
Maxima durch die Lisp-Konstante @code{NIL} dargestellt.
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{%gamma}
@defvr {Konstante} %gamma
@ifinfo
@vrindex Euler-Mascheroni-Konstante
@end ifinfo

@c The Euler-Mascheroni constant, 0.5772156649015329 ....
@c @c DOUBTLESS THERE IS MORE TO SAY HERE.

Die Euler-Mascheroni-Konstante mit dem Wert @code{0.5772156649015329} als
Gleitkommazahl in doppelter Genauigkeit.

Die Funktion @mref{bfloat} kann @code{%gamma} mit einer beliebigen Genauigkeit
berechnen.
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{ind}
@defvr {Konstante} ind
@ifinfo
@vrindex Unbestimmt
@end ifinfo

@c @code{ind} represents a bounded, indefinite result.

@code{ind} repr@"asentiert ein unbestimmtes Ergebnis.  Siehe auch 
@mrefdot{limit}

Beispiel:

@c ===beg===
@c limit (sin(1/x), x, 0);
@c ===end===
@example
(%i1) limit (sin(1/x), x, 0);
(%o1)                          ind
@end example
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{inf}
@defvr {Konstante} inf
@ifinfo
@vrindex positiv unendlich
@end ifinfo

@c @code{inf} represents real positive infinity.

@code{inf} repr@"asentiert einen positiven unendlich gro@ss{}en Wert.
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{infinity}
@defvr {Konstante} infinity
@ifinfo
@vrindex komplex unendlich
@end ifinfo

@c @code{infinity} represents complex infinity.

@code{infinity} repr@"asentiert einen komplexen unendlichen Wert.
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{minf}
@defvr {Konstante} minf
@ifinfo
@vrindex negativ unendlich
@vrindex minus unendlich
@end ifinfo

@c @code{minf} represents real minus (i.e., negative) infinity.

@code{minf} repr@"asentiert einen negativen unendlichen Wert.
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{%phi}
@defvr {Konstante} %phi
@ifinfo
@vrindex phi
@vrindex Goldene Zahl
@end ifinfo

@c @code{%phi} represents the so-called @i{golden mean}, @math{(1 + sqrt(5))/2}.
@c The numeric value of @code{%phi} is the double-precision floating-point value
@c 1.618033988749895d0.

@code{%phi} repr@"asentiert die @i{Goldene Zahl} @math{(1 + sqrt(5))/2}.  Der
Gleitkommawert in doppelter Genauigkeit ist 1.618033988749895d0.

@c @code{fibtophi} expresses Fibonacci numbers @code{fib(n)} in terms of 
@c @code{%phi}.
@c By default, Maxima does not know the algebraic properties of @code{%phi}.
@c After evaluating @code{tellrat(%phi^2-%phi-1)} and @code{algebraic: true},
@c @code{ratsimp} can simplify some expressions containing @code{%phi}.

Die Funktion @mref{fibtophi} dr@"uckt Fibonacci-Zahlen @code{fib(n)} durch die
Goldene Zahl @code{%phi} aus.  Stan@-dard@-m@"a@ss{}ig kennt Maxima keine
algebraischen Eigenschaften der Konstanten @code{%phi}.  Mit den Eingaben
@mref{tellrat, tellrat(%phi^2-%phi-1)} und @mref{algebraic, algebraic:true} kann
die Funktion @mref{ratsimp} einige Vereinfachungen ausf@"uhren.

Beispiele:

@c @code{fibtophi} expresses Fibonacci numbers @code{fib(n)} in terms of 
@c @code{%phi}.

@code{fibtophi} dr@"uckt Fibonacci-Zahlen @code{fib(n)} durch @code{%phi} aus.

@c ===beg===
@c fibtophi (fib (n));
@c fib (n-1) + fib (n) - fib (n+1);
@c fibtophi (%);
@c ratsimp (%);
@c ===end===
@example
(%i1) fibtophi (fib (n));
                           n             n
                       %phi  - (1 - %phi)
(%o1)                  -------------------
                           2 %phi - 1
(%i2) fib (n-1) + fib (n) - fib (n+1);
(%o2)          - fib(n + 1) + fib(n) + fib(n - 1)
(%i3) fibtophi (%);
            n + 1             n + 1       n             n
        %phi      - (1 - %phi)        %phi  - (1 - %phi)
(%o3) - --------------------------- + -------------------
                2 %phi - 1                2 %phi - 1
                                          n - 1             n - 1
                                      %phi      - (1 - %phi)
                                    + ---------------------------
                                              2 %phi - 1
(%i4) ratsimp (%);
(%o4)                           0
@end example

@c By default, Maxima does not know the algebraic properties of @code{%phi}.
@c After evaluating @code{tellrat (%phi^2 - %phi - 1)} and 
@c @code{algebraic: true}, @code{ratsimp} can simplify some expressions 
@c containing @code{%phi}.

Mit den Eingaben @code{tellrat(%phi^2-%phi-1)} und @code{algebraic:true} kann 
die Funktion @code{ratsimp} einige Vereinfachungen f@"ur Ausdr@"ucke 
ausf@"uhren, die @code{%phi} enthalten.

@c ===beg===
@c e : expand ((%phi^2 - %phi - 1) * (A + 1));
@c ratsimp (e);
@c tellrat (%phi^2 - %phi - 1);
@c algebraic : true;
@c ratsimp (e);
@c ===end===
@example
(%i1) e : expand ((%phi^2 - %phi - 1) * (A + 1));
                 2                      2
(%o1)        %phi  A - %phi A - A + %phi  - %phi - 1
(%i2) ratsimp (e);
                  2                     2
(%o2)        (%phi  - %phi - 1) A + %phi  - %phi - 1
(%i3) tellrat (%phi^2 - %phi - 1);
                            2
(%o3)                  [%phi  - %phi - 1]
(%i4) algebraic : true;
(%o4)                         true
(%i5) ratsimp (e);
(%o5)                           0
@end example
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{%pi}
@defvr {Konstante} %pi
@ifinfo
@vrindex Pi
@end ifinfo

@c @code{%pi} represents the ratio of the perimeter of a circle to its diameter.
@c The numeric value of @code{%pi} is the double-precision floating-point value 
@c 3.141592653589793d0.

@code{%pi} repr@"asentiert die Kreiszahl.  Der numerische Wert in doppelter
Gleitkommagenauigkeit ist @code{3.141592653589793d0}.

Die Funktion @mref{bfloat} kann @code{%pi} mit einer beliebigen Genauigkeit
berechnen.
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{true}
@defvr {Konstante} true

@c @code{true} represents the Boolean constant of the same name. Maxima 
@c implements @code{true} by the value @code{T} in Lisp.

@code{true} repr@"asentiert den logischen Wert @code{wahr}.  Intern ist 
@code{true} als die Lisp-Konstante @code{T} implementiert.
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{und}
@defvr {Konstante} und
@ifinfo
@vrindex nicht definiert
@end ifinfo

@c @code{und} represents an undefined result.

@code{und} repr@"asentiert ein nicht definiertes Ergebnis.  Siehe auch 
@mrefdot{limit}

Beispiel:

@c ===beg===
@c limit (x*sin(x), x, inf);
@c ===end===
@example
(%i1) limit (x*sin(x), x, inf);
(%o1)                          und
@end example
@end defvr

@c --- 21.08.2010 DK -----------------------------------------------------------
@anchor{zeroa}
@defvr {Konstante} zeroa

@c @code{zeroa} represents an infinitesimal above zero. @code{zeroa} can be used
@c in expressions. @code{limit} simplifies expressions which contain 
@c infinitesimals.

@code{zeroa} repr@"asentiert eine positive unendlich kleine Zahl.  @code{zeroa}
kann in Ausdr@"ucken benutzt werden.  Die Funktion @code{limit} vereinfacht
Ausdr@"ucke, die infinitesimale Gr@"o@ss{}en enthalten.

Siehe auch @mref{zerob} und @mrefdot{limit}

Beispiele:

@c @code{limit} simplifies expressions which contain infinitesimals:

@code{limit} vereinfacht Ausdr@"ucke, die infinitesimale Gr@"o@ss{}en enthalten:

@c ===beg===
@c limit(zeroa);
@c limit(zeroa+x);
@c ===end===
@example
(%i1) limit(zeroa);
(%o1)                           0
(%i2) limit(x+zeroa);
(%o2)                           x
@end example
@end defvr

@c --- 15.10.2010 DK -----------------------------------------------------------
@anchor{zerob}
@defvr {Konstante} zerob

@c @code{zerob} represents an infinitesimal below zero. @code{zerob} can be used
@c in expressions. @code{limit} simplifies expressions which contain 
@c infinitesimals.

@code{zerob} repr@"asentiert eine negative unendlich kleine Zahl.  @code{zerob}
kann in Ausdr@"ucken benutzt werden.  Die Funktion @code{limit} vereinfacht
Ausdr@"ucke, die infinitesimale Gr@"o@ss{}en enthalten.

Siehe auch @mref{zeroa} und @mrefdot{limit}
@end defvr

@c -----------------------------------------------------------------------------
@node Funktionen und Variablen f@"ur Arrays, Einf@"uhrung in Listen, Funktionen und Variablen f@"ur Konstante, Datentypen und -strukturen
@section Funktionen und Variablen f@"ur Arrays
@c -----------------------------------------------------------------------------

@c --- 12.11.2010 DK -----------------------------------------------------------
@anchor{array}
@deffn  {Funktion} array (@var{name}, @var{dim_1}, @dots{}, @var{dim_n})
@deffnx {Funktion} array (@var{name}, @var{type}, @var{dim_1}, @dots{}, @var{dim_n})
@deffnx {Funktion} array ([@var{name_1}, @dots{}, @var{name_m}], @var{dim_1}, @dots{}, @var{dim_n})

@c Creates an @math{n}-dimensional array. @math{n} may be less than or equal to 
@c 5. The subscripts for the @math{i}'th dimension are the integers running from
@c 0 to @var{dim_i}.  

Erzeugt ein @math{n}-dimensionales Array.  @math{n} kann kleiner oder gleich 5 
Dimensionen sein.  Die Indizes der @math{i}-ten Dimension sind ganze Zahlen
in einem Bereich von 0 bis @var{dim_i}.

@c @code{array (@var{name}, @var{dim_1}, ..., @var{dim_n})} creates a general 
@c array.

@code{array(@var{name}, @var{dim_1}, ..., @var{dim_n})} erzeugt ein allgemeines 
Array.

@c @code{array (@var{name}, @var{type}, @var{dim_1}, ..., @var{dim_n})} creates
@c an array, with elements of a specified type. @var{type} can be @code{fixnum}
@c for integers of limited size or @code{flonum} for floating-point numbers.

@code{array(@var{name}, @var{type}, @var{dim_1}, ..., @var{dim_n})} erzeugt 
ein Array mit Elementen, die vom Typ @var{type} sind.  @var{type} kann 
@code{fixnum} f@"ur ganze Zahlen oder @code{flonum} f@"ur Gleitkommazahlen sein.
 
@c @code{array([@var{name_1}, ..., @var{name_m}], @var{dim_1}, ..., 
@c @var{dim_n})} creates @math{m} arrays, all of the same dimensions.
@c SAME TYPE AS WELL ??

@code{array([@var{name_1}, ..., @var{name_m}], @var{dim_1}, ..., @var{dim_n})}
erzeugt @math{m} Arrays, die alle die gleiche Dimension haben.

@c THIS DISCUSSION OF UNDECLARED ARRAYS REALLY WANTS TO BE SOMEWHERE ELSE
@c If the user assigns to a subscripted variable before declaring the
@c corresponding array, an undeclared array is created.
@c Undeclared arrays, otherwise known as hashed arrays (because hash
@c coding is done on the subscripts), are more general than declared
@c arrays.  The user does not declare their maximum size, and they grow
@c dynamically by hashing as more elements are assigned values.  The
@c subscripts of undeclared arrays need not even be numbers.  However,
@c unless an array is rather sparse, it is probably more efficient to
@c declare it when possible than to leave it undeclared.  The @code{array}
@c function can be used to transform an undeclared array into a declared array.
@c HOW DOES ONE CHANGE AN UNDECLARED ARRAY INTO A DECLARED ARRAY EXACTLY ??

Wird einer indizierten Variablen ein Wert zugewiesen, bevor das entsprechende
Array definiert wird, so wird ein undeklariertes Array erzeugt.  Undeklarierte
Arrays, auch Hashed-Arrays genannt, sind allgemeiner als deklarierte Arrays.
Diese Arrays haben keine feste Gr@"o@ss{}e und wachsen dynamisch mit der 
Zuweisung von Werten.  Die Indizes eines undeklarierten Arrays m@"ussen keine 
ganze Zahlen sein.  Deklarierte Arrays sind im Allgemeinen effizienter, 
so dass Arrays falls m@"oglich deklariert werden sollten.  Die Funktion 
@code{array} kann genutzt werden, um ein undeklariertes Array in ein 
deklariertes Array umzuwandeln.
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{arrayapply}
@deffn {Funktion} arrayapply (@var{A}, [@var{i_1}, @dots{}, @var{i_n}])

@c Evaluates @code{@var{A} [@var{i_1}, ..., @var{i_n}]},
@c where @var{A} is an array and @var{i_1}, ..., @var{i_n} are integers.

Wertet @code{@var{A}[@var{i_1}, ... , @var{i_n}]} aus, wobei @var{A} ein Array
und @var{i_1}, @dots{}, @var{i_n} ganze Zahlen sind.

@c This is reminiscent of @code{apply}, except the first argument is an array
@c instead of a function.

@code{arrayapply} entspricht der Funktion @mrefcomma{apply} wobei in diesem Fall
das Argument ein Array und keine Funktion ist.
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{arrayinfo}
@deffn {Funktion} arrayinfo (@var{A})

@c Returns information about the array @var{A}. The argument @var{A} may be a
@c declared array, an undeclared (hashed) array, an array function, or a 
@c subscripted function.

Gibt Information @"uber das Array @var{A} zur@"uck.  Das Argument @var{A} kann
ein deklariertes oder ein undeklariertes Array sowie eine Array-Funktion oder
eine indizierte Funktion sein.

@c For declared arrays, @code{arrayinfo} returns a list comprising the atom 
@c @code{declared}, the number of dimensions, and the size of each dimension.
@c The elements of the array, both bound and unbound, are returned by 
@c @code{listarray}.

F@"ur ein deklarierte Array gibt @code{arryinfo} eine Liste zur@"uck, die 
@code{declared}, die Zahl der Dimensionen und die Gr@"o@ss{}e der Dimensionen 
enh@"alt.  Die Elemente des Arrays werden von der Funktion @mref{listarray}@ 
zur@"uckgegeben.

@c For undeclared arrays (hashed arrays), @code{arrayinfo} returns a list 
@c comprising the atom @code{hashed}, the number of subscripts, and the 
@c subscripts of every element which has a value. The values are returned by 
@c @code{listarray}.

F@"ur ein undeklariertes Array (Hashed-Array) gibt @code{arrayinfo} eine Liste
zur@"uck, die @code{hashed}, die Zahl der Indizes und die Indizes enth@"alt,
deren Elemente einen Wert haben.  Die Werte der Elemente werden mit der Funktion 
@mref{listarray} zur@"uckgegeben.

@c For array functions, @code{arrayinfo} returns a list comprising the atom 
@c @code{hashed}, the number of subscripts, and any subscript values for which 
@c there are stored function values. The stored function values are returned by 
@c @code{listarray}.

F@"ur Array-Funktionen gibt @code{arrayinfo} eine Liste zur@"uck, die 
@code{hashed} die Zahl der Indizes und die Indizes enth@"alt, f@"ur die 
Funktionen im Array enthalten sind.  Die Funktionen werden mit der Funktion 
@mref{listarray} angezeigt.

@c For subscripted functions, @code{arrayinfo} returns a list comprising the 
@c atom @code{hashed}, the number of subscripts, and any subscript values for 
@c which there are lambda expressions. The lambda expressions are returned by 
@c @code{listarray}.

F@"ur indizierte Funktionen gibt @code{arrayinfo} eine Liste zur@"uck, die 
@code{hashed}, die Zahl der Indizes und die Indizies enth@"alt, f@"ur die 
Lambda-Ausdr@"ucke vorhanden sind.  Die Lambda-Ausdr@"ucke werden von der 
Funktion @mref{listarray} angezeigt.

Beispiele:

@c @code{arrayinfo} and @code{listarray} applied to a declared array.

@code{arrayinfo} und @code{listarray} angewendet auf ein deklariertes Array.

@c ===beg===
@c array (aa, 2, 3);
@c aa [2, 3] : %pi;
@c aa [1, 2] : %e;
@c arrayinfo (aa);
@c listarray (aa);
@c ===end===
@example
@group
(%i1) array (aa, 2, 3);
(%o1)                          aa
@end group
@group
(%i2) aa [2, 3] : %pi;
(%o2)                          %pi
@end group
@group
(%i3) aa [1, 2] : %e;
(%o3)                          %e
@end group
@group
(%i4) arrayinfo (aa);
(%o4)                 [declared, 2, [2, 3]]
@end group
@group
(%i5) listarray (aa);
(%o5) [#####, #####, #####, #####, #####, #####, %e, #####, 
                                        #####, #####, #####, %pi]
@end group
@end example

@c @code{arrayinfo} and @code{listarray} applied to an undeclared (hashed) 
@c array.

@code{arrayinfo} und @code{listarray} angewendet auf ein undeklariertes Array.

@c ===beg===
@c bb [FOO] : (a + b)^2;
@c bb [BAR] : (c - d)^3;
@c arrayinfo (bb);
@c listarray (bb);
@c ===end===
@example
@group
(%i1) bb [FOO] : (a + b)^2;
                                   2
(%o1)                       (b + a)
@end group
@group
(%i2) bb [BAR] : (c - d)^3;
                                   3
(%o2)                       (c - d)
@end group
@group
(%i3) arrayinfo (bb);
(%o3)               [hashed, 1, [BAR], [FOO]]
@end group
@group
(%i4) listarray (bb);
                              3         2
(%o4)                 [(c - d) , (b + a) ]
@end group
@end example

@c @code{arrayinfo} and @code{listarray} applied to an array function.

@code{arrayinfo} und @code{listarray} angewendet auf eine Array-Funktion.

@c ===beg===
@c cc [x, y] := y / x;
@c cc [u, v];
@c cc [4, z];
@c arrayinfo (cc);
@c listarray (cc);
@c ===end===
@example
@group
(%i1) cc [x, y] := y / x;
                                     y
(%o1)                      cc     := -
                             x, y    x
@end group
@group
(%i2) cc [u, v];
                                v
(%o2)                           -
                                u
@end group
@group
(%i3) cc [4, z];
                                z
(%o3)                           -
                                4
@end group
@group
(%i4) arrayinfo (cc);
(%o4)              [hashed, 2, [4, z], [u, v]]
@end group
@group
(%i5) listarray (cc);
                              z  v
(%o5)                        [-, -]
                              4  u
@end group
@end example

@c @code{arrayinfo} and @code{listarray} applied to a subscripted function.

@code{arrayinfo} und @code{listarray} angewendet auf eine indizierte Funktion.

@c ===beg===
@c dd [x] (y) := y ^ x;
@c dd [a + b];
@c dd [v - u];
@c arrayinfo (dd);
@c listarray (dd);
@c ===end===
@example
@group
(%i1) dd [x] (y) := y ^ x;
                                     x
(%o1)                     dd (y) := y
                            x
@end group
@group
(%i2) dd [a + b];
                                    b + a
(%o2)                  lambda([y], y     )
@end group
@group
(%i3) dd [v - u];
                                    v - u
(%o3)                  lambda([y], y     )
@end group
@group
(%i4) arrayinfo (dd);
(%o4)             [hashed, 1, [b + a], [v - u]]
@end group
@group
(%i5) listarray (dd);
                         b + a                v - u
(%o5)      [lambda([y], y     ), lambda([y], y     )]
@end group
@end example
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{arraymake}
@deffn {Funktion} arraymake (@var{A}, [@var{i_1}, @dots{}, @var{i_n}])

@c Returns the expression @code{@var{A}[@var{i_1}, ..., @var{i_n}]}. The result
@c is an unevaluated array reference.

Gibt eine nicht ausgewertete Referenz @code{@var{A}[@var{i_1}, ..., @var{i_n}]}
auf das Array @var{A} zur@"uck.

@c @code{arraymake} is reminiscent of @code{funmake}, except the return value 
@c is an unevaluated array reference instead of an unevaluated function call.

@code{arraymake} entspricht der Funktion @mrefcomma{funmake} wobei in diesem
Fall das Argument eine nicht ausgewerte Referenz auf ein Array ist.

Beispiele:

@c ===beg===
@c arraymake (A, [1]);
@c arraymake (A, [k]);
@c arraymake (A, [i, j, 3]);
@c array (A, fixnum, 10);
@c fillarray (A, makelist (i^2, i, 1, 11));
@c arraymake (A, [5]);
@c ''%;
@c L : [a, b, c, d, e];
@c arraymake ('L, [n]);
@c ''%, n = 3;
@c A2 : make_array (fixnum, 10);
@c fillarray (A2, [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
@c arraymake ('A2, [8]);
@c ''%;
@c ===end===
@example
@group
(%i1) arraymake (A, [1]);
(%o1)                          A
                                1
@end group
@group
(%i2) arraymake (A, [k]);
(%o2)                          A
                                k
@end group
@group
(%i3) arraymake (A, [i, j, 3]);
(%o3)                       A
                             i, j, 3
@end group
@group
(%i4) array (A, fixnum, 10);
(%o4)                           A
@end group
@group
(%i5) fillarray (A, makelist (i^2, i, 1, 11));
(%o5)                           A
@end group
@group
(%i6) arraymake (A, [5]);
(%o6)                          A
                                5
@end group
@group
(%i7) ''%;
(%o7)                          36
@end group
@group
(%i8) L : [a, b, c, d, e];
(%o8)                    [a, b, c, d, e]
@end group
@group
(%i9) arraymake ('L, [n]);
(%o9)                          L
                                n
@end group
@group
(%i10) ''%, n = 3;
(%o10)                          c
@end group
@group
(%i11) A2 : make_array (fixnum, 10);
(%o11)          @{Array:  #(0 0 0 0 0 0 0 0 0 0)@}
@end group
@group
(%i12) fillarray (A2, [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
(%o12)          @{Array:  #(1 2 3 4 5 6 7 8 9 10)@}
@end group
@group
(%i13) arraymake ('A2, [8]);
(%o13)                         A2
                                 8
@end group
@group
(%i14) ''%;
(%o14)                          9
@end group
@end example
@end deffn

@c --- 12.11.2010 DK -----------------------------------------------------------
@anchor{arrays}
@defvr {Systemvariable} arrays
Standardwert: @code{[]}

@c @code{arrays} is a list of arrays that have been allocated. These comprise 
@c arrays declared by @code{array}, hashed arrays constructed by implicit 
@c definition (assigning something to an array element), and array functions 
@c defined by @code{:=} and @code{define}. Arrays defined by @code{make_array} 
@c are not included.

@code{arrays} ist eine Informationsliste @mref{infolists} der Arrays, die 
definiert sind.  Die Liste enth@"alt deklarierte Arrays, undedeklarierte Arrays 
und Array-Funktionen, die mit @mref{:=} oder @mref{define} definiert sind.
Arrays die mit @mref{make_array} definiert sind, sind nicht in der Liste 
enthalten.

@c See also @code{array}, @code{arrayapply}, @code{arrayinfo}, @code{arraymake},
@c @code{fillarray}, @code{listarray}, and @code{rearray}.
@c IS THIS AN EXHAUSTIVE LIST ??

Siehe auch @mrefcomma{array} @mrefcomma{arrayapply} @mrefcomma{arrayinfo}@ 
@mrefcomma{arraymake} @mrefcomma{fillarray} @mref{listarray} und 
@mrefdot{rearray}

Beispiele:

@c ===beg===
@c array (aa, 5, 7);
@c bb [FOO] : (a + b)^2;
@c cc [x] := x/100;
@c dd : make_array ('any, 7);
@c arrays;
@c ===end===
@example
@group
(%i1) array (aa, 5, 7);
(%o1)                          aa
@end group
@group
(%i2) bb [FOO] : (a + b)^2;
                                   2
(%o2)                       (b + a)
@end group
@group
(%i3) cc [x] := x/100;
                                   x
(%o3)                      cc  := ---
                             x    100
@end group
@group
(%i4) dd : make_array ('any, 7);
(%o4)       @{Array:  #(NIL NIL NIL NIL NIL NIL NIL)@}
@end group
@group
(%i5) arrays;
(%o5)                     [aa, bb, cc]
@end group
@end example
@end defvr

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{fillarray}
@deffn {Funktion} fillarray (@var{A}, @var{B})

@c Fills array @var{A} from @var{B}, which is a list or an array.

F@"ullt das Array @var{A} mit den Werten aus @var{B}.  Das Argument @var{B} ist 
eine Liste oder ein Array.

@c If a specific type was declared for @var{A} when it was created, it can only 
@c be filled with elements of that same type; it is an error if an attempt is 
@c made to copy an element of a different type.

Ist das Array @var{A} mit einem Typ deklariert, dann kann es nur mit Elementen
gef@"ullt werden, die den gleichen Typ haben.

@c If the dimensions of the arrays @var{A} and @var{B} are different, @var{A} 
@c is filled in row-major order.  If there are not enough elements in @var{B} 
@c the last element is used to fill out the rest of @var{A}.  If there are too 
@c many, the remaining ones are ignored.

Sind die Dimensionen von @var{A} und @var{B} verschieden, werden in @var{A}
zun@"achst die Zeilen aufgef@"ullt.  Hat @var{B} nicht gen@"ugend Elemente um 
das Array @var{A} aufzuf@"ullen, werden die restlichen Elemente mit dem letzten 
Wert von @var{B} aufgef@"ullt.  @"Uberz@"ahlige Elemente in @var{B} werden 
ignoriert.

@c @code{fillarray} returns its first argument.

@code{fillarray} gibt das erste Argument zur@"uck.

Beispiele:

@c Create an array of 9 elements and fill it from a list.

Erzeuge ein Array mit 9 Elementen und f@"ulle es mit den Elementen einer Liste.

@c ===beg===
@c array (a1, fixnum, 8);
@c listarray (a1);
@c fillarray (a1, [1, 2, 3, 4, 5, 6, 7, 8, 9]);
@c listarray (a1);
@c ===end===
@example
@group
(%i1) array (a1, fixnum, 8);
(%o1)                          a1
@end group
@group
(%i2) listarray (a1);
(%o2)              [0, 0, 0, 0, 0, 0, 0, 0, 0]
@end group
@group
(%i3) fillarray (a1, [1, 2, 3, 4, 5, 6, 7, 8, 9]);
(%o3)                          a1
@end group
@group
(%i4) listarray (a1);
(%o4)              [1, 2, 3, 4, 5, 6, 7, 8, 9]
@end group
@end example

@c When there are too few elements to fill the array, the last element is 
@c repeated. When there are too many elements, the extra elements are ignored.

Sind zu wenige Elemente vorhanden, um das Array aufzuf@"ullen, wird das Array
mit dem letzten Element aufgef@"ullt.  @"Uberz@"ahlige Elemente werden 
ignoriert.

@c ===beg===
@c a2 : make_array (fixnum, 8);
@c fillarray (a2, [1, 2, 3, 4, 5]);
@c fillarray (a2, [4]);
@c fillarray (a2, makelist (i, i, 1, 100));
@c ===end===
@example
@group
(%i1) a2 : make_array (fixnum, 8);
(%o1)             @{Array:  #(0 0 0 0 0 0 0 0)@}
@end group
@group
(%i2) fillarray (a2, [1, 2, 3, 4, 5]);
(%o2)             @{Array:  #(1 2 3 4 5 5 5 5)@}
@end group
@group
(%i3) fillarray (a2, [4]);
(%o3)             @{Array:  #(4 4 4 4 4 4 4 4)@}
@end group
@group
(%i4) fillarray (a2, makelist (i, i, 1, 100));
(%o4)             @{Array:  #(1 2 3 4 5 6 7 8)@}
@end group
@end example

@c Multple-dimension arrays are filled in row-major order.

Arrays werden zeilenweise aufgef@"ulllt.

@c ===beg===
@c a3 : make_array (fixnum, 2, 5);
@c fillarray (a3, [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
@c a4 : make_array (fixnum, 5, 2);
@c fillarray (a4, a3);
@c ===end===
@example
@group
(%i1) a3 : make_array (fixnum, 2, 5);
(%o1)        @{Array:  #2A((0 0 0 0 0) (0 0 0 0 0))@}
@end group
@group
(%i2) fillarray (a3, [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
(%o2)        @{Array:  #2A((1 2 3 4 5) (6 7 8 9 10))@}
@end group
@group
(%i3) a4 : make_array (fixnum, 5, 2);
(%o3)     @{Array:  #2A((0 0) (0 0) (0 0) (0 0) (0 0))@}
@end group
@group
(%i4) fillarray (a4, a3);
(%o4)     @{Array:  #2A((1 2) (3 4) (5 6) (7 8) (9 10))@}
@end group
@end example
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{listarray}
@deffn {Funktion} listarray (@var{A})

@c Returns a list of the elements of the array @var{A}. The argument @var{A} 
@c may be a declared array, an undeclared (hashed) array, an array function, or 
@c a subscripted function.

Gibt eine Liste mit den Elementen des Arrays @var{A} zur@"uck.  Das Argument 
@var{A} kann ein deklariertes, undeklariertes, eine Array-Funktion oder eine
indizierte Funktion sein.

@c Elements are listed in row-major order. That is, elements are sorted 
@c according to the first index, then according to the second index, and so on.
@c The sorting order of index values is the same as the order established by 
@c @code{orderless}.

Die Elemente werden zeilenweise ausgegeben.  F@"ur Hashed-Arrays mit Indizes, 
die keine ganze Zahlen sind, wird die Sortierung von der Funktion 
@mref{orderless} bestimmt.

@c For undeclared arrays, array functions, and subscripted functions, the 
@c elements correspond to the index values returned by @code{arrayinfo}.

F@"ur undeklarierte Arrays, Array-Funktionen und indizierte Funktionen werden
die Elemente in der Reihenfolge wie von der Funktion 
@mref{arrayinfo} zur@"uckgegeben.

@c Unbound elements of declared general arrays (that is, not @code{fixnum} and 
@c not @code{flonum}) are returned as @code{#####}. Unbound elements of declared
@c @code{fixnum} or @code{flonum} arrays are returned as 0 or 0.0, respectively.
@c Unbound elements of undeclared arrays, array functions, and subscripted 
@c functions are not returned.

Elemente von deklarierten Arrays, denen noch kein Wert zugewiesen wurde und die
keinen Typ haben, werden als @code{#####} zur@"uckgegeben.  Elemente von
deklarierten Arrays mit einem Typ, geben den Wert 0 f@"ur den Typ @code{fixnum}
und 0.0 f@"ur den Typ @code{flonum} zur@"uck.

Beispiele:

@c @code{listarray} and @code{arrayinfo} applied to a declared array.

Anwendung der Funktionen @code{listarray} und @code{arryinfo} f@"ur ein
deklariertes Array.

@c ===beg===
@c array (aa, 2, 3);
@c aa [2, 3] : %pi;
@c aa [1, 2] : %e;
@c listarray (aa);
@c arrayinfo (aa);
@c ===end===
@example
@group
(%i1) array (aa, 2, 3);
(%o1)                          aa
@end group
@group
(%i2) aa [2, 3] : %pi;
(%o2)                          %pi
@end group
@group
(%i3) aa [1, 2] : %e;
(%o3)                          %e
@end group
@group
(%i4) listarray (aa);
(%o4) [#####, #####, #####, #####, #####, #####, %e, #####, 
                                        #####, #####, #####, %pi]
@end group
@group
(%i5) arrayinfo (aa);
(%o5)                 [declared, 2, [2, 3]]
@end group
@end example

@c @code{listarray} and @code{arrayinfo} applied to an undeclared (hashed) 
@c array.

Anwendung der Funktionen @code{listarray} und @code{arryinfo} f@"ur ein
undeklariertes Array.

@c ===beg===
@c bb [FOO] : (a + b)^2;
@c bb [BAR] : (c - d)^3;
@c listarray (bb);
@c arrayinfo (bb);
@c ===end===
@example
@group
(%i1) bb [FOO] : (a + b)^2;
                                   2
(%o1)                       (b + a)
@end group
@group
(%i2) bb [BAR] : (c - d)^3;
                                   3
(%o2)                       (c - d)
@end group
@group
(%i3) listarray (bb);
                              3         2
(%o3)                 [(c - d) , (b + a) ]
@end group
@group
(%i4) arrayinfo (bb);
(%o4)               [hashed, 1, [BAR], [FOO]]
@end group
@end example

@c @code{listarray} and @code{arrayinfo} applied to an array function.

Anwendung der Funktionen @code{listarray} und @code{arryinfo} f@"ur eine
Array-Funktion.

@c ===beg===
@c cc [x, y] := y / x;
@c cc [u, v];
@c cc [4, z];
@c listarray (cc);
@c arrayinfo (cc);
@c ===end===
@example
@group
(%i1) cc [x, y] := y / x;
                                     y
(%o1)                      cc     := -
                             x, y    x
@end group
@group
(%i2) cc [u, v];
                                v
(%o2)                           -
                                u
@end group
@group
(%i3) cc [4, z];
                                z
(%o3)                           -
                                4
@end group
@group
(%i4) listarray (cc);
                              z  v
(%o4)                        [-, -]
                              4  u
@end group
@group
(%i5) arrayinfo (cc);
(%o5)              [hashed, 2, [4, z], [u, v]]
@end group
@end example

@c @code{listarray} and @code{arrayinfo} applied to a subscripted function.

Anwendung der Funktionen @code{listarray} und @code{arryinfo} f@"ur ein
indizierte Funktion.

@c ===beg===
@c dd [x] (y) := y ^ x;
@c dd [a + b];
@c dd [v - u];
@c listarray (dd);
@c arrayinfo (dd);
@c ===end===
@example
@group
(%i1) dd [x] (y) := y ^ x;
                                     x
(%o1)                     dd (y) := y
                            x
@end group
@group
(%i2) dd [a + b];
                                    b + a
(%o2)                  lambda([y], y     )
@end group
@group
(%i3) dd [v - u];
                                    v - u
(%o3)                  lambda([y], y     )
@end group
@group
(%i4) listarray (dd);
                         b + a                v - u
(%o4)      [lambda([y], y     ), lambda([y], y     )]
@end group
@group
(%i5) arrayinfo (dd);
(%o5)             [hashed, 1, [b + a], [v - u]]
@end group
@end example
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{make_array}
@deffn {Funktion} make_array (@var{type}, @var{dim_1}, @dots{}, @var{dim_n})

@c Creates and returns a Lisp array.  @var{type} may be @code{any}, 
@c @code{flonum}, @code{fixnum}, @code{hashed} or @code{functional}. There are 
@c @math{n} indices, and the @math{i}'th index runs from 0 to 
@c @math{@var{dim_i} - 1}.

Erzeugt ein Lisp-Array.  Das Argument @var{type} kann die Werte @code{any},
@code{flonum}, @code{fixnum}, @code{hashed} oder @code{functional} haben.
Das Array hat @math{i} Dimensionen und der Index @math{i} l@"auft von 0 bis
@math{@var{dim_i}-1}.

@c The advantage of @code{make_array} over @code{array} is that the return value
@c doesn't have a name, and once a pointer to it goes away, it will also go 
@c away. For example, if @code{y: make_array (...)} then @code{y} points to an 
@c object which takes up space, but after @code{y: false}, @code{y} no longer
@c points to that object, so the object can be garbage collected.  

@c TODO: ES FEHLT EINE UEBERSETZUNG, DIE WENIGER TECHNISCH SEIN SOLLTE.

@c 'FUNCTIONAL ARGUMENT IN MAKE_ARRAY APPEARS TO BE BROKEN
@c EVEN AFTER READING THE CODE (SRC/AR.LISP) I CAN'T TELL HOW THIS IS SUPPOSED 
@c TO WORK COMMENTING OUT THIS STUFF TO PREVENT CONFUSION AND HEARTBREAK
@c RESTORE IT WHEN MAKE_ARRAY ('FUNCTIONAL, ...) IS FIXED
@c @code{y: make_array ('functional, 'f, 'hashed, 1)} - the second argument to
@c @code{make_array} in this case is the function to call to calculate array
@c elements, and the rest of the arguments are passed recursively to
@c @code{make_array} to generate the "memory" for the array function object.

Beispiele:

@c ===beg===
@c A1 : make_array (fixnum, 10);
@c A1 [8] : 1729;
@c A1;
@c A2 : make_array (flonum, 10);
@c A2 [2] : 2.718281828;
@c A2;
@c A3 : make_array (any, 10);
@c A3 [4] : x - y - z;
@c A3;
@c A4 : make_array (fixnum, 2, 3, 5);
@c fillarray (A4, makelist (i, i, 1, 2*3*5));
@c A4 [0, 2, 1];
@c ===end===
@example
@group
(%i1) A1 : make_array (fixnum, 10);
(%o1)           @{Array:  #(0 0 0 0 0 0 0 0 0 0)@}
@end group
@group
(%i2) A1 [8] : 1729;
(%o2)                         1729
@end group
@group
(%i3) A1;
(%o3)          @{Array:  #(0 0 0 0 0 0 0 0 1729 0)@}
@end group
@group
(%i4) A2 : make_array (flonum, 10);
(%o4) @{Array:  #(0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0)@}
@end group
@group
(%i5) A2 [2] : 2.718281828;
(%o5)                      2.718281828
@end group
@group
(%i6) A2;
(%o6) 
     @{Array:  #(0.0 0.0 2.718281828 0.0 0.0 0.0 0.0 0.0 0.0 0.0)@}
@end group
@group
(%i7) A3 : make_array (any, 10);
(%o7) @{Array:  #(NIL NIL NIL NIL NIL NIL NIL NIL NIL NIL)@}
@end group
@group
(%i8) A3 [4] : x - y - z;
(%o8)                      - z - y + x
@end group
@group
(%i9) A3;
(%o9) @{Array:  #(NIL NIL NIL NIL ((MPLUS SIMP) $X ((MTIMES SIMP)\
 -1 $Y) ((MTIMES SIMP) -1 $Z))
  NIL NIL NIL NIL NIL)@}
@end group
@group
(%i10) A4 : make_array (fixnum, 2, 3, 5);
(%o10) @{Array:  #3A(((0 0 0 0 0) (0 0 0 0 0) (0 0 0 0 0)) ((0 0 \
0 0 0) (0 0 0 0 0) (0 0 0 0 0)))@}
@end group
@group
(%i11) fillarray (A4, makelist (i, i, 1, 2*3*5));
(%o11) @{Array:  #3A(((1 2 3 4 5) (6 7 8 9 10) (11 12 13 14 15))
    ((16 17 18 19 20) (21 22 23 24 25) (26 27 28 29 30)))@}
@end group
@group
(%i12) A4 [0, 2, 1];
(%o12)                         12
@end group
@end example
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{rearray}
@deffn {Funktion} rearray (@var{A}, @var{dim_1}, @dots{}, @var{dim_n})

@c Changes the dimensions of an array. The new array will be filled with the
@c elements of the old one in row-major order.  If the old array was too small,
@c the remaining elements are filled with @code{false}, @code{0.0} or @code{0},
@c depending on the type of the array.  The type of the array cannot be changed.

@"Andert die Dimension eines Arrays.  Das neue Array wird zeilenweise mit den 
Werten des alten Arrays aufgef@"ullt.  Hat das alte Array nicht gen@"ugend 
Elemente werden die restlichen Elemente entsprechend dem Typ des Arrays mit 
@code{false}, @code{0.0} oder @code{0} aufgef@"ullt.  Der Typ des Arrays kann 
nicht ge@"andert werden.
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{remarray}
@deffn  {Funktion} remarray (@var{A_1}, @dots{}, @var{A_n})
@deffnx {Funktion} remarray (all)

@c Removes arrays and array associated functions and frees the storage occupied.
@c The arguments may be declared arrays, undeclared (hashed) arrays, array
@c functions, and subscripted functions.

Entfernt Arrays und Array-Funktionen.  Der vom Array belegte Speicher wird
freigegeben.  Die Argumente k@"onnen deklarierte und undeklarierte Arrays sowie 
Array-Funktionen und indizierte Funktionen sein.

@c @code{remarray (all)} removes all items in the global list @code{arrays}.

@code{remarray(all)} entfernt alle Arrays, die in der Informationsliste
@mref{arrays} enthalten sind.

@c WHAT DOES THIS MEAN EXACTLY ??
@c It may be necessary to use this function if it is
@c desired to redefine the values in a hashed array.

@c TODO: KLARSTELLUNG UND UEBERSETZUNG

@c @code{remarray} returns the list of arrays removed.

@code{remarray} gibt eine Liste der Arrays zur@"uck, die entfernt wurden.
@end deffn

@c --- 13.11.2010 DK -----------------------------------------------------------
@anchor{subvar}
@deffn {Funktion} subvar (@var{x}, @var{i})

@c Evaluates the subscripted expression @code{@var{x}[@var{i}]}.
@c @code{subvar} evaluates its arguments.

Wertet den indizierten Ausdruck @code{@var{x}[@var{i}]} aus.  @code{subvar}
wertet die Argumente aus.

@c @code{arraymake (@var{x}, [@var{i}]} constructs the expression 
@c @code{@var{x}[@var{i}]}, but does not evaluate it.

@code{arraymake(@var{x}, [@var{i}]} generiert den Ausdruck 
@code{@var{x}[@var{i}]}, ohne diesen auszuwerten.  Siehe @mrefdot{arraymake}

Beispiele:

@c ===beg===
@c x : foo $
@c i : 3 $
@c subvar (x, i);
@c foo : [aa, bb, cc, dd, ee]$
@c subvar (x, i);
@c arraymake (x, [i]);
@c ''%;
@c ===end===
@example
(%i1) x : foo $
(%i2) i : 3 $
@group
(%i3) subvar (x, i);
(%o3)                         foo
                                 3
@end group
(%i4) foo : [aa, bb, cc, dd, ee]$
@group
(%i5) subvar (x, i);
(%o5)                          cc
@end group
@group
(%i6) arraymake (x, [i]);
(%o6)                         foo
                                 3
@end group
@group
(%i7) ''%;
(%o7)                          cc
@end group
@end example
@end deffn

@c --- 12.11.2010 DK -----------------------------------------------------------
@anchor{subvarp}
@deffn {Funktion} subvarp (@var{expr})

@c Returns @code{true} if @var{expr} is a subscripted variable, for example
@c @code{a[i]}.

Gibt @code{true} zur@"uck, wenn @var{expr} eine indizierte Variable wie zum
Beispiel @code{a[i]} ist.
@end deffn

@c THIS IS REALLY CONFUSING

@c --- 12.11.2010 DK -----------------------------------------------------------
@anchor{use_fast_arrays}
@defvr {Optionsvariable} use_fast_arrays

@c - if @code{true} then only two types of arrays are recognized.

Hat die Optionsvariable @code{use_fast_arrays} den Wert @code{true}, werden nur
die folgenden zwei Typen von Arrays erkannt:

@enumerate
@c 1) The art-q array (t in Common Lisp) which may have several dimensions
@c indexed by integers, and may hold any Lisp or Maxima object as an
@c entry.  To construct such an array, enter @code{a:make_array(any,3,4);} 
@c then @code{a} will have as value, an array with twelve slots, and the 
@c indexing is zero based.

@item
Ein Array, das mehrere Dimensionen haben kann und ganzzahligen Indizes.  Den
Elementen kann jedes Lisp oder Maxima-Objekt zugewiesen werden.  Zum Beispiel
weist das Kommando @code{a:make_array(any,3,4)} der Variablen @var{a} ein
Array zu, das @code{12} Elemente hat.

@c 2) The Hash_table array which is the default type of array created if one
@c does @code{b[x+1]:y^2} (and @code{b} is not already an array, a list, or a
@c matrix -- if it were one of these an error would be caused since
@c @code{x+1} would not be a valid subscript for an art-q array, a list or
@c a matrix). Its indices (also known as keys) may be any object.  
@c It only takes one key at a time (@code{b[x+1,u]:y} would ignore the @code{u}).
@c Referencing is done by @code{b[x+1] ==> y^2}.  Of course the key may be
@c a list, e.g. @code{b[[x+1,u]]:y} would be valid.  This is incompatible 
@c with the old Maxima hash arrays, but saves consing.

@item
Ein undeklariertes Array, welches standardm@"a@ss{}ig erzeugt wird, wenn zum 
Beispiel einer indizierten Variablen @code{b[x+1]:y^2} ein Wert zugewiesen wird 
und @code{b} nicht bereits ein deklariertes Array oder eine Liste ist.  Ist 
@var{b} bereits deklariert, wird eine Fehlermeldung ausgeben, da der Index 
@var{x+1} keine ganze Zahl ist.  Der Index eines undeklarierten Arrays kann ein 
beliebiger Ausdruck sein.
@end enumerate

@c An advantage of storing the arrays as values of the symbol is that the
@c usual conventions about local variables of a function apply to arrays as
@c well.  The Hash_table type also uses less consing and is more efficient
@c than the old type of Maxima hashar.  To obtain consistent behaviour in
@c translated and compiled code set @code{translate_fast_arrays} to be 
@c @code{true}.

@c TODO: VEREINFACHE DIE TECHNISCHE BESCHREIBUNG.
@end defvr

@c --- End of file DataTypes.de.texi -------------------------------------------

