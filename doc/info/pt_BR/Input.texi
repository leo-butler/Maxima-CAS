@c Language: Brazilian Portuguese, Encoding: iso-8859-1
@c /Input.texi/1.52/Sun Jan  7 05:49:17 2007/-ko/
@menu
* Coment@'arios::
* Arquivos::                       
* Defini@value{cedilha}@~oes para Entrada e Sa@'ida::  
@end menu

@node Coment@'arios, Arquivos, Entrada e Sa@'ida, Entrada e Sa@'ida
@section Coment@'arios

Um coment@'ario na entrada do Maxima @'e qualquer texto entre @code{/*} e @code{*/}.

O analisador do Maxima trata um coment@'ario como espa@,{c}@~ao em branco para o prop@'osito de
encontrar indica@,{c}@~oes no fluxo de entrada;
uma indica@,{c}@~ao sempre termina um coment@'ario.
Uma entrada tal como @code{a/* foo */b} cont@'em duas indica@,{c}@~oes, @code{a} e @code{b},
e n@~ao uma indica@,{c}@~ao simples @code{ab}.
Coment@'arios s@~ao de outra ignorados pelo Maxima;
nem o conte@'udo nem a localiza@,{c}@~ao dos coment@'arios s@~ao armazenados pelo analisador de express@~oes de entrada.

Coment@'arios podem ser aninhados de forma a terem um n@'ivel de estratifica@,{c}@~ao arbitrario.
O delimitador @code{/*} e o delimitador @code{*/} formam pares.
A quantidade de @code{/*} deve ser a mesma quantidade de @code{*/}.

Exemplos:

@c ===beg===
@c /* aa is a variable of interest */  aa : 1234;
@c /* Value of bb depends on aa */  bb : aa^2;
@c /* User-defined infix operator */  infix ("b");
@c /* Parses same as a b c, not abc */  a/* foo */b/* bar */c;
@c /* Comments /* can be nested /* to arbitrary depth */ */ */  1 + xyz;
@c ===end===
@example
(%i1) /* aa is a variable of interest */  aa : 1234;
(%o1)                         1234
(%i2) /* Value of bb depends on aa */  bb : aa^2;
(%o2)                        1522756
(%i3) /* User-defined infix operator */  infix ("b");
(%o3)                           b
(%i4) /* Parses same as a b c, not abc */  a/* foo */b/* bar */c;
(%o4)                         a b c
(%i5) /* Comments /* can be nested /* to arbitrary depth */ */ */  1 + xyz;
(%o5)                        xyz + 1
@end example


@node Arquivos, Defini@value{cedilha}@~oes para Entrada e Sa@'ida, Coment@'arios, Entrada e Sa@'ida
@section Arquivos
Um arquivo @'e simplesmente uma @'area sobre um dispositivo particular de armazenagem que cont@'em dados ou texto.
Arquivos em disco s@~ao figurativamente agrupados dentro de "diret@'orios".
Um diret@'orio @'e apenas uma lista de arquivos.
Comandos que lidam com arquivos s@~ao:
@code{save},
@code{load},
@code{loadfile},
@code{stringout},
@code{batch},
@code{demo},
@code{writefile},
@code{closefile},
e
@code{appendfile}.

@node Defini@value{cedilha}@~oes para Entrada e Sa@'ida,  , Arquivos, Entrada e Sa@'ida
@section Defini@value{cedilha}@~oes para Entrada e Sa@'ida de Dados
@defvr {Vari@'avel de sistema} __
@ifinfo
@vrindex Express@~ao de entrada atual
@end ifinfo
@code{__} @'e a express@~ao de entrada atualmente sendo avaliada.
Isto @'e, enquanto um express@~ao de entrada @var{expr} est@'a sendo avaliada, @code{__} @'e @var{expr}.

@code{__} @'e atribu@'ida @`a express@~ao de entrada antes de a entrada ser simplificada ou avaliada.
Todavia, o valor de @code{__} @'e simplificado (mas n@~ao avaliado) quando for mostrado.

@code{__} @'e reconhecido por @code{batch} e @code{load}.
Em um arquivo processado por @code{batch},
@code{__} tem o mesmo significado que na linha de comando interativa.
Em um arquivo processado por @code{load},
@code{__} est@'a associado @`a express@~ao de entrada mais recentemente informada no prompt interativo
ou em um arquivo de lote (batch);
@code{__} n@~ao @'e associado @`a express@~oes de entrada no arquivo que est@'a sendo processado.
Em particular, quando @code{load (@var{nomearquivo})} for chamado a partir da linha de comando interativa,
@code{__} @'e associado a @code{load (@var{nomearquivo})}
enquanto o arquivo est@'a sendo processado.

Veja tamb@'em @code{_} e @code{%}.

Exemplos:

@c ===beg===
@c print ("Eu fui chamada como", __);
@c foo (__);
@c g (x) := (print ("Express@~ao atual de entrada =", __), 0);
@c [aa : 1, bb : 2, cc : 3];
@c (aa + bb + cc)/(dd + ee + g(x));
@c ===end===
@example
(%i1) print ("Eu fui chamada como", __);
Eu fui chamada como print(Eu fui chamada como, __) 
(%o1)              print(Eu fui chamada como, __)
(%i2) foo (__);
(%o2)                     foo(foo(__))
(%i3) g (x) := (print ("Express@~ao atual de entrada =", __), 0);
(%o3) g(x) := (print("Express@~ao atual de entrada =", __), 0)
(%i4) [aa : 1, bb : 2, cc : 3];
(%o4)                       [1, 2, 3]
(%i5) (aa + bb + cc)/(dd + ee + g(x));
                               cc + bb + aa
Express@~ao atual de entrada = -------------- 
                              g(x) + ee + dd
                                6
(%o5)                        -------
                             ee + dd
@end example

@end defvr

@defvr {Vari@'avel de sistema} _
@ifinfo
@vrindex Entrada anterior
@end ifinfo

@code{_} @'e a mais recente express@~ao de  entrada (e.g., @code{%i1}, @code{%i2}, @code{%i3}, ...).

A @code{_} @'e atribu@'ida @`a express@~ao de entrada antes dela ser simplificada ou avaliada.
Todavia, o valor de @code{_} @'e simplificado (mas n@~ao avaliado) quando for mostrado.

@code{_} @'e reconhecido por @code{batch} e @code{load}.
Em um arquivo processado por @code{batch},
@code{_} tem o mesmo significado que na linha de comando interativa.
Em um arquivo processado por load @code{load},
@code{_} est@'a associado @`a express@~ao de entrada mais recentemente avaliada na linha de comando interativa
ou em um arquivo de lote;
@code{_} n@~ao est@'a associada a express@~oes de entrada no arquivo que est@'a sendo processado.

Veja tamb@'em @code{__} e @code{%}.

Exemplos:

@c ===beg===
@c 13 + 29;
@c :lisp $_
@c _;
@c sin (%pi/2);
@c :lisp $_
@c _;
@c a: 13$
@c b: 29$
@c a + b;
@c :lisp $_
@c _;
@c a + b;
@c ev (_);
@c ===end===
@example
(%i1) 13 + 29;
(%o1)                          42
(%i2) :lisp $_
((MPLUS) 13 29)
(%i2) _;
(%o2)                          42
(%i3) sin (%pi/2);
(%o3)                           1
(%i4) :lisp $_
((%SIN) ((MQUOTIENT) $%PI 2))
(%i4) _;
(%o4)                           1
(%i5) a: 13$
(%i6) b: 29$
(%i7) a + b;
(%o7)                          42
(%i8) :lisp $_
((MPLUS) $A $B)
(%i8) _;
(%o8)                         b + a
(%i9) a + b;
(%o9)                          42
(%i10) ev (_);
(%o10)                         42
@end example

@end defvr

@defvr {Vari@'avel de sistema} %
@ifinfo
@vrindex Sa@'ida anterior
@end ifinfo
@code{%} @'e a express@~ao de sa@'ida (e.g., @code{%o1}, @code{%o2}, @code{%o3}, ...)
mais recentemente calculada pelo Maxima,
pode ou n@~ao ser mostrada.

@code{%} @'e reconhecida por @code{batch} e @code{load}.
Em um arquivo processado por @code{batch},
@code{%} tem o mesmo significado que na linha de comando interativa.
Em um arquivo processado por @code{load},
@code{%} @'e associado @`a express@~ao de entrada mais recentemente calculada na linha de comando interativa 
ou em um arquivo de lote;
@code{%} n@~ao est@'a associada a express@~oes de sa@'ida no arquivo que est@'a sendo processado.

Veja tamb@'em @code{_}, @code{%%}, e @code{%th}

@end defvr

@defvr {Vari@'avel de sistema} %%
@ifinfo
@vrindex Resultado anterior em express@~ao composta
@end ifinfo
Em declara@,{c}@~ao composta,
a saber @code{block}, @code{lambda}, ou @code{(@var{s_1}, ..., @var{s_n})},
@code{%%} @'e os valor da declara@,{c}@~ao anterior.
Por exemplo,

@example
block (integrate (x^5, x), ev (%%, x=2) - ev (%%, x=1));
block ([prev], prev: integrate (x^5, x), ev (prev, x=2) - ev (prev, x=1));
@end example

retornam o mesmo resultado, a saber @code{21/2}.

Uma declara@,{c}@~ao composta pode compreender outras declara@,{c}@~oes compostas.
Pode uma declara@,{c}@~ao ser simples ou composta, 
@code{%%} @'e o valor da declara@,{c}@~ao anterior.
Por exemplo,

@example
block (block (a^n, %%*42), %%/6)
@end example

retorna @code{7*a^n}.

Dentro da declara@,{c}@~ao composta, o valor de @code{%%} pode ser inspecionado em uma parada de linha de comando,
que @'e aberta pela execu@,{c}@~ao da fun@,{c}@~ao @code{break}.
Por exemplo, na parada de linha de comando aberta por

@example
block (a: 42, break ())$
@end example

digitando @code{%%;} retorna @code{42}.

Na primeira declara@,{c}@~ao em uma declara@,{c}@~ao composta,
ou fora de uma declara@,{c}@~ao composta,
@code{%%} @'e indefinido.

@code{%%}  reconhecido por @code{batch} e @code{load},
e possem o mesmo significao que na linha de comando interativa.

Veja tamb@'em @code{%}.

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} %edispflag
Valor padr@~ao: @code{false}

Quando @code{%edispflag} for @code{true},
Maxima mostra @code{%e} para um expoente negativo como um quociente.
Por exemplo, @code{%e^-x} @'e mostrado como @code{1/%e^x}.

@end defvr

@deffn {Fun@,{c}@~ao} %th (@var{i})
@ifinfo
@fnindex N'@'esima sa@'ida anterior
@end ifinfo
O valor da @var{i}'@'esima express@~ao pr@'evia de sa@'ida.
Isto @'e, se a pr@'oxima express@~ao a ser calculada for a @var{n}'@'esima sa@'ida,
@code{%th (@var{m})} ser@'a a (@var{n} - @var{m})'@'esima sa@'ida.

@code{%th} @'e @'util em arquivos @code{batch} ou para referir-se a um grupo de express@~oes de sa@'ida.
Por exemplo,

@example
block (s: 0, for i:1 thru 10 do s: s + %th (i))$
@end example

atribui @`a vari@'avel @code{s} a soma das @'ultimas dez express@~oes de sa@'ida.

@code{%th} @'e reconhecido por @code{batch} e @code{load}.
Em um arquivo processado por @code{batch},
@code{%th} possue o mesmo significado que na linha de comando interativa.
Em um arquivo processado por @code{load},
@code{%th} refere-se a express@~oes de sa@'ida mais recentemente calculadas na linha de comando interativa
ou em um arquivo de lote;
@code{%th} n@~ao se refere a express@~oes de sa@'ida no arquivo que est@'a sendo processado.

Veja tamb@'em @code{%}.

@end deffn

@deffn {S@'imbolo especial} ?
@ifinfo
@fnindex Consulta documenta@,{c}@~ao
@end ifinfo
Como prefixo para uma fun@,{c}@~ao ou nome de vari@'avel, @code{?} significa que
o nome @'e um nome Lisp, n@~ao um nome Maxima.
Por exemplo, @code{?round} significa a fun@,{c}@~ao Lisp @code{ROUND}.
Veja @ref{Lisp e Maxima} para mais sobre esse ponto.

A nota@,{c}@~ao @code{? palavra} (um ponto de interroga@,{c}@~ao seguido de uma palavra e separado desta por um espa@,{c}o em branco)
@'e equivalente a @code{describe("palavra")}.
O ponto de interroga@,{c}@~ao deve aparecer no in@'icio de uma linha de entrada;
de outra forma o ponto de interroga@,{c}@~ao n@~ao @'e reconhecido com um pedido de documenta@,{c}@~ao.

@end deffn

@deffn {S@'imbolo especial} ?!
@ifinfo
@fnindex Consulta documenta@,{c}@~ao (busca exata)
@end ifinfo

A nota@,{c}@~ao @code{?! palavra} (@code{?!} seguido de um espa@,{c}o em branco e uma palavra)
@'e equivalente a @code{describe("palavra", exact)}.
O ponto de interroga@,{c}@~ao deve ocorrer no in@'icio de uma linha de entrada;
de outra forma n@~ao @'e reconhecido com um pedido de documenta@,{c}@~ao.

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} absboxchar
Valor padr@~ao: @code{!}

@code{absboxchar} @'e o caracter usado para para desenhar o sinal de valor
absoluto em torno de express@~oes que s@~ao maiores que uma linha de altura.

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} file_output_append
Valor padr@~ao: @code{false}

@code{file_output_append} governa se fun@value{cedilha}@~oes de sa@'ida de arquivo
anexam ao final ou truncam seu arquivo de sa@'ida.
Quando @code{file_output_append} for @code{true},
tais fun@value{cedilha}@~oes anexam ao final de seu arquivo de sa@'ida.
De outra forma, o arquivo de sa@'ida @'e truncado.

@code{save}, @code{stringout}, e @code{with_stdout} respeitam @code{file_output_append}.
Outras fun@value{cedilha}@~oes que escrevem arquivos de sa@'ida n@~ao respeitam @code{file_output_append}.
Em particular, montagem de gr@'aficos e tradu@value{cedilha}@~oes de fun@value{cedilha}@~oes sempre truncam seu arquivo de sa@'ida,
e @code{tex} e @code{appendfile} sempre anexam ao final.
@c WHAT ABOUT WRITEFILE ??

@end defvr

@deffn {Fun@,{c}@~ao} appendfile (@var{nomearquivo})
Adiciona ao final de @var{nomearquivo} uma transcri@,{c}@~ao do console.
@code{appendfile} @'e o mesmo que @code{writefile}, 
exceto que o arquivo transcrito, se j@'a existe, ter@'a sempre alguma coisa adicionada ao seu final.

@code{closefile} fecha o arquivo transcrito que foi aberto anteriormente por @code{appendfile} ou por @code{writefile}.

@end deffn

@c batch CAN TAKE 'test AS AN OPTIONAL ARGUMENT IN WHICH CASE IT CALLS test-batch
@c (SAME AS run_testsuite) -- SHOULD DOCUMENT batch (file, 'test)
@c FIX BUG WHICH CAUSES batch (<file>, 'test) TO FAIL, THEN DOCUMENT IT HERE
@deffn {Fun@,{c}@~ao} batch (@var{nomearquivo})
L@^e express@~oes Maxima do arquivo @var{nomearquivo} e as avalia.
@code{batch} procura pelo arquivo @var{nomearquivo} na lista @code{file_search_maxima}.
Veja @code{file_search}.

@var{nomearquivo} compreende uma seq@"u@^encia de express@~oes Maxima,
cada uma terminada com @code{;} ou @code{$}.
A var@'avel especial @code{%} e a fun@,{c}@~ao @code{%th}
referem-se a resultados pr@'evios dentro do arquivo.
O arquivo pode incluir constru@,{c}@~oes @code{:lisp}.
Espa@,{c}os, tabula@,{c}@~oes, e o caracter de nova linha no arquivo ser@~ao ignorados.
um arquivo de entrada conveniente pode ser criado por um editor de texto ou pela fun@,{c}@~ao @code{stringout}.

@code{batch} l@^e cada express@~ao de entrada de @var{nomearquivo},
mostra a entrada para o console, 
calcula a correspondente express@~ao de sa@'ida,
e mostra a express@~ao de sa@'ida.
R@'otulos de entrada s@~ao atribu@'idos para express@~oes de entrada
e r@'otulos de sa@'ida s@~ao atribu@'idos para express@~oes de sa@'ida.
@code{batch} avalia toda express@~ao de entrada no arquivo
a menos que exista um erro.
Se uma entrada de usu@'ario for requisitada (por @code{asksign} ou por @code{askinteger}, por exemplo)
@code{batch} interrompe para coletar a entrada requisitada e ent@~ao continua.

@c CTRL-C BREAKS batch IN CMUCL, BUT CLISP (ALTHO IT SHOWS "User break") KEEPS GOING !!!
@c DON'T KNOW ABOUT GCL !!!
O recurso de requisi@,{c}@~ao de entrada ao usu@'ario possibilita interromper @code{batch} pela digita@,{c}@~ao de @code{control-C} no console.
O efeito de @code{control-C} depende da subjacente implementa@,{c}@~ao do Lisp.

@code{batch} tem muitos usos,
tais como fornecer um reservat@'orio para trabalhar linhas de comando,
para fornecer demonstra@,{c}@~oes livres de erros,
ou para ajudar a organizar alguma coisa na solu@,{c}@~ao de problemas complexos.

@code{batch} avalia seu argumento.
@c LACK OF A RETURN VALUE IS A BUG; THE INTENT IS TO RETURN THE FILE PATH, TO JUDGE BY THE SOURCE CODE
@code{batch} n@~ao possui valor de retorno.

Veja tamb@'em @code{load}, @code{batchload}, e @code{demo}.

@end deffn

@c RECOMMEND CUTTING THIS ITEM, AS THE load SUBSUMES FUNCTIONALITY OF batchload
@deffn {Fun@,{c}@~ao} batchload (@var{nomearquivo})
L@^e express@~oes Maxima de @var{nomearquivo} e as avalia,
sem mostrar a entrada ou express@~oes de sa@'ida
e sem atribuir r@'otulos para express@~oes de sa@'ida.
Sa@'idas impressas (tais como produzidas por @code{print} ou @code{describe})
s@~ao mostradas, todavia.

A vari@'avel especial @code{%} e a fun@,{c}@~ao @code{%th}
referem-se a resultados anteriores do interpretador interativo,
n@~ao a resultados dentro do arquivo.
O arquivo n@~ao pode incluir constru@,{c}@~oes @code{:lisp}.

@code{batchload} retorna o caminho de @var{nomearquivo}, como uma seq@"u@^encia de caracteres.
@code{batchload} avalia seu argumento.

Veja tamb@'em @code{batch} e @code{load}.
@c batchload APPEARS TO HAVE THE SAME EFFECT AS load.  WHY NOT GET RID OF batchload ???

@end deffn

@deffn {Fun@,{c}@~ao} closefile ()
Fecha o arquivo transcrito aberto por @code{writefile} ou @code{appendfile}.

@end deffn

@c NEEDS CLARIFICATION !!!
@deffn {Fun@,{c}@~ao} collapse (@var{expr})
Reduz @var{expr} fazendo com que todas as suas
subexpress@~oes comuns (i.e., iguais)  sejam compartilhadas (i.e., usam a mesma c@'elulas),
dessa forma economizando espa@,{c}o.  (@code{collapse} @'e uma subrotina usada pelo comando
@code{optimize}.)  Dessa forma, chamar @code{collapse} pode ser @'util
ap@'os um @code{save} arquivo.  Voc@^e pode diminuir muitas express@~oes
juntas pelo uso de @code{collapse ([@var{expr_1}, ..., @var{expr_n}])}.  Similarmente, voc@^e pode
diminuir os elementos de um array @code{A} fazendo
@code{collapse (listarray ('A))}.

@end deffn

@deffn {Fun@,{c}@~ao} concat (@var{arg_1}, @var{arg_2}, ...)
Concatena seus argumentos.
Os argumentos devem obrigat@'oriamente serem avaliados para atomos.
O valor de retorno ou @'e um s@'imbolo se o primeiro argumento for um s@'imbolo
ou @'e uma seq@"u@^encia de caracteres no formato do Maxima em caso contr@'ario.

@code{concat} avalia seus argumentos.
O ap@'ostrofo @code{'} evita avalia@,{c}@~ao.

@example
(%i1) y: 7$
(%i2) z: 88$
(%i3) concat (y, z/2);
(%o3)                          744
(%i4) concat ('y, z/2);
(%o4)                          y44
@end example

Um s@'imbolo constru@'ido por @code{concat} pode
ser atribu@'ido a um valor e aparecer em express@~oes.
O operador de atribui@,{c}@~ao @code{::} (duplo dois pontos) avalia seu lado esquerdo.

@example
(%i5) a: concat ('y, z/2);
(%o5)                          y44
(%i6) a:: 123;
(%o6)                          123
(%i7) y44;
(%o7)                          123
(%i8) b^a;
                               y44
(%o8)                         b
(%i9) %, numer;
                               123
(%o9)                         b
@end example

Note que embora @code{concat (1, 2)} seja visto como n@'umeros no console, na realidade @'e uma seq@"u@^encia de caracteres no formato do Maxima.

@example
(%i10) concat (1, 2) + 3;
(%o10)                       12 + 3
@end example

@end deffn

@deffn {Fun@,{c}@~ao} sconcat (@var{arg_1}, @var{arg_2}, ...)

Concatena seus argumentos em uma seq@"u@^encia de caracteres.
Ao contr@'ario de @code{concat}, os argumentos arrumados @i{n@~ao} precisam ser at@^omicos.

O resultado @'e uma seq@"u@^encia de caracteres no format do Lisp.
@c THAT'S ODD; WHY NOT A MAXIMA STRING ??

@example
(%i1) sconcat ("xx[", 3, "]:", expand ((x+y)^3));
(%o1)               xx[3]:y^3+3*x*y^2+3*x^2*y+x^3
@end example

@end deffn

@c AFTER REVIEWING src/displa.lisp, IT LOOKS LIKE THIS VARIABLE HAS NO EFFECT
@c CUT IT ON THE NEXT PASS
@c @defvar cursordisp
@c Default value: @code{true}
@c 
@c When @code{cursordisp} is @code{true}, express@~oes are drawn by
@c the displayer in logical sequence.  This only works with a console
@c which can do cursor movement.  If @code{false}, express@~oes are
@c printed line by line.
@c 
@c @code{cursordisp} is always @code{false} when a @code{writefile} is in
@c effect.
@c 
@c @end defvar

@c REPHRASE, NEEDS EXAMPLES
@deffn {Fun@,{c}@~ao} disp (@var{expr_1}, @var{expr_2}, ...)
@'e como @code{display} mas somente os valores dos
argumentos s@~ao mostrados em lugar de equa@,{c}@~oes.  A fun@,{c}@~ao @code{disp} @'e @'util para
argumentos complicados que n@~ao possuem nomes ou onde somente o valor
do argumento @'e de interesse e n@~ao o nome.

@end deffn

@c HMM, THIS NEXT ITEM IS DEFINED IN A SHARE FILE (itensor.lisp); 
@c DOES ITS DESCRIPTION WANT TO BE ELSEWHERE ???
@deffn {Fun@,{c}@~ao} dispcon (@var{tensor_1}, @var{tensor_2}, ...)
@deffnx {Fun@,{c}@~ao} dispcon (all)
Mostra as propriedades de contra@,{c}@~ao de
seus argumentos da forma que foram dadas para @code{defcon}.  @code{dispcon (all)} mostra todas as
propriedades de contra@,{c}@~ao que foram definidas.

@end deffn

@c REPHRASE, MORE EXAMPLES
@deffn {Fun@,{c}@~ao} display (@var{expr_1}, @var{expr_2}, ...)
Mostra equa@,{c}@~oes cujo lado esquerdo @'e
@var{expr_i} n@~ao avaliado, e cujo lado direito @'e o valor da express@~ao
centrada na linha.  Essa fun@,{c}@~ao @'e @'util em blocos e em @code{for}
declara@,{c}@~oes com o objetivo de ter resultados intermedi@'arios mostrados.  Os
argumentos para @code{display} s@~ao usualmente @'atomos, vari@'aveis com subscritos, ou
chamadas de fun@,{c}@~ao.  Veja tamb@'em @code{disp}.

@example
(%i1) display(B[1,2]);
                                      2
                         B     = X - X
                          1, 2
(%o1)                            done
@end example

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} display2d
Valor padr@~ao: @code{true}

Quando @code{display2d} @'e @code{false},
O console visualizador @'e unidimensional ao inv@'es de bidimensional.

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} display_format_internal
Valor padr@~ao: @code{false}

Quando @code{display_format_internal} @'e @code{true},
express@~oes s@~ao mostradas sem ser por caminhos que
escondam a representa@,{c}@~ao matem@'atica interna.  O visualizador ent@~ao
corresponde ao que @code{inpart} retorna em lugar de @code{part}.

Exemplos:

@example
User     part       inpart
a-b;      A - B     A + (- 1) B

           A            - 1
a/b;       -         A B
           B
                       1/2
sqrt(x);   sqrt(X)    X

          4 X        4
X*4/3;    ---        - X
           3         3
@end example

@end defvr

@c IS THIS FUNCTION STILL USEFUL ???
@c REPHRASE, NEEDS EXAMPLES
@deffn {Fun@,{c}@~ao} dispterms (@var{expr})
Mostra @var{expr} em partes uma abaixo da outra.
Isto @'e, primeiro o operador de @var{expr} @'e mostrado, ent@~ao cada parcela em
uma adi@,{c}@~ao, ou fatores em um produto, ou parte de uma express@~ao mais geral @'e
mostrado separadamente.  Isso @'e @'util se @var{expr} @'e muito larga para ser
mostrada de outra forma.  Por exemplo se @code{P1}, @code{P2}, ...  s@~ao express@~oes
muito largas ent@~ao o programa visualizador pode sair fora do espa@,{c}o de armazenamento na
tentativa de mostrar @code{P1 + P2 + ...}  tudo de uma vez.  Todavia,
@code{dispterms (P1 + P2 + ...)} mostra @code{P1}, ent@~ao abaixo disso @code{P2}, etc.  Quando n@~ao
usando @code{dispterms}, se uma express@~ao exponencial @'e muito alta para ser
mostrada como @code{A^B} isso aparece como @code{expt (A, B)} (ou como @code{ncexpt (A, B)} no
caso de @code{A^^B}).

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} error_size
Valor padr@~ao: 10

@code{error_size} modifica mensagens de erro conforme o tamanho das express@~oes que aparecem nelas.
Se o tamanho de uma express@~ao (como determinado pela fun@,{c}@~ao Lisp @code{ERROR-SIZE})
@'e maior que @code{error_size},
a express@~ao @'e substitu@'ida na mensagem por um s@'imbolo,
e o o s@'imbolo @'e atribu@'ido @`a express@~ao.
Os s@'imbolos s@~ao obtidos da lista @code{error_syms}.

De outra forma, a express@~ao @'e menor que @code{error_size},
e a express@~ao @'e mostrada na mensagem.

Veja tamb@'em @code{error} e @code{error_syms}.

Exemplo:
@c OUTPUT GENERATED BY THE FOLLOWING
@c U: (C^D^E + B + A)/(cos(X-1) + 1)$
@c error_size: 20$
@c error ("Express@~ao exemplo @'e", U);
@c errexp1;
@c error_size: 30$
@c error ("Express@~ao exemplo @'e", U);

O tamanho de @code{U}, como determinado por @code{ERROR-SIZE}, @'e 24.

@example
(%i1) U: (C^D^E + B + A)/(cos(X-1) + 1)$

(%i2) error_size: 20$

(%i3) error ("Express@~ao exemplo @'e", U);

Express@~ao exemplo @'e errexp1
 -- an error.  Quitting.  To debug this try debugmode(true);
(%i4) errexp1;
                            E
                           D
                          C   + B + A
(%o4)                    --------------
                         cos(X - 1) + 1
(%i5) error_size: 30$

(%i6) error ("Express@~ao exemplo @'e", U);

                           E
                          D
                         C   + B + A
Express@~ao exemplo @'e --------------
                        cos(X - 1) + 1
 -- an error.  Quitting.  To debug this try debugmode(true);
@end example

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} error_syms
Valor padr@~ao: @code{[errexp1, errexp2, errexp3]}

Em mensagens de erro,
express@~oes mais largas que @code{error_size} s@~ao substitu@'idas por s@'imbolos, e os
s@'imbolos s@~ao escolhidos para as express@~oes.  Os s@'imbolos s@~ao obtidos da
lista @code{error_syms}.
A primeira express@~ao muito larga @'e substitu@'ida por @code{error_syms[1]},
a segunda por @code{error_syms[2]}, e assim por diante.

Se houverem mais express@~oes muito largas que h@'a elementos em @code{error_syms},
s@'imbolos s@~ao constru@'idos automaticamente,
com o @var{n}-@'esimo s@'imbolo equivalente a @code{concat ('errexp, @var{n})}.

Veja tamb@'em @code{error} e @code{error_size}.

@end defvr

@deffn {Fun@,{c}@~ao} expt (@var{a}, @var{b})
Se uma express@~ao exponencial @'e muito alta para ser mostrada
cmo @code{@var{a}^@var{b}} isso aparece como @code{expt (@var{a}, @var{b})} (ou como @code{ncexpt (@var{a}, @var{b})} no caso de
@code{@var{a}^^@var{b}}).

@c THIS SEEMS LIKE A BUG TO ME.  expt, ncexpt SHOULD BE RECOGNIZED SINCE MAXIMA
@c ITSELF PRINTS THEM SOMETIMES.  THESE SHOULD JUST SIMPLIFY TO ^ AND ^^, RESPECTIVELY.
@code{expt} e @code{ncexpt} n@~ao s@~ao reconhecidas em entradas.

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} exptdispflag
Valor padr@~ao: @code{true}

Quando @code{exptdispflag} @'e @code{true}, Maxima mostra express@~oes
com expoente negativo usando quocientes, e.g., @code{X^(-1)} como @code{1/X}.

@end defvr

@c NEEDS EXAMPLES
@deffn {Fun@,{c}@~ao} filename_merge (@var{path}, @var{nomearquivo})
Constroem um caminho modificado de @var{path} e @var{nomearquivo}.
Se o componente final de @var{path} @'e da forma @code{###.@var{algumacoisa}},
o componente @'e substitu@'ido com @code{@var{nomearquivo}.@var{algumacoisa}}.
De outra forma, o componente final @'e simplesmente substitu@'ido por @var{nomearquivo}.

@c SAY SOMETHING ABOUT ARG TYPE -- LISP STRINGS WORK BETTER THAN MAXIMA STRINGS
@c SAY SOMETHING ABOUT RETURN TYPE
@end deffn

@deffn {Fun@,{c}@~ao} file_search (@var{nomearquivo})
@deffnx {Fun@,{c}@~ao} file_search (@var{nomearquivo}, @var{listacaminho})

@code{file_search} procura pelo arquivo @var{nomearquivo} e retorna o caminho para o arquivo
(como uma seq@"u@^encia de caracteres) se ele for achado; de outra forma @code{file_search} retorna @code{false}.
@code{file_search (@var{nomearquivo})} procura nos diret@'orios padr@~oes de busca,
que s@~ao especificados pelas vari@'aveis @code{file_search_maxima}, @code{file_search_lisp}, e @code{file_search_demo}.

@code{file_search} primeiro verifica se o nome atual passado existe,
antes de tentar coincidir esse nome atual com o modelo ``coringa'' de busca do arquivo.
Veja @code{file_search_maxima} concernente a modelos de busca de arquivos.

O argumento @var{nomearquivo} pode ser um caminho e nome de arquivo,
ou apenas um nome de arquivo, ou, se um diret@'orio de busca de arquivo inclui um modelo de busca de arquivo,
apenas a base do nome de arquivo (sem uma extens@~ao).
Por exemplo,

@example
file_search ("/home/wfs/special/zeta.mac");
file_search ("zeta.mac");
file_search ("zeta");
@end example

todos acham o mesmo arquivo, assumindo que o arquivo exista e @code{/home/wfs/special/###.mac}
est@'a em @code{file_search_maxima}.

@code{file_search (@var{nomearquivo}, @var{listacaminho})} procura somente nesses diret@'orios
especificados por @var{listacaminho},
que @'e uma lista de seq@"u@^encias de caracteres.
O argumento @var{listacaminho} substitui os diret@'orios de busca padr@~ao,
ent@~ao se a lista do caminho @'e dada, @code{file_search} procura somente nesses especificados,
e n@~ao qualquer dos diret@'orios padr@~ao de busca.
Mesmo se existe somente um diret@'orio em @var{listacaminho}, esse deve ainda ser dado como uma lista de um @'unico elemento.

O usu@'ario pode modificar o diret@'orio de busca padr@~ao.  Veja @code{file_search_maxima}.

@code{file_search} @'e invocado por @code{load} com @code{file_search_maxima} e @code{file_search_lisp}
como diret@'orios de busca.

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} file_search_maxima
@defvrx {Vari@'avel de op@,{c}@~ao} file_search_lisp
@defvrx {Vari@'avel de op@,{c}@~ao} file_search_demo
Essas vari@'aveis especificam listas de diret@'orios a serem procurados
por @code{load}, @code{demo}, e algumas outras fun@,{c}@~oes do Maxima.
O valor padr@~ao dessas vari@'aveis
nomeia v@'arios diret@'orios na instala@,{c}@~a padr@~ao do Maxima.

O usu@'aro pode modificar essas vari@'aveis,
quer substituindo os valores padr@~ao ou colocando no final diret@'orios adicionais.
Por exemplo,

@example
file_search_maxima: ["/usr/local/foo/###.mac",
    "/usr/local/bar/###.mac"]$
@end example

substitui o valor padr@~ao de @code{file_search_maxima},
enquanto

@example
file_search_maxima: append (file_search_maxima,
    ["/usr/local/foo/###.mac", "/usr/local/bar/###.mac"])$
@end example

adiciona no final da lista dois diret@'orios adicionais.
Isso pode ser conveniente para colocar assim uma express@~ao no arquivo @code{maxima-init.mac}
de forma que o caminho de busca de arquivo @'e atribu@'ido automaticamente quando o Maxima inicia.

Multiplas extens@~oes de arquivo e e multiplos caminhos podem ser especificados por
constru@,{c}@~oes ``coringa'' especiais.
A seq@"u@^encia de caracteres @code{###} expande a busca para al@'em do nome b@'asico,
enquanto uma lista separada por v@'irgulas e entre chaves @code{@{foo,bar,baz@}} expande
em multiplas seq@"u@^encias de caracteres.
Por exemplo, supondo que o nome b@'asico a ser procurado seja @code{neumann},

@example
"/home/@{wfs,gcj@}/###.@{lisp,mac@}"
@end example

expande em @code{/home/wfs/neumann.lisp}, @code{/home/gcj/neumann.lisp}, @code{/home/wfs/neumann.mac}, e @code{/home/gcj/neumann.mac}.

@end defvr

@deffn {Fun@,{c}@~ao} file_type (@var{nomearquivo})
Retorna uma suposta informa@,{c}@~ao sobre o conte@'udo de @var{nomearquivo},
baseada na extens@~ao do arquivo.
@var{nomearquivo} n@~ao precisa referir-se a um arquivo atual;
nenhuma tentativa @'e feita para abrir o arquivo e inspecionar seu conte@'udo.

O valor de retorno @'e um s@'imbolo, qualquer um entre @code{object}, @code{lisp}, ou @code{maxima}.
Se a extens@~ao come@,{c}a com @code{m} ou @code{d}, @code{file_type} retorna @code{maxima}.
Se a extens@~ao come@,{c}a om @code{l}, @code{file_type} retorna @code{lisp}.
Se nenhum dos acima, @code{file_type} retorna @code{object}.

@end deffn

@deffn {Fun@,{c}@~ao} grind (@var{expr})
@deffnx {Vari@'avel de op@,{c}@~ao} grind
A fun@,{c}@~ao @code{grind} imprime @var{expr}
para o console em uma forma adequada de entrada para Maxima.
@code{grind} sempre retorna @code{done}.

Quando @var{expr} for um nome de uma fun@,{c}@~ao ou o nome de uma macro,
@code{grind} mostra na tela a defini@,{c}@~ao da fun@,{c}@~ao ou da macro em lugar de apenas o nome.

Veja tamb@'em @code{string}, que retorna uma seq@"u@^encia de caracteres em lugar de imprimir sua sa@'ida.
@code{grind} tenta imprimir a express@~ao de uma maneira que a faz
levemente mais f@'acil para ler que a sa@'ida de @code{string}.

Quando a vari@'avel @code{grind} @'e @code{true},
a sa@'ida de @code{string} e @code{stringout} tem o mesmo formato que @code{grind};
de outra forma nenhuma tentativa @'e feita para formatar especialmente a sa@'ida dessas fun@,{c}@~oes.
O valor padr@~ao da vari@'avel @code{grind} @'e @code{false}.

@code{grind} pode tamb@'em ser especificado como um argumento de @code{playback}.
Quando @code{grind} est@'a presente,
@code{playback} imprime express@~oes de entrada no mesmo formato que a fun@,{c}@~ao @code{grind}.
De outra forma, nenhuma tentativa @'e feita para formatar especialmente as express@~oes de entrada.
@code{grind} avalia seus argumentos.

Exemplos:

@c ===beg===
@c aa + 1729;
@c grind (%);
@c [aa, 1729, aa + 1729];
@c grind (%);
@c matrix ([aa, 17], [29, bb]);
@c grind (%);
@c set (aa, 17, 29, bb);
@c grind (%);
@c exp (aa / (bb + 17)^29);
@c grind (%);
@c expr: expand ((aa + bb)^10);
@c grind (expr);
@c string (expr);
@c cholesky (A):= block ([n : length (A), L : copymatrix (A),
@c p : makelist (0, i, 1, length (A))], for i thru n do for j : i thru n do
@c (x : L[i, j], x : x - sum (L[j, k] * L[i, k], k, 1, i - 1), if i = j then
@c p[i] : 1 / sqrt(x) else L[j, i] : x * p[i]), for i thru n do L[i, i] : 1 / p[i],
@c for i thru n do for j : i + 1 thru n do L[i, j] : 0, L)$
@c grind (cholesky);
@c string (fundef (cholesky));
@c ===end===
@example
(%i1) aa + 1729;
(%o1)                       aa + 1729
(%i2) grind (%);
aa+1729$
(%o2)                         done
(%i3) [aa, 1729, aa + 1729];
(%o3)                 [aa, 1729, aa + 1729]
(%i4) grind (%);
[aa,1729,aa+1729]$
(%o4)                         done
(%i5) matrix ([aa, 17], [29, bb]);
                           [ aa  17 ]
(%o5)                      [        ]
                           [ 29  bb ]
(%i6) grind (%);
matrix([aa,17],[29,bb])$
(%o6)                         done
(%i7) set (aa, 17, 29, bb);
(%o7)                   @{17, 29, aa, bb@}
(%i8) grind (%);
@{17,29,aa,bb@}$
(%o8)                         done
(%i9) exp (aa / (bb + 17)^29);
                                aa
                            -----------
                                     29
                            (bb + 17)
(%o9)                     %e
(%i10) grind (%);
%e^(aa/(bb+17)^29)$
(%o10)                        done
(%i11) expr: expand ((aa + bb)^10);
         10           9        2   8         3   7         4   6
(%o11) bb   + 10 aa bb  + 45 aa  bb  + 120 aa  bb  + 210 aa  bb
         5   5         6   4         7   3        8   2
 + 252 aa  bb  + 210 aa  bb  + 120 aa  bb  + 45 aa  bb
        9        10
 + 10 aa  bb + aa
(%i12) grind (expr);
bb^10+10*aa*bb^9+45*aa^2*bb^8+120*aa^3*bb^7+210*aa^4*bb^6
     +252*aa^5*bb^5+210*aa^6*bb^4+120*aa^7*bb^3+45*aa^8*bb^2
     +10*aa^9*bb+aa^10$
(%o12)                        done
(%i13) string (expr);
(%o13) bb^10+10*aa*bb^9+45*aa^2*bb^8+120*aa^3*bb^7+210*aa^4*bb^6\
+252*aa^5*bb^5+210*aa^6*bb^4+120*aa^7*bb^3+45*aa^8*bb^2+10*aa^9*\
bb+aa^10
(%i14) cholesky (A):= block ([n : length (A), L : copymatrix (A),
p : makelist (0, i, 1, length (A))], for i thru n do for j : i thru n do
(x : L[i, j], x : x - sum (L[j, k] * L[i, k], k, 1, i - 1), if i = j then
p[i] : 1 / sqrt(x) else L[j, i] : x * p[i]), for i thru n do L[i, i] : 1 / p[i],
for i thru n do for j : i + 1 thru n do L[i, j] : 0, L)$
(%i15) grind (cholesky);
cholesky(A):=block(
         [n:length(A),L:copymatrix(A),
          p:makelist(0,i,1,length(A))],
         for i thru n do
             (for j from i thru n do
                  (x:L[i,j],x:x-sum(L[j,k]*L[i,k],k,1,i-1),
                   if i = j then p[i]:1/sqrt(x)
                       else L[j,i]:x*p[i])),
         for i thru n do L[i,i]:1/p[i],
         for i thru n do (for j from i+1 thru n do L[i,j]:0),L)$
(%o15)                        done
(%i16) string (fundef (cholesky));
(%o16) cholesky(A):=block([n:length(A),L:copymatrix(A),p:makelis\
t(0,i,1,length(A))],for i thru n do (for j from i thru n do (x:L\
[i,j],x:x-sum(L[j,k]*L[i,k],k,1,i-1),if i = j then p[i]:1/sqrt(x\
) else L[j,i]:x*p[i])),for i thru n do L[i,i]:1/p[i],for i thru \
n do (for j from i+1 thru n do L[i,j]:0),L)
@end example

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} ibase
Valor padr@~ao: 10

Inteiros fornecidos dentro do Maxima s@~ao interpretados
com respeito @`a base @code{ibase}.

A @code{ibase} pode ser atribu@'ido qualquer inteiro entre 2 e 35 (decimal), inclusive.
@c WHY NOT 36, BY THE WAY ??
Quando @code{ibase} @'e maior que 10, os numerais compreendem aos numerais decimais de 0 at@'e 9
@c UPPERCASE/LOWERCASE DISTINCTION HERE ??
mais as letras mai@'usculas do alfabeto A, B, C, ..., como necess@'ario.
Os numerais para a base 35, a maior base aceit@'avel,
compreendem de 0 at@'e 9 e de A at@'e Y.
@c HOW, EXACTLY, DOES ONE TYPE IN THE LETTERS ??
@c ibase: 11$ 1A; YIELDS AN ERROR
@c \1A; #1A; \#1A; DON'T WORK EITHER

Veja tamb@'em @code{obase}.

@c NEED EXAMPLES HERE
@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} inchar
Valor padr@~ao: @code{%i}

@code{inchar} @'e o prefixo dos r@'otulos de express@~oes fornecidas pelo usu@'ario.
Maxima automaticamente constr@'oi um r@'otulo para cada express@~ao de entrada
por concatena@,{c}@~ao de @code{inchar} e @code{linenum}.
A @code{inchar} pode ser atribu@'ido qualquer seq@"u@^encia de caracteres ou s@'imbolo, n@~ao necessariamente um caracter simples.

@example
(%i1) inchar: "input";
(%o1)                                input
(input1) expand ((a+b)^3);
                            3        2      2      3
(%o1)                      b  + 3 a b  + 3 a  b + a
(input2)
@end example

Veja tamb@'em @code{labels}.

@end defvr

@deffn {Fun@,{c}@~ao} ldisp (@var{expr_1}, ..., @var{expr_n})
Mostra express@~oes @var{expr_1}, ..., @var{expr_n} para o console
como sa@'ida impressa na tela.
@code{ldisp} atribue um r@'otulo de express@~ao intermedi@'aria a cada argumento
e retorna a lista de r@'otulos.

Veja tamb@'em @code{disp}.

@example
(%i1) e: (a+b)^3;
                                   3
(%o1)                       (b + a)
(%i2) f: expand (e);
                     3        2      2      3
(%o2)               b  + 3 a b  + 3 a  b + a
(%i3) ldisp (e, f);
                                   3
(%t3)                       (b + a)

                     3        2      2      3
(%t4)               b  + 3 a b  + 3 a  b + a

(%o4)                      [%t3, %t4]
(%i4) %t3;
                                   3
(%o4)                       (b + a)
(%i5) %t4;
                     3        2      2      3
(%o5)               b  + 3 a b  + 3 a  b + a
@end example

@end deffn

@deffn {Fun@,{c}@~ao} ldisplay (@var{expr_1}, ..., @var{expr_n})
Mostra express@~oes @var{expr_1}, ..., @var{expr_n} para o console
como sa@'ida impressa na tela.
Cada express@~ao @'e impressa como uma equa@,{c}@~ao da forma @code{lhs = rhs}
na qual @code{lhs} @'e um dos argumentos de @code{ldisplay}
e @code{rhs} @'e seu valor.
Tipicamente cada argumento @'e uma vari@'avel.
@code{ldisp} atribui um r@'otulo de express@~ao intermedi@'aia a cada equa@,{c}@~ao
e retorna a lista de r@'otulos.

Veja tamb@'em @code{display}.

@example
(%i1) e: (a+b)^3;
                                   3
(%o1)                       (b + a)
(%i2) f: expand (e);
                     3        2      2      3
(%o2)               b  + 3 a b  + 3 a  b + a
(%i3) ldisplay (e, f);
                                     3
(%t3)                     e = (b + a)

                       3        2      2      3
(%t4)             f = b  + 3 a b  + 3 a  b + a

(%o4)                      [%t3, %t4]
(%i4) %t3;
                                     3
(%o4)                     e = (b + a)
(%i5) %t4;
                       3        2      2      3
(%o5)             f = b  + 3 a b  + 3 a  b + a
@end example

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} linechar
Valor padr@~ao: @code{%t}

@code{linechar} @'e o refixo de r@'otulos de express@~oes intermedi@'arias gerados pelo Maxima.
Maxima constr@'oi um r@'otulo para cada express@~ao intermedi@'aria (se for mostrada)
pela concatena@,{c}@~ao de @code{linechar} e @code{linenum}.
A @code{linechar} pode ser atribu@'ido qualquer seq@"u@^encia de caracteres ou s@'imbolo, n@~ao necess@'ariamente um caractere simples.

Express@~oes intermedi@'arias podem ou n@~ao serem mostradas.
See @code{programmode} e @code{labels}.

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} linel
Valor padr@~ao: 79

@code{linel} @'e a largura assumida (em caracteres) do console
para o prop@'osito de mostrar express@~oes.
A @code{linel} pode ser atribu@'ido qualquer valor pelo usu@'ario,
embora valores muio pequenos ou muito grandes possam ser impratic@'aveis.
Textos impressos por fun@,{c}@~oes internas do Maxima, tais como mensagens de erro e a sa@'ida de @code{describe},
n@~ao s@~ao afetadas por @code{linel}.

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} lispdisp
Valor padr@~ao: @code{false}

Quando @code{lispdisp} for @code{true},
s@'imbolos Lisp s@~ao mostrados com um ponto de interroga@,{c}@~ao @code{?} na frente.
De outra forma,
s@'imbolos Lisp ser@~ao mostrados sem o ponto de interroga@,{c}@~o na frente.

Exemplos:

@c ===beg===
@c lispdisp: false$
@c ?foo + ?bar;
@c lispdisp: true$
@c ?foo + ?bar;
@c ===end===
@example
(%i1) lispdisp: false$
(%i2) ?foo + ?bar;
(%o2)                       foo + bar
(%i3) lispdisp: true$
(%i4) ?foo + ?bar;
(%o4)                      ?foo + ?bar
@end example

@end defvr

@deffn {Fun@,{c}@~ao} load (@var{nomearquivo})
Avalia express@~oes em @var{nomearquivo}, 
dessa forma conduzindo vari@'aveis, fun@,{c}@~oes, e outros objetos dentro do Maxima.
A associa@,{c}@~ao de qualquer objeto existente @'e substitu@'ida pela associa@,{c}@~ao recuperada de @var{nomearquivo}.  
Para achar o arquivo,
@code{load} chama @code{file_search} com @code{file_search_maxima} e @code{file_search_lisp}
como diret@'orios de busca.
Se @code{load} obt@'em sucesso, isso retorna o nome do arquivo.
De outra forma @code{load} imprime uma mensagem e erro.

@code{load} trabalha igualmente bem para c@'odigos Lisp e c@'odigos Maxima.
Arquivos criados por @code{save}, @code{translate_file}, e @code{compile_file}, que criam c@'odigos Lisp,
e @code{stringout}, que criam c@'odigos Maxima,
podem ser processadas por @code{load}.
@code{load} chama @code{loadfile} para carregar arquivos Lisp e @code{batchload} para carregar arquivos Maxima.

@code{load} n@~ao reconhece constru@,{c}@~oes @code{:lisp} em arquivos do Maxima,
e quando processando @var{nomearquivo},
as vari@'aveis globais @code{_}, @code{__}, @code{%}, e @code{%th} possuem as mesmas associa@,{c}@~oes
que possuiam quando @code{load} foi chamada.

Veja tamb@'em @code{loadfile}, @code{batch}, @code{batchload}, e @code{demo}.
@code{loadfile} processa arquivos Lisp;
@code{batch}, @code{batchload}, e @code{demo} processam arquivos Maxima.

Veja @code{file_search} para mais detalhes sobre o mecanismo de busca de arquivos.

@code{load} avalia seu argumento.

@end deffn

@c RECOMMEND CUTTING THIS ITEM, AS THE load SUBSUMES FUNCTIONALITY OF loadfile
@deffn {Fun@,{c}@~ao} loadfile (@var{nomearquivo})
Avalia express@~oes Lisp em @var{nomearquivo}.
@code{loadfile} n@~ao invoca @code{file_search}, ent@~ao @code{nomearquivo} deve obrigat@'oriamente incluir
a extens@~ao do arquivo e tanto quanto o caminho como necess@'ario para achar o arquivo.

@code{loadfile} pode processar arquivos criados por @code{save}, @code{translate_file}, e @code{compile_file}.
O usu@'ario pode achar isso mais conveniente para usar @code{load} em lugar de @code{loadfile}.

@code{loadfile} avalia seu argumento, ent@~ao @code{nomearquivo} deve obrigat@'oriamente ser uma seq@"u@^encia de caracteres literal,
n@~ao uma vari@'avel do tipo seq@"u@^encia de caracteres.
O operador ap@'ostrofo-ap@'ostrofo @code{'@w{}'} n@~ao aceita avalia@,{c}@~ao.

@end deffn

@c loadprint DOESN'T EXACTLY WORK LIKE THIS, BUT IT HARDLY SEEMS WORTH FIXING
@c I GUESS THIS COULD BE UPDATED TO DESCRIBE THE WAY IT ACTUALLY WORKS
@defvr {Vari@'avel de op@,{c}@~ao} loadprint
Valor padr@~ao: @code{true}

@code{loadprint} diz se deve imprimir uma mensagem quando um arquivo @'e chamado.

@itemize @bullet
@item
Quando @code{loadprint} @'e @code{true}, sempre imprime uma mensagem.
@item
Quando @code{loadprint} @'e @code{'loadfile}, imprime uma mensagem somente se
um arquivo @'e chamado pela fun@,{c}@~ao @code{loadfile}.
@item
Quando @code{loadprint} @'e @code{'autoload},
imprime uma mensagem somente se um arquivo @'e automaticamente carregado.
Veja @code{setup_autoload}.
@item
Quando @code{loadprint} @'e @code{false}, nunca imprime uma mensagem.
@end itemize

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} obase
Valor padr@~ao: 10

@code{obase} @'e a base para inteiros mostrados pelo Maxima.

A @code{obase} poode ser atribu@'ido qualquer inteiro entre 2 e 35 (decimal), inclusive.
@c WHY NOT 36, BY THE WAY ??
Quando @code{obase} @'e maior que 10, os numerais compreendem os numerais decimais de 0 at@'e 9
e letras mai@'usulas do alfabeto A, B, C, ..., quando necess@'ario.
Os numerais para a base 35, a maior base aceit@'avel,
compreendem de 0 at@'e 9, e de A at@'e Y.

Veja tamb@'em @code{ibase}.

@c NEED EXAMPLES HERE
@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} outchar
Valor padr@~ao: @code{%o}

@code{outchar} @'e o prefixo dos r@'otulos de express@~oes calculadas pelo Maxima.
Maxima automaticamente constr@'oi um r@'otulo para cada express@~ao calculada
pela concatena@,{c}@~ao de @code{outchar} e @code{linenum}.
A @code{outchar} pode ser atribu@'ido qualquer seq@"u@^encia de caracteres ou s@'imbolo, n@~ao necess@'ariamente um caractere simples.

@example
(%i1) outchar: "output";
(output1)                           output
(%i2) expand ((a+b)^3);
                            3        2      2      3
(output2)                  b  + 3 a b  + 3 a  b + a
(%i3)
@end example

Veja tamb@'em @code{labels}.

@end defvr

@c STILL EXISTS, NEEDS CLARIFICATION !!!
@defvr {Vari@'avel de op@,{c}@~ao} packagefile
Valor padr@~ao: @code{false}

Projetistas de pacotes que usam @code{save}
ou @code{translate} para criar pacotes (arquivos) para outros
usarem podem querer escolher @code{packagefile: true} para prevenir qu informa@,{c}@~oes
sejam acrescentadas @`a lista de informa@,{c}@~oes do Maxima (e.g. @code{values},
@code{fun@,{c}@~oes}) exceto onde necess@'ario quando o arquivo @'e carregado.
Nesse caminho, o conte@'udo do pacote n@~ao pegar@'a no
caminho do usu@'ario quando ele adicionar seus pr@'oprios dados.  Note que isso n@~ao
resolve o problema de poss@'iveis conflitos de nome.  Tamb@'em note que
o sinalizador simplesmente afeta o que @'e sa@'ida para o arquivo pacote.
Escolhendo o sinalizador para @code{true} @'e tamb@'em @'util para criar arquivos de
init do Maxima.

@end defvr

@defvr {Vari@'avel de op@,{c}@~ao} pfeformat
Valor padr@~ao: @code{false}

Quando @code{pfeformat} @'e @code{true}, uma raz@~ao de inteiros @'e
mostrada com o caractere s@'olido (barra normal),
e um denominador inteiro @code{n}
@'e mostrado como um termo multiplicativo em primeiro lugar @code{1/n}.

@example
(%i1) pfeformat: false$
(%i2) 2^16/7^3;
                              65536
(%o2)                         -----
                               343
(%i3) (a+b)/8;
                              b + a
(%o3)                         -----
                                8
(%i4) pfeformat: true$ 
(%i5) 2^16/7^3;
(%o5)                       65536/343
(%i6) (a+b)/8;
(%o6)                      1/8 (b + a)
@end example

@end defvr

@deffn {Fun@,{c}@~ao} print (@var{expr_1}, ..., @var{expr_n})
Avalia e mostra @var{expr_1}, ..., @var{expr_n}
uma ap@'os a outra, da esquerda para a direita,
iniciando no lado esquerdo do console.

O valor retornado por @code{print} @'e o valor de seu @'ultimo argumento.
@code{print} n@~ao gera r@'otulos de express@~ao intermedi@'aria.

Veja tamb@'em @code{display}, @code{disp}, @code{ldisplay}, e @code{ldisp}.
Essas fun@,{c}@~oes mostram uma express@~ao por linha, enquanto @code{print} tenta
mostrar duas ou mais express@~oes por linha.

Para mostrar o conte@'udo de um arquivo, veja @code{printfile}.

@example
(%i1) r: print ("(a+b)^3 is", expand ((a+b)^3), "log (a^10/b) is", radcan (log (a^10/b)))$
            3        2      2      3
(a+b)^3 is b  + 3 a b  + 3 a  b + a  log (a^10/b) is 

                                              10 log(a) - log(b) 
(%i2) r;
(%o2)                  10 log(a) - log(b)
(%i3) disp ("(a+b)^3 is", expand ((a+b)^3), "log (a^10/b) is", radcan (log (a^10/b)))$
                           (a+b)^3 is

                     3        2      2      3
                    b  + 3 a b  + 3 a  b + a

                         log (a^10/b) is

                       10 log(a) - log(b)
@end example

@end deffn

@c RECOMMEND CUTTING THIS ITEM AND CUTTING $tcl_output IN src/plot.lisp
@c THIS REALLY, REALLY SEEMS LIKE CRUFT:
@c $tcl_output IS NEVER CALLED WITHIN MAXIMA SOURCE,
@c AND THIS EXTREMELY NARROW, LIMITED FUNCTIONALITY ISN'T USEFUL TO USERS
@c AND IT'S BROKEN: INCORRECT OUTPUT FOR CLISP, DIES ON GCL
@deffn {Fun@,{c}@~ao} tcl_output (@var{list}, @var{i0}, @var{skip})
@deffnx {Fun@,{c}@~ao} tcl_output (@var{list}, @var{i0})
@deffnx {Fun@,{c}@~ao} tcl_output ([@var{list_1}, ..., @var{list_n}], @var{i})

Imprime os elementos de uma lista entre chaves @code{@{ @}},
conveniente como parte de um programa na linguagem Tcl/Tk.

@code{tcl_output (@var{list}, @var{i0}, @var{skip})}
imprime @var{list}, come@,{c}ando com o elemento @var{i0} e imprimindo elementos
@code{@var{i0} + @var{skip}}, @code{@var{i0} + 2 @var{skip}}, etc.

@code{tcl_output (@var{list}, @var{i0})}
@'e equivalente a @code{tcl_output (@var{list}, @var{i0}, 2)}.

@code{tcl_output ([@var{list_1}, ..., @var{list_n}], @var{i})}
imprime os @var{i}'@'esimos elementos de @var{list_1}, ..., @var{list_n}.

Exemplos:
@c EXAMPLE INPUT
@c tcl_output ([1, 2, 3, 4, 5, 6], 1, 3)$
@c tcl_output ([1, 2, 3, 4, 5, 6], 2, 3)$
@c tcl_output ([3/7, 5/9, 11/13, 13/17], 1)$
@c tcl_output ([x1, y1, x2, y2, x3, y3], 2)$
@c tcl_output ([[1, 2, 3], [11, 22, 33]], 1)$

@c EXAMPLE OUTPUT: CLISP
@c OUTPUT IS OK FOR FIRST TWO, BROKEN FOR OTHERS
@c GCL OUTPUT: SAME FOR FIRST TWO
@c GCL FAILS ON OTHERS (IN EACH CASE COMPLAINING ELEMENTS ARE "not of type (OR RATIONAL LISP:FLOAT)"
@example
(%i1) tcl_output ([1, 2, 3, 4, 5, 6], 1, 3)$

 @{1.000000000     4.000000000     
 @}
(%i2) tcl_output ([1, 2, 3, 4, 5, 6], 2, 3)$

 @{2.000000000     5.000000000     
 @}
(%i3) tcl_output ([3/7, 5/9, 11/13, 13/17], 1)$

 @{((RAT SIMP) 3 7) ((RAT SIMP) 11 13) 
 @}
(%i4) tcl_output ([x1, y1, x2, y2, x3, y3], 2)$

 @{$Y1 $Y2 $Y3 
 @}
(%i5) tcl_output ([[1, 2, 3], [11, 22, 33]], 1)$

 @{SIMP 1.000000000     11.00000000     
 @}
@end example

@end deffn

@deffn {Fun@,{c}@~ao} read (@var{expr_1}, ..., @var{expr_n})
Imprime @var{expr_1}, ..., @var{expr_n}, ent@~ao l@^e uma express@~ao do console
e retorna a express@~ao avaliada.
A express@~ao @'e terminada com um ponto e v@'irgula @code{;} ou o sinal de d@'olar @code{$}.

Veja tamb@'em @code{readonly}.

@example
(%i1) foo: 42$ 
(%i2) foo: read ("foo is", foo, " -- enter new value.")$
foo is 42  -- enter new value.  
(a+b)^3;
(%i3) foo;
                                     3
(%o3)                         (b + a)
@end example

@end deffn

@deffn {Fun@,{c}@~ao} readonly (@var{expr_1}, ..., @var{expr_n})
Imprime @var{expr_1}, ..., @var{expr_n}, ent@~ao l@^e uma express@~ao do console
e retorna a express@~ao (sem avalia@,{c}@~ao).
A express@~ao @'e terminada com um @code{;} (ponto e v@'irgula) ou @code{$} (sinal de d@'olar).

@example
(%i1) aa: 7$
(%i2) foo: readonly ("Forne@,{c}a uma express@~ao:");
Enter an express@~ao: 
2^aa;
                                  aa
(%o2)                            2
(%i3) foo: read ("Forne@,{c}a uma express@~ao:");
Enter an express@~ao: 
2^aa;
(%o3)                            128
@end example

Veja tamb@'em @code{read}.

@end deffn

@deffn {Fun@,{c}@~ao} reveal (@var{expr}, @var{depth})
Substitue partes de @var{expr} no inteiro especificado @var{depth}
com sum@'ario descritivo.

@itemize @bullet
@item
Somas e diferen@,{c}as s@~ao substitu@'idas por @code{sum(@var{n})}
onde @var{n} @'e o n@'umero de operandos do produto.
@item
Produtos s@~ao substitu@'idos por @code{product(@var{n})}
onde @var{n} @'e o n@'umero de operandos da multiplica@,{c}@~ao.
@item
Exponenciais s@~ao substitu@'idos por @code{expt}.  
@item
Quocientes s@~ao substitu@'idos por @code{quotient}.
@item
Nega@,{c}@~ao un@'aria @'e substitu@'ida por @code{negterm}.
@end itemize

Quando @var{depth} @'e maior que ou igual @`a m@'axima intensidade de @var{expr},
@code{reveal (@var{expr}, @var{depth})} retornam @var{expr} sem modifica@,{c}@~oes.

@code{reveal} avalia seus argumentos.
@code{reveal} retorna express@~ao sumarizada.

Exemplo:

@example
(%i1) e: expand ((a - b)^2)/expand ((exp(a) + exp(b))^2);
                          2            2
                         b  - 2 a b + a
(%o1)               -------------------------
                        b + a     2 b     2 a
                    2 %e      + %e    + %e
(%i2) reveal (e, 1);
(%o2)                       quotient
(%i3) reveal (e, 2);
                             sum(3)
(%o3)                        ------
                             sum(3)
(%i4) reveal (e, 3);
                     expt + negterm + expt
(%o4)               ------------------------
                    product(2) + expt + expt
(%i5) reveal (e, 4);
                       2                 2
                      b  - product(3) + a
(%o5)         ------------------------------------
                         product(2)     product(2)
              2 expt + %e           + %e
(%i6) reveal (e, 5);
                         2            2
                        b  - 2 a b + a
(%o6)              --------------------------
                       sum(2)     2 b     2 a
                   2 %e       + %e    + %e
(%i7) reveal (e, 6);
                          2            2
                         b  - 2 a b + a
(%o7)               -------------------------
                        b + a     2 b     2 a
                    2 %e      + %e    + %e
@end example

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} rmxchar
Valor padr@~ao: @code{]}

@code{rmxchar} @'e the caractere desenhado lado direito de uma matriz.

Veja tamb@'em @code{lmxchar}.

@end defvr

@c NEEDS EXAMPLES
@deffn {Fun@,{c}@~ao} save (@var{nomearquivo}, @var{nome_1}, @var{nome_2}, @var{nome_3}, ...)
@deffnx {Fun@,{c}@~ao} save (@var{nomearquivo}, values, functions, labels, ...)
@deffnx {Fun@,{c}@~ao} save (@var{nomearquivo}, [@var{m}, @var{n}])
@deffnx {Fun@,{c}@~ao} save (@var{nomearquivo}, @var{nome_1}=@var{expr_1}, ...)
@deffnx {Fun@,{c}@~ao} save (@var{nomearquivo}, all)
@deffnx {Fun@,{c}@~ao} save (@var{nomearquivo}, @var{nome_1}=@var{expr_1}, @var{nome_2}=@var{expr_2}, ...)

Armazena os valores correntes de @var{nome_1}, @var{nome_2}, @var{nome_3}, ..., em @var{nomearquivo}.
Os argumentos s@~ao os nomes das vari@'aveis, fun@,{c}@~oes, ou outros objetos.
Se um nome n@~ao possui valore ou fun@,{c}@~ao associada a ele, esse nome sem nenhum valor ou fun@,{c}@~ao associado ser@'a ignorado.
@code{save} retorna @var{nomearquivo}.

@code{save} armazena dados na forma de express@~oes Lisp.
Os dados armazenados por @code{save} podem ser recuperados por @code{load (@var{nomearquivo})}.

O sinalizador global @code{file_output_append} governa
se @code{save} anexa ao final ou trunca o arquivo de sa@'ida.
Quando @code{file_output_append} for @code{true},
@code{save} anexa ao final doarquivo de sa@'ida.
De outra forma, @code{save} trunca o arquivo de sa@'ida.
Nesse caso, @code{save} cria o arquivo se ele n@~ao existir ainda.

A forma especial @code{save (@var{nomearquivo}, values, functions, labels, ...)}
armazena os @'itens nomeados por @code{values}, @code{fun@,{c}@~oes}, @code{labels}, etc.
Os nomes podem ser quaisquer especificados pela vari@'avel @code{infolists}.
@code{values} compreende todas as vari@'aveis definidas pelo usu@'ario.

A forma especial @code{save (@var{nomearquivo}, [@var{m}, @var{n}])} armazena os valores de
r@'otulos de entrada e sa@'ida de @var{m} at@'e @var{n}.
Note que @var{m} e @var{n} devem obrigat@'oriamente ser inteiros literais.
R@'otulos de entrada e sa@'ida podem tamb@'em ser armazenados um a um, e.g., @code{save ("foo.1", %i42, %o42)}.
@code{save (@var{nomearquivo}, labels)} armazena todos os r@'otulos de entrada e sa@'ida.
Quando r@'otulos armazenados s@~ao recuperados, eles substituem r@'otulos existentes.

A forma especial @code{save (@var{nomearquivo}, @var{nome_1}=@var{expr_1}, @var{nome_2}=@var{expr_2}, ...)}
armazena os valores de @var{expr_1}, @var{expr_2}, ...,
com nomes @var{nome_1}, @var{nome_2}, ....
Isso @'e @'util para aplicar essa forma para r@'otulos de entrada e sa@'ida, e.g., @code{save ("foo.1", aa=%o88)}.
O lado direito dessa igualdade nessa forma pode ser qualquer express@~ao, que @'e avaliada.
Essa forma n@~ao introduz os novos nomes no ambiente corrente do Maxima,
mas somente armazena-os em @var{nomearquivo}.

Essa forma especial e a forma geral de @code{save} podem ser misturados.
Por exemplo, @code{save (@var{nomearquivo}, aa, bb, cc=42, fun@,{c}@~oes, [11, 17])}.

A forma especial @code{save (@var{nomearquivo}, all)} armazena o estado corrente do Maxima.
Isso inclui todas as vari@'aveis definidas pelo usu@'ario, fun@,{c}@~oes, arrays, etc., bem como
alguns @'itens definidos automaticamente.
Os @'ites salvos incluem vari@'aveis de sistema,
tais como @code{file_search_maxima} ou @code{showtime}, se a elas tiverem sido atribu@'idos novos valores pelo usu@'ario;
veja @code{myoptions}.

@code{save} avalia seus argumentos.  
@var{nomearquivo} deve obrigat@'oriamente ser uma seq@"u@^encia de caracteres, n@~ao uma vari@'avel tipo seq@"u@^encia de caracteres.
O primeiro e o @'ultimo r@'otulos a salvar, se especificado, devem obrigat@'oriamente serem inteiros.
O operador ap@'ostrofo-ap@'ostrofo @code{'@w{}'} avalia uma vari@'avel tipo seq@"u@^encia de caracteres para seu valor seq@"u@^encia de caracteres,
e.g., @code{s: "foo.1"$ save (''s, all)$},
e vari@'aveis inteiras para seus valores inteiros, e.g., @code{m: 5$ n: 12$ save ("foo.1", [''m, ''n])$}.

@c ANY USE FOR THE FOLLOWING ???
@c The following device constructs a list of variables to save e saves them.
@c 
@c @example
@c s: "foo.1"$
@c l: ['aa, 'bb, 'cc, 'dd]$
@c apply (save, append ([s], l))$
@c @end example
@end deffn

@c NEEDS MORE WORK !!!
@defvr {Vari@'avel de op@,{c}@~ao} savedef
Valor padr@~ao: @code{true}

Quando @code{savedef} @'e @code{true}, a ves@~ao Maxima de uma
fun@,{c}@~ao de usu@'ario @'e preservada quando a fun@,{c}@~ao @'e traduzida.
Isso permite que a defini@,{c}@~ao seja mostrada por @code{dispfun} e autoriza a fun@,{c}@~ao a
ser editada.

Quando @code{savedef} @'e @code{false}, os nomes de fun@,{c}@~oes traduzidas s@~ao
removidos da lista de @code{fun@,{c}@~oes}.

@end defvr

@c THIS FUNCTION IS IN THE SHARE PACKAGE itensor.lisp
@c MOVE THIS DESCRIPTION TO Itensor.texi
@deffn {Fun@,{c}@~ao} show (@var{expr})
Mostra @code{expr} com os objetos indexados
tendo @'indices covariantes como subscritos, @'indices contravariantes como
sobrescritos.  Os @'indices derivativos s@~ao mostrados como subscritos,
separados dos @'indices covariantes por uma v@'irgula.

@end deffn

@deffn {Fun@,{c}@~ao} showratvars (@var{expr})
Retorna uma lista de vari@'aveis express@~ao racional can@^onica (CRE) na express@~ao @code{expr}.

Veja tamb@'em @code{ratvars}.

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} stardisp
Valor padr@~ao: @code{false}

Quando @code{stardisp} @'e @code{true}, multiplica@,{c}@~ao @'e
mostrada com um asterisco @code{*} entre os operandos.

@end defvr

@c NEEDS CLARIFICATION AND EXAMPLES
@deffn {Fun@,{c}@~ao} string (@var{expr})
Converte @code{expr} para a nota@,{c}@~ao linear do Maxima
apenas como se tivesse sido digitada.

O valor de retorno de @code{string} @'e uma seq@"u@^encia de caracteres,
e dessa forma n@~ao pode ser usada em um c@'alculo.

@end deffn

@c SHOULD BE WRITTEN WITH LEADING ? BUT THAT CONFUSES CL-INFO SO WORK AROUND
@c @defvr {Vari@~avel de op@,{c}@~ao} stringdisp
@defvr {Vari@~avel de op@,{c}@~ao} stringdisp
Valor padr@~ao: @code{false}

Quando @code{stringdisp} for @code{true},
seq@"u@^encias de caracteres ser@~ao mostradas contidas em aspas duplas.
De outra forma,
aspas n@~ao s@~ao mostradas.

@code{stringdisp} @'e sempre @code{true} quando mostrando na tela uma defini@,{c}@~ao de fun@,{c}@~ao.

Exemplos:

@c ===beg===
@c stringdisp: false$
@c "This is an example string.";
@c foo () := print ("This is a string in a function definition.");
@c stringdisp: true$
@c "This is an example string.";
@c ===end===
@example
(%i1) stringdisp: false$
(%i2) "This is an example string.";
(%o2)              This is an example string.
(%i3) foo () := print ("This is a string in a function definition.");
(%o3) foo() := 
              print("This is a string in a function definition.")
(%i4) stringdisp: true$
(%i5) "This is an example string.";
(%o5)             "This is an example string."
@end example

@end defvr

@deffn {Fun@,{c}@~ao} stringout (@var{nomearquivo}, @var{expr_1}, @var{expr_2}, @var{expr_3}, ...)
@deffnx {Fun@,{c}@~ao} stringout (@var{nomearquivo}, [@var{m}, @var{n}])
@deffnx {Fun@,{c}@~ao} stringout (@var{nomearquivo}, input)
@deffnx {Fun@,{c}@~ao} stringout (@var{nomearquivo}, functions)
@deffnx {Fun@,{c}@~ao} stringout (@var{nomearquivo}, values)

@code{stringout} escreve express@~oes para um arquivo na mesma forma de
express@~oes que foram digitadas para entrada.  O arquivo pode ent@~ao ser usado
como entrada para comandos @code{batch} ou @code{demo}, e isso pode ser editado para
qualquer prop@'osito.  @code{stringout} pode ser executado enquanto @code{writefile} est@'a em progresso.

O sinalizador global @code{file_output_append} governa
se @code{stringout} anexa ao final ou trunca o arquivo de sa@'ida.
Quando @code{file_output_append} for @code{true},
@code{stringout} anexa ao final do arquivo de sa@'da.
De outra forma, @code{stringout} trunca o arquivo de sa@'ida.
Nesse caso, @code{stringout} cria o arquivo de sa@'ida se ele n@~ao existir ainda.

A forma geral de @code{stringout} escreve os valores de um ou mais 
express@~oes para o arquivo de sa@'ida.  Note que se uma express@~ao @'e uma
vari@'avel, somente o valor da vari@'avel @'e escrito e n@~ao o nome
da vari@'avel.  Como um @'util caso especial, as express@~oes podem ser
r@'otulos de entrada (@code{%i1}, @code{%i2}, @code{%i3}, ...) ou r@'otulos de sa@'ida (@code{%o1}, @code{%o2}, @code{%o3}, ...).

Se @code{grind} @'e @code{true}, @code{stringout} formata a sa@'ida usando o formato 
@code{grind}.  De outra forma o formato @code{string} @'e usado.  Veja @code{grind} e @code{string}.

A forma especial @code{stringout (@var{nomearquivo}, [@var{m}, @var{n}])} escreve os
valores dos r@'otulos de entrada de m at@'e n, inclusive.  

A forma especial @code{stringout (@var{nomearquivo}, input)} escreve todos
os r@'otulos de entrada para o arquivo.

A forma especial @code{stringout (@var{nomearquivo}, functions)} escreve todas
as fun@,{c}@~oes definidas pelo usu@'ario (nomeadas pela lista global @code{functions}) para o arquivo.

A forma especial @code{stringout (@var{nomearquivo}, values)} escreve todas as
vari@'aveis atribu@'idas pelo usu@'ario (nomeadas pela lista global @code{values})
para o arquivo.  Cada vari@'avel @'e impressa como uma
declara@,{c}@~ao de atribui@,{c}@~ao, com o nome da vari@'avel seguida de dois pontos, e seu
valor.  Note que a forma geral de @code{stringout} n@~ao imprime 
vari@'aveis como declara@,{c}@~oes de atribui@,{c}@~ao.

@end deffn

@c NEEDS EXPANSION, CLARIFICATION, AND EXAMPLES
@deffn {Fun@,{c}@~ao} tex (@var{expr})
@deffnx {Fun@,{c}@~ao} tex (@var{r@'otulo})
@deffnx {Fun@,{c}@~ao} tex (@var{expr}, @var{momearquivo})
@deffnx {Fun@,{c}@~ao} tex (@var{label}, @var{nomearquivo})

Imprime uma representa@,{c}@~ao de uma express@~ao
adequada para o sistema TeX  de prepara@,{c}@~ao de documento.
O resultado @'e um fragmento de um documento,
que pode ser copiado dentro de um documento maior.
Esse fragmento n@~ao pode ser processado de forma direta e isolada.

@code{tex (@var{expr})} imprime uma representa@,{c}@~ao TeX da @var{expr} no console.

@code{tex (@var{r@'otulo})} imprime uma representa@,{c}@~ao TeX de uma express@~ao chamada @var{r@'otulo}
e atribui a essa um r@'otulo de equa@,{c}@~ao (a ser mostrado @`a esquerda da express@~ao).
O r@'otulo de equa@,{c}@~ao TeX @'e o mesmo que o r@'otulo da equa@,{c}@~ao no Maxima.

@code{tex (@var{expr}, @var{nomearquivo})} anexa ao final uma representa@,{c}@~ao TeX de @var{expr}
no arquivo @var{nomearquivo}.
@code{tex} n@~ao avalia o argumento @var{nomearquivo}; ap@'ostrofo-ap@'ostrofo @code{''} for@,{c}a a avalia@,{c}@~ao so argumento.

@code{tex (@var{r@'otulo}, @var{nomearquivo})} anexa ao final uma representa@,{c}@~ao TeX da
express@~ao chamada de @var{r@'otulo}, com um r@'otulo de equa@,{c}@~ao, ao arquivo @var{nomearquivo}.


@code{tex} n@~ao avalia o argumento @var{nomearquivo}; ap@'ostrofo-ap@'ostrofo @code{''} for@,{c}a a avalia@,{c}@~ao so argumento.
@code{tex} avalia seus argumentos ap@'os testar esse argumento para ver se @'e um r@'otulo.
duplo ap@'ostrofo @code{''} for@,{c}a a avalia@,{c}@~ao do argumento, desse modo frustrando o teste
e prevenindo o r@'otulo.

Veja tamb@'em @code{texput}.

Exemplos:

@example
(%i1) integrate (1/(1+x^3), x);
                                    2 x - 1
                  2            atan(-------)
             log(x  - x + 1)        sqrt(3)    log(x + 1)
(%o1)      - --------------- + ------------- + ----------
                    6             sqrt(3)          3
(%i2) tex (%o1);
$$-@{@{\log \left(x^2-x+1\right)@}\over@{6@}@}+@{@{\arctan \left(@{@{2\,x-1
 @}\over@{\sqrt@{3@}@}@}\right)@}\over@{\sqrt@{3@}@}@}+@{@{\log \left(x+1\right)
 @}\over@{3@}@}\leqno@{\tt (\%o1)@}$$
(%o2)                          (\%o1)
(%i3) tex (integrate (sin(x), x));
$$-\cos x$$
(%o3)                           false
(%i4) tex (%o1, "foo.tex");
(%o4)                          (\%o1)
@end example

@end deffn

@deffn {Fun@,{c}@~ao} texput (@var{a}, @var{s})
@deffnx {Fun@,{c}@~ao} texput (@var{a}, @var{s}, @var{operator_type})
@deffnx {Fun@,{c}@~ao} texput (@var{a}, [@var{s_1}, @var{s_2}], matchfix)
@deffnx {Fun@,{c}@~ao} texput (@var{a}, [@var{s_1}, @var{s_2}, @var{s_3}], matchfix)

Atribui a sa@'ida TeX para o @'atomo @var{a},
que pode ser um s@'imbolo ou o nome de um operador.

@code{texput (@var{a}, @var{s})} faz com que a fun@,{c}@~ao @code{tex}
interpole a seq@"u@^encia de caracteres @var{s} dentro da sa@'ida TeX em lugar de @var{a}.

@code{texput (@var{a}, @var{s}, @var{operator_type})},
onde @var{operator_type} @'e @code{prefix}, @code{infix}, @code{postfix}, @code{nary}, ou @code{nofix},
faz com que a fun@,{c}@~ao @code{tex} interpole @var{s} dentro da sa@'ida TeX em lugar de @var{a},
e coloca o texto interpolado na posi@,{c}@~ao apropriada.

@code{texput (@var{a}, [@var{s_1}, @var{s_2}], matchfix)}
faz com que a fun@,{c}@~ao @code{tex} interpole @var{s_1} e @var{s_2} dentro da sa@'ida TeX
sobre qualquer lado dos argumentos de @var{a}.
Os argumentos (se mais de um) s@~ao separados por v@'irgulas.

@code{texput (@var{a}, [@var{s_1}, @var{s_2}, @var{s_3}], matchfix)}
faz com que a fun@,{c}@~ao @code{tex} interpole @var{s_1} e @var{s_2} dentro da sa@'ida TeX
sobre qualquer lado dos argumentos de @var{a},
com @var{s_3} separando os argumentos.

Exemplos:

Atribui sa@'ida TeX a uma vari@'avel.
@c ===beg===
@c texput (me,"\\mu_e");
@c tex (me);
@c ===end===

@example
(%i1) texput (me,"\\mu_e");
(%o1)                         \mu_e
(%i2) tex (me);
$$\mu_e$$
(%o2)                         false
@end example

Atribui sa@'ida TeX a uma fun@,{c}@~ao comum (n@~ao a um operador).
@c ===beg===
@c texput (lcm, "\\mathrm{lcm}");
@c tex (lcm (a, b));
@c ===end===

@example
(%i1) texput (lcm, "\\mathrm@{lcm@}");
(%o1)                     \mathrm@{lcm@}
(%i2) tex (lcm (a, b));
$$\mathrm@{lcm@}\left(a , b\right)$$
(%o2)                         false
@end example

Atribui sa@'ida TeX a um operador prefixado.
@c ===beg===
@c prefix ("grad");
@c texput ("grad", " \\nabla ", prefix);
@c tex (grad f);
@c ===end===

@example
(%i1) prefix ("grad");
(%o1)                         grad
(%i2) texput ("grad", " \\nabla ", prefix);
(%o2)                        \nabla 
(%i3) tex (grad f);
$$ \nabla f$$
(%o3)                         false
@end example

Atribui sa@'ida TeX a um operador infixado.
@c ===beg===
@c infix ("~");
@c texput ("~", " \\times ", infix);
@c tex (a ~ b);
@c ===end===

@example
(%i1) infix ("~");
(%o1)                           ~
(%i2) texput ("~", " \\times ", infix);
(%o2)                        \times 
(%i3) tex (a ~ b);
$$a \times b$$
(%o3)                         false
@end example

Atribui sa@'ida TeX a um operadro p@'osfixado.
@c ===beg===
@c postfix ("@");
@c texput ("@", "!!", postfix);
@c tex (x @);
@c ===end===

@example
(%i1) postfix ("@@");
(%o1)                           @@
(%i2) texput ("@@", "!!", postfix);
(%o2)                          !!
(%i3) tex (x @@);
$$x!!$$
(%o3)                         false
@end example

Atribui sa@'ida TeX a um operador n-@'ario.
@c ===beg===
@c nary ("@@");
@c texput ("@@", " \\circ ", nary);
@c tex (a @@ b @@ c @@ d);
@c ===end===

@example
(%i1) nary ("@@@@");
(%o1)                          @@@@
(%i2) texput ("@@@@", " \\circ ", nary);
(%o2)                         \circ 
(%i3) tex (a @@@@ b @@@@ c @@@@ d);
$$a \circ b \circ c \circ d$$
(%o3)                         false
@end example

Atribui sa@'ida TeX a um operador nofix.
@c ===beg===
@c nofix ("foo");
@c texput ("foo", "\\mathsc{foo}", nofix);
@c tex (foo);
@c ===end===

@example
(%i1) nofix ("foo");
(%o1)                          foo
(%i2) texput ("foo", "\\mathsc@{foo@}", nofix);
(%o2)                     \mathsc@{foo@}
(%i3) tex (foo);
$$\mathsc@{foo@}$$
(%o3)                         false
@end example

Atribui sa@'ida TeX a um operadro matchfix.
@c ===beg===
@c matchfix ("<<", ">>");
@c texput ("<<", [" \\langle ", " \\rangle "], matchfix);
@c tex (<<a>>);
@c tex (<<a, b>>);
@c texput ("<<", [" \\langle ", " \\rangle ", " \\, | \\,"], matchfix);
@c tex (<<a>>);
@c tex (<<a, b>>);
@c ===end===

@example
(%i1) matchfix ("<<", ">>");
(%o1)                          <<
(%i2) texput ("<<", [" \\langle ", " \\rangle "], matchfix);
(%o2)                [ \langle ,  \rangle ]
(%i3) tex (<<a>>);
$$ \langle a \rangle $$
(%o3)                         false
(%i4) tex (<<a, b>>);
$$ \langle a , b \rangle $$
(%o4)                         false
(%i5) texput ("<<", [" \\langle ", " \\rangle ", " \\, | \\,"], matchfix);
(%o5)           [ \langle ,  \rangle ,  \, | \,]
(%i6) tex (<<a>>);
$$ \langle a \rangle $$
(%o6)                         false
(%i7) tex (<<a, b>>);
$$ \langle a \, | \,b \rangle $$
(%o7)                         false
@end example

@end deffn

@c NEEDS CLARIFICATION
@deffn {Fun@,{c}@~ao} system (@var{comando})
Executa @var{comando} como um processo separado.
O comando @'e passado ao shell padra@~o para execu@,{c}@~ao.
@code{system} n@~ao @'e suportado por todos os sistemas
operacionais, mas geralmente existe em ambientes Unix e Unix-like.

Supondo que @code{_hist.out}
@'e uma lista de freq@"u@^encia que voc@^e deseja imprimir como um gr@'afico em barras
usando @code{xgraph}.  

@example
(%i1) (with_stdout("_hist.out",
           for i:1 thru length(hist) do (
             print(i,hist[i]))),
       system("xgraph -bar -brw .7 -nl < _hist.out"));
@end example

Com o objetivo de fazer com que a impress@~ao do gr@'afico seja conclu@'ida em segundo plano (retornando o controle para o Maxima)
e remover o arquivo tempor@'ario ap@'os isso ter sido conclu@'ido fa@,{c}a:

@example
system("(xgraph -bar -brw .7 -nl < _hist.out;  rm -f _hist.out)&")
@end example

@end deffn

@defvr {Vari@'avel de op@,{c}@~ao} ttyoff
Valor padr@~ao: @code{false}

Quando @code{ttyoff} @'e @code{true}, express@~oes de sa@'ida n@~ao s@~ao mostradas.
Express@~oes de sa@'ida s@~ao ainda calculadas e atribu@'idas r@'otulos.  Veja @code{labels}.

Textos impresso por fun@,{c}@~oes internas do Maxima, tais como mensagens de erro e a sa@'ida de @code{describe},
n@~ao s@~ao afetadas por @code{ttyoff}.

@end defvr

@deffn {Fun@,{c}@~ao} with_stdout (@var{nomearquivo}, @var{expr_1}, @var{expr_2}, @var{expr_3}, ...)
Abre @var{nomearquivo} e ent@~ao avalia @var{expr_1}, @var{expr_2}, @var{expr_3}, ....
Os valores dos argumentos n@~ao s@~ao armazenados em @var{nomearquivo},
mas qualquer sa@'ida impressa gerada pela avalia@,{c}@~ao dos argumentos
(de @code{print}, @code{display}, @code{disp}, ou @code{grind}, por exemplo)
vai para @var{nomearquivo} em lugar do console.

O sinalizador global @code{file_output_append} governa
se @code{with_stdout} anexa ao final ou trunca o arquivo de sa@'ida.
Quando @code{file_output_append} for @code{true},
@code{with_stdout} anexa ao final do arquivo de sa@'ida.
De outra forma, @code{with_stdout} trunca o arquivo de sa@'ida.
Nesse caso, @code{with_stdout} cria o arquivo se ele n@~ao existir ainda.

@code{with_stdout} retorna o valor do seu argumento final.

Veja tamb@'em @code{writefile}.

@c THIS DOESN'T SEEM VERY IMPORTANT TO MENTION ...
@c Note the binding of display2d to be
@c false, otherwise the printing will have things like "- 3" instead
@c of "-3".
@c
@example
@c THIS EXAMPLE USES SOME UNIX-ISH CONSTRUCTS -- WILL IT WORK IN WINDOWS ???
@c ALSO IT'S SORT OF COMPLICATED AND THE SIMPLER SECOND EXAMPLE ILLUSTRATES with_stdout BETTER !!!
@c mygnuplot (f, var, range, number_ticks) :=
@c  block ([numer:true, display2d:false],
@c  with_stdout("tmp.out",
@c    dx: (range[2]-range[1])/number_ticks,
@c    for x: range[1] thru range[2] step dx
@c       do print (x, at (f, var=x))),
@c  system ("echo \"set data style lines; set title '", f,"' ;plot '/tmp/gnu'
@c ;pause 10 \" | gnuplot"))$
(%i1) with_stdout ("tmp.out", for i:5 thru 10 do print (i, "! yields", i!))$
(%i2) printfile ("tmp.out")$
5 ! yields 120 
6 ! yields 720 
7 ! yields 5040 
8 ! yields 40320 
9 ! yields 362880 
10 ! yields 3628800
@end example

@end deffn

@deffn {Fun@,{c}@~ao} writefile (@var{nomearquivo})
Come@,{c}a escrevendo uma transcri@,{c}@~ao da sess@~ao Maxima para @var{nomearquivo}.
Toda intera@,{c}@~ao entre o usu@'ario e Maxima @'e ent@~ao gravada nesse arquivo,
@c FOLLOWING CLAIM PROBABLY NEEDS TO BE QUALIFIED
da mesma forma que aparece no console.

Como a transcri@,{c}@~ao @'e impressa no formato de sa@'ida do console,
isso n@~ao pode ser reaproveitado pelo Maxima.
Para fazer um arquivo contendo express@~oes que podem ser reaproveitadas,
veja @code{save} e @code{stringout}.
@code{save} armazena express@~oes no formato Lisp, enquanto @code{stringout} armazena express@~oes no formato Maxima.

O efeito de executar @code{writefile} quando @var{nomearquivo} ainda existe
depende da implementa@,{c}@~ao Lisp subjacente;
o arquivo transcrito pode ser substitu@'ido, ou o arquivo pode receber um anexo.
@code{appendfile} sempre anexa para o arquivo transcrito.

Isso pode ser conveniente para executar @code{playback} ap@'os
@code{writefile} para salvar a visualiza@,{c}@~ao de intera@,{c}@~oes pr@'evias.
Como @code{playback} mostra somente as vari@'aveis de entrada e sa@'ida (@code{%i1}, @code{%o1}, etc.),
qualquer sa@'ida gerada por uma declara@,{c}@~ao de impress@~ao em uma fun@,{c}@~ao
(como oposi@,{c}@~ao a um valor de retorno) n@~ao @'e mostrada por @code{playback}.

@code{closefile} fecha o arquivo transcrito aberto por @code{writefile} ou @code{appendfile}.

@end deffn
