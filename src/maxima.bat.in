@echo off

set arg0=%0
set arg1=%1
set arg2=%2
set arg3=%3
set arg4=%4
set arg5=%5
set arg6=%6
set arg7=%7
set arg8=%8
set arg9=%9

set lisp=@DEFAULTLISP@
set version=@VERSION@
set prefix=@prefix@
set maxima_prefix=@prefix@
set package=@PACKAGE@
set verbose=false

:startparseargs
if "%1" == "-l" goto foundlisp
if "%1" == "--lisp" goto foundlisp
if "%1" == "-u" goto foundversion
if "%1" == "--use-version" goto foundversion
if "%1" == "-v" goto foundverbose
if "%1" == "--verbose" goto foundverbose

:continueparseargs
shift
if not "%1" == "" goto startparseargs
goto endparseargs

:foundlisp
set lisp=%2
shift
goto continueparseargs

:foundversion
set version=%2
shift
goto continueparseargs

:foundverbose
set verbose=true
goto continueparseargs

:endparseargs

if "%MAXIMA_LAYOUT_AUTOTOOLS%" == "" goto defaultlayout
set layout_autotools=true
goto endlayout

:defaultlayout
set layout_autotools=@default_layout_autotools@

:endlayout

if "%MAXIMA_PREFIX%" == "" goto defaultvars
if "%layout_autotools%" == "true" goto maxim_autotools
set maxima_imagesdir=%MAXIMA_PREFIX%\src
goto endsetupvars

:maxim_autotools
set maxima_imagesdir=%MAXIMA_PREFIX%\lib\%package%\%version%
goto endsetupvars

:defaultvars
if "%layout_autotools%" == "true" goto defmaxim_autotools
set maxima_imagesdir=%prefix%\src
goto endsetupvars

:defmaxim_autotools
set maxima_imagesdir=%prefix%\lib\%package%\%version%
goto endsetupvars

:endsetupvars

set maxima_image_base=%maxima_imagesdir%\binary-%lisp%\maxima

if "%verbose%" == "true" @echo on
if "%lisp%" == "gcl" goto dogcl
if "%lisp%" == "clisp" goto doclisp

@echo "maxima error: lisp %lisp% not known."
goto end

:dogcl
%maxima_imagesdir%\binary-gcl\maxima.exe -eval "(user::run)" -f -- "%arg1%" "%arg2%" "%arg3%" "%arg4%" "%arg5%" "%arg6%" "%arg7%" "%arg8%" "%arg9%"
goto end

:doclisp
if "%layout_autotools%" == "true" goto clisp_autotools
clisp -q -M %maxima_image_base%.mem "" -- "%arg1%" "%arg2%" "%arg3%" "%arg4%" "%arg5%" "%arg6%" "%arg7%" "%arg8%" "%arg9%"
goto end

:clisp_autotools
%maxima_imagesdir%\binary-clisp\lisp.exe -q -M %maxima_image_base%.mem "" -- "%arg1%" "%arg2%" "%arg3%" "%arg4%" "%arg5%" "%arg6%" "%arg7%" "%arg8%" "%arg9%"
goto end

:end
