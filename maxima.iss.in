;--------------------------------------------------------
;-- maxima.iss -- Maxima InnoSetup installation script --
;--------------------------------------------------------
;
;   InnoSetup version 5.1.7 or above is recommended
;

[Setup]
AppName=Maxima
AppVerName=Maxima @VERSION@
AppId=Maxima-@VERSION@
UsePreviousAppDir=no
AppPublisher=The Maxima Development Team
AppPublisherURL=http://maxima.sourceforge.net
AppSupportURL=http://maxima.sourceforge.net
AppUpdatesURL=http://maxima.sourceforge.net
AppVersion=@VERSION@
OutputBaseFilename=maxima-@VERSION@
DefaultDirName={pf}\Maxima-@VERSION@
DefaultGroupName=Maxima-@VERSION@
InfoBeforeFile=interfaces\xmaxima/win32\InfoBefore.txt
AllowNoIcons=yes
LicenseFile=@prefix@\share\maxima\@VERSION@\doc\COPYING
Compression=lzma/ultra
SolidCompression=yes
Uninstallable=yes
UninstallFilesDir={app}\uninst
ShowLanguageDialog=yes


[Types] 
Name: "full"; Description: "Full installation" 
Name: "compact"; Description: "Compact installation" 
Name: "custom"; Description: "Custom installation"; Flags: iscustom


[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"
Name: "ge"; MessagesFile: "compiler:Languages\German.isl"
Name: "it"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "es"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "pt_BR"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "ru"; MessagesFile: "compiler:Languages\Russian.isl"


[Components]
Name: "core"; Description: "Maxima core with command line interface"; Types: full compact custom; Flags: fixed
Name: "wxmaxima"; Description: "wxMaxima graphic shell"; Types: full custom
Name: "xmaxima"; Description: "XMaxima graphic shell"; Types: full custom
Name: "lang"; Description: "Maxima language packs"; Types: full custom
Name: "lang\spanish"; Description: "Spanish"; Types: full custom
Name: "lang\portuguese"; Description: "Portuguese"; Types: full custom


[Tasks]
Name: "wxmdesktopicon"; Description: "Create a &wxMaxima desktop icon"; GroupDescription: "Additional icons:"; MinVersion: 4,4;  Components: "wxmaxima"
Name: "xmdesktopicon"; Description: "Create a &XMaxima desktop icon"; GroupDescription: "Additional icons:"; Flags: unchecked; MinVersion: 4,4;  Components: "xmaxima"


[Files]
Source: "@prefix@\bin\maxima-command.ico"; DestDir: "{app}\bin\"; Flags: ignoreversion; Components: "core"
Source: "@prefix@\bin\maxima.bat"; DestDir: "{app}\bin\"; Flags: ignoreversion; AfterInstall: ReplaceApp(); Components: "core" 
Source: "@prefix@\readme.txt"; DestDir: "{app}"; Flags: isreadme; Components: "core"
Source: "@prefix@\*.*"; Excludes: "\bin\xmaxima.*,\bin\xmaxima,\wxMaxima,\info\es\*.*,\info\pt\*.*,\share\maxima\@VERSION@\doc\html\es\*.*,\share\maxima\@VERSION@\doc\html\pt\*.*"; DestDir: "{app}\";  Flags: recursesubdirs; Components: "core"

Source: "@prefix@\wxMaxima\*.*"; DestDir: "{app}\wxMaxima\"; Flags: ignoreversion recursesubdirs; Components: "wxmaxima"

Source: "@prefix@\bin\xmaxima.*"; DestDir: "{app}\bin\"; Flags: ignoreversion; Components: "xmaxima"
Source: "@prefix@\bin\xmaxima"; DestDir: "{app}\bin\"; Flags: ignoreversion; Components: "xmaxima"

Source: "@prefix@\info\es\*.*"; DestDir: "{app}\info\es\";  Flags: recursesubdirs;  Components: "lang\spanish"
Source: "@prefix@\share\maxima\@VERSION@\doc\html\es\*.*"; DestDir: "{app}\share\maxima\@VERSION@\doc\html\es\";  Flags: recursesubdirs;  Components: "lang\spanish"

Source: "@prefix@\info\pt\*.*"; DestDir: "{app}\info\pt\";  Flags: recursesubdirs;  Components: "lang\portuguese"
Source: "@prefix@\share\maxima\@VERSION@\doc\html\pt\*.*"; DestDir: "{app}\share\maxima\@VERSION@\doc\html\pt\";  Flags: recursesubdirs;  Components: "lang\portuguese"

[Icons]
Name: "{group}\Command line Maxima"; Filename: "{app}\bin\maxima.bat"; IconFilename: "{app}\bin\maxima-command.ico";  Components: "core"
Name: "{group}\Introduction"; Filename: "{app}\share\maxima\@VERSION@\doc\html\intromax.html"; WorkingDir: "{app}\share\maxima\@VERSION@";  Components: "core"
Name: "{group}\Reference Manual"; Filename: "{app}\share\maxima\@VERSION@\doc\html\maxima_toc.html"; WorkingDir: "{app}\share\maxima\@VERSION@";  Components: "core"
Name: "{group}\README"; Filename: "{app}\readme.txt"; Components: "core"

Name: "{group}\wxMaxima"; Filename: "{app}\wxMaxima\wxMaxima.exe"; WorkingDir: "{app}";  Components: "wxmaxima"
Name: "{group}\wxMaxima on the Web"; Filename: "{app}\wxMaxima\wxMaxima.url";  Components: "wxmaxima"
Name: "{group}\wxMaxima Online Forum"; Filename: "{app}\wxMaxima\Forum.url";  Components: "wxmaxima"
Name: "{userdesktop}\wxMaxima"; Filename: "{app}\wxMaxima\wxMaxima.exe"; MinVersion: 4,4; Tasks: wxmdesktopicon; WorkingDir: "{app}\wxMaxima";  Components: "wxmaxima"

Name: "{group}\XMaxima"; Filename: "{app}\bin\xmaxima.exe"; IconFilename: "{app}\share\maxima\@VERSION@\xmaxima\maxima-icon.ico";  Components: "xmaxima"
Name: "{userdesktop}\XMaxima"; Filename: "{app}\bin\xmaxima.exe"; MinVersion: 4,4; Tasks: xmdesktopicon; IconFilename: "{app}\share\maxima\@VERSION@\xmaxima\maxima-icon.ico";  Components: "xmaxima"

Name: "{group}\Uninstall"; Filename:"{uninstallexe}"

Name: "{group}\Reference Manual (Spanish)"; Filename: "{app}\share\maxima\@VERSION@\doc\html\es\maxima_toc.html"; WorkingDir: "{app}\share\maxima\@VERSION@";  Components: "lang\spanish"
Name: "{group}\Reference Manual (Portuguese)"; Filename: "{app}\share\maxima\@VERSION@\doc\html\pt\maxima_toc.html"; WorkingDir: "{app}\share\maxima\@VERSION@";  Components: "lang\portuguese"


;[Run]
;Filename: "{app}\bin\xmaxima.exe"; Description: "Launch XMaxima"; Flags: postinstall skipifsilent skipifdoesntexist
;Filename: "{app}\wxMaxima\wxMaxima.exe"; Description: "Launch wxMaxima"; Flags: postinstall skipifsilent skipifdoesntexist


[Code]

{ Check if a path contains spaces.  If it does, convert it to
  the equivalent short path }
function PathWithoutSpaces( strIn: String): String;
begin
  if (Pos(' ',strIn) = 0) then
    Result := strIn
  else
    Result := GetShortName(strIn);
end;


{ Based on code from Inno Setup Extensions Knowledge Base
  Article 14 - How to replace a line in a text file
  http://www13.brinkster.com/vincenzog/isxart.asp?idart=14
  Author: Stefan Bracke }

{ Note: Functions called by AfterInstall can have maximum of one argument }

procedure ReplaceApp();
var
  iLineCounter : Integer;
  a_strTextfile : TArrayOfString;
  strFilename : String;
  strFind : String;
  strNewLine : String;
  strApp : String;
begin
  strApp := ExpandConstant('{app}');
  strFilename := strApp + '\bin\maxima.bat';
  strFind := 'set maxima_prefix';
  strNewLine := 'set maxima_prefix='+PathWithoutSpaces(strApp);
  
  { Load textfile into string array }
  LoadStringsFromFile(strFilename, a_strTextfile);
  
  { Search through all textlines for given text }
  for iLineCounter := 0 to GetArrayLength(a_strTextfile)-1 do
    begin
      { Overwrite textline when text searched for is part of it }
      if (Pos(strFind, a_strTextfile[iLineCounter]) > 0) then
        a_strTextfile[iLineCounter] := strNewLine;
    end;

  { Save string array to textfile (overwrite, no append!) }
  SaveStringsToFile(strFilename, a_strTextfile, False);

end;
