kill(allbut(props));
done;
/* Tests of confluent hypergeometric function */

/* See A&S 13.6 */
/* Bessel functions */

assume(not(equal(z,0)));
[not equal(z,0)]$

/* A&S 13.6.1 */
hgfred([v+1/2],[2*v+1],2*%i*z);
4^(v/2)*bessel_j(v,z)*gamma(v+1)*exp(%i*z)/z^v$

/* A&S 13.6.3 */
hgfred([v+1/2],[2*v+1],2*z);
4^(v/2)*bessel_i(v,z)*gamma(v+1)*exp(z)/z^v$

/* A&S 13.6.4 */
expand(hgfred([n+1],[2*n+2],2*%i*z));
4^(n/2+1/4)*bessel_j(n+1/2,z)*gamma(n+3/2)*z^(-n-1/2)*%e^(%i*z)$

/* A&S 13.6.5 */
expand(hgfred([-n],[-2*n],2*%i*z));
bessel_j(-n-1/2,z)*gamma(1/2-n)*4^(-n/2-1/4)*z^(n+1/2)*%e^(%i*z)$

/* A&S 13.6.6 */
hgfred([n+1],[2*n+2],2*z);
bessel_i(n+1/2,z)*gamma(n+3/2)*z^(-n-1/2)*%e^z/4^((-n-1/2)/2)$

/* A&S 13.6.10 */
hgfred([a],[a+1],-z);
a*z^(-a)*%gammagreek(a,z)$

/* A&S 13.6.12 */
hgfred([a],[a],z);
exp(z)$

/* A&S 13.6.13 */
besselexpand:true;
true$

expand(hgfred([1],[2],-2*%i*z));
exp(-%i*z)*sin(z)/z$

/* A&S 13.6.14 */
expand(hgfred([1],[2],2*z));
exp(z)*sinh(z)/z$

/* A&S 13.6.17 
 * Note that this formula is in terms of He but we want just H.  
 * A&S 22.5.18 gives the relationship between He and H:
 *
 * He(n,x) = 2^(-n/2)*H(n,x/sqrt(2))
 */
hgfred([-4],[1/2],z^2/2);
hermite(8,z/sqrt(2))/1680$

/* A&S 13.6.18 */
hgfred([-4],[3/2],z^2/2);
sqrt(2)*hermite(9,z/sqrt(2))/30240/z$

/* A&S 13.6.19 */
ratsimp(hgfred([1/2],[3/2],-z^2));
sqrt(%pi)/2/abs(z)*erf(abs(z))$

/* A&S 7.1.21 */
ratsimp(hgfred([1],[3/2],z^2));
sqrt(%pi)*exp(z^2)*erf(abs(z))/2/abs(z)$

/* Hypergeometric function 2F1 tests 
*
* See A&S 15.1
*
*/
/* A&S 15.1.3 */
hgfred([1,1],[2],z);
-log(1-z)/z;

/* A&S 15.1.4 */
hgfred([1/2,1], [3/2], z^2);
log((1+z)/(1-z))/(2*z)$

/* A&S 15.1.5 */
hgfred([1/2,1], [3/2], -z^2);
atan(z)/z$

/* A&S 15.1.6 */
hgfred([1/2,1/2], [3/2], z^2);
asin(z)/z$

hgfred([1,1], [3/2], z^2);
asin(z)/z/sqrt(1-z^2)$

/* A&S 15.1.7 */
hgfred([1/2,1/2], [3/2], -z^2);
log(z+sqrt(1+z^2))/z$

hgfred([1,1], [3/2], -z^2);
log(z+sqrt(1+z^2))/z/sqrt(1+z^2)$

/* A&S 15.1.8 */
hgfred([a,b],[b],z);
(1-z)^(-a)$

/* A&S 15.1.9 */
hgfred([a,a+1/2],[1/2],z^2);
1/2*((z+1)^(-2*a)+(1-z)^(-2*a))$

/* A&S 15.1.10 */
hgfred([a,a+1/2],[3/2],z^2);
((z+1)^(1-2*a)-(1-z)^(1-2*a))/2/(1-2*a)/z$

/* A&S 15.1.11 */
hgfred([-a,a],[1/2],-z^2);
((z+sqrt(1+z^2))^(2*a) + (sqrt(1+z^2)-z)^(2*a))/2$

/* A&S 15.1.12 */
hgfred([a,1-a],[1/2],-z^2);
((sqrt(1+z^2)+z)^(2*a-1)+(sqrt(1+z^2)-z)^(2*a-1))/2/sqrt(1+z^2)$

/* A&S 15.1.13 */
hgfred([a,a+1/2],[1+2*a],z);
2^(2*a)/(1+sqrt(1-z))^(2*a)$

hgfred([a+1,a+1/2],[1+2*a],z);
2^(2*a)/(1+sqrt(1-z))^(2*a)/sqrt(1-z)$

/* A&S 15.1.14 */
hgfred([a,a+1/2],[2*a],z);
2^(2*a-1)/sqrt(1-z)*(1+sqrt(1-z))^(1-2*a)$

/* A&S 15.1.15 */
hgfred([a,1-a],[3/2],sin(z)^2);
sin((1-2*a)*z)/sin(z)/(1-2*a)$

/* A&S 15.1.16 */
hgfred([a,2-a],[3/2],sin(z)^2);
sin((2-2*a)*z)/(2-2*a)/(sin(z)*cos(z))$

/* A&S 15.1.17 */
assume(not(equal(sin(z),0)));
[not equal(sin(z),0)]$

hgfred([-a,a],[1/2],sin(z)^2);
cos(2*a*z)$

/* A&S 15.1.18 */
hgfred([a,1-a],[1/2],sin(z)^2);
cos((2*a-1)*z)/cos(z)$

/* A&S 15.1.19 */
assume(not(equal(tan(z),0)));
[not equal(tan(z), 0)]$

hgfred([a,a+1/2],[1/2],-tan(z)^2);
cos(z)^(2*a)*cos(2*a*z)$

/* A&S 15.4.3 */
assume(x > 0);
[x > 0]$

hgfred([-n,n],[1/2],x);
cos(2*n*asin(sqrt(x)))$

hgfred([-10,10],[1/2],x);
chebyshev_t(10,1-2*x)$

/* A&S 15.4.4 */
hgfred([-n,n+1],[1],x);
legendre_p(n,1-2*x);

/* A&S 8.1.2
 * Is there a bug in that formula?
 * http://functions.wolfram.com/HypergeometricFunctions/LegendreP2General/26/01/02/ 
 * shows the formula with (1+z) and (1-z).  I'm going to use the formula from the 
 * given link.
 *
 * But this differs from formula 14 in Higher Transcendental Functions.
 *
 * Now what?
 *
 * This tests the legf14 function.
 */
radcan(((1+z)/(1-z))^(u/2)/gamma(1-u)*hgfred([-v,v+1],[1-u],(1-z)/2));
assoc_legendre_p(v,u,z)$

/* A&S 15.4.17 
 *
 * F(a,1-a;c;x) = gamma(c)*x^(1/2-c/2)*(1-x)^(c/2-1/2)*P(-a,1-c,1-2*x)
 *
 * This differs from formula 14!  Now what?
 *
 * This tests the legf14 function.
 */
hgfred([a,1-a],[c],x);
assoc_legendre_p(-a, 1-c, 1-2*x)*gamma(c)*2^((1-c)/2)*x^((1-c)/2)/(2-2*x)^((1-c)/2)$

/* A&S 15.4.10 
 *
 * F(a,a+1/2;c;z) = 2^(c-1)*gamma(c)*z^(1/2-c/2)*(1-z)^(c/2-a-1/2)*P(2*a-c,1-c,1/sqrt(1-z))
 *
 * Test for legf24 function.
 */
hgfred([a,a+1/2],[c],x);
2^(c-1)*gamma(c)*z^(1/2-c/2)*(1-z)^(c/2-a-1/2)*assoc_legendre_p(2*a-c,1-c,1/sqrt(1-z))$

2^u*(z^2-1)^(-u/2)*z^(v+u)/gamma(1-u)*hgfred([-v/2-u/2,1/2-v/2-u/2],[1-u],1-1/z^2);
assoc_legendre_p(v,u,z)$

/* A&S 15.4.12
 * Test for legf20 function.
 */
hgfred([a,b],[a+b+1/2],x);
2^(a+b-1/2)*gamma(a+b+1/2)*x^(1/2*(1/2-a-b))*assoc_legendre_p(a-b-1/2,1/2-a-b,sqrt(1-x))$

2^m*(z^2-1)^(-m/2)/gamma(1-m)*hgfred([1/2+n/2-m/2, -n/2-m/2], [1-m], 1-z^2);
assoc_legendre_p(n,m,z)$

/*
 * A&S 15.4.7
 * Test for legf36 function.
 */
hgfred([a,b],[2*b],z);
2^(2*b)/sqrt(%pi)*gamma(b+1/2)/gamma(2*b-a)*z^(-b)*(1-z)^((b-a)/2)*exp(%i*%pi*(a-b))*assoc_legendre_q(b-1,b-a,2/z-1)$

2^v*gamma(1+v)*gamma(1+v+u)*(z+1)^(u/2-v-1)*(z-1)^(-u/2)/gamma(2+2*v)*hgfred([1+v-u,1+v],[2+2*v],2/(1+z));
assoc_legendre_q(v, u, z)$

/*
 * A&S 15.4.14
 *
 * F(a,b;a-b+1;z) = gamma(a-b+1)*z^(b/2-a/2)*(1-z)^(-b)*P(-b,b-a,(1+z)/(1-z))
 *
 * Test for legf16 function
 */
radcan(hgfred([a,b],[a-b+1],z));
gamma(-b+a+1)*z^(b/2-a/2)*(1-z)^(-b)*assoc_legendre_p(-b,b-a,(1+z)/(1-z))$

/*
 * Test for legf16 function
 */
2^(-v)*(z+1)^(u/2+v)*(z-1)^(-u/2)/gamma(1-u)*ratsimp(hgfred([-v,-v-u],[1-u],(z-1)/(z+1)));
assoc_legendre_p(v, u, z)$

/* Regression tests */

/* simpg tests */

/* F([...,0,...],[non-zero], z) is 1 */
hgfred([a,0],[c], z);
1$

/* F([non-zero],[...,zero or neg integer,...],z) is undefined */
hgfred([a,b],[0],z);
%f[2,1]([a,b],[0],z)$

hgfred([a,b],[c,4,-3],z);
%f[2,3]([a,b],[c,4,-3],z)$

/* F([...,neg integer,...],[...],z) is a polynomial */

/* Test 1F1 polynomials */
hgfred([-7],[c],z);
5040*gen_laguerre(7,c-1,z)*gamma(c)/gamma(c+7)$

hgfred([-2],[1],z);
laguerre(2,z)$

hgfred([-7],[1],z);
laguerre(7,z)$

hgfred([-2],[1/2],z);
hermite(4,sqrt(z))/12$

hgfred([-2],[3/2],z);
hermite(5,sqrt(z))/(120*sqrt(z))$

hgfred([a],[a+1],-z);
a*%gammagreek(a,z)/z^a$

hgfred([a],[a+2],-z);
a*(a+1)*%gammagreek(a,z)/z^a-a*(a+1)*%gammagreek(a+1,z)*z^(-a-1)$

/* Test 2F0 polynomials */
hgfred([-3,-3+1/2],[],z);
-hermite(6,%i/sqrt(z))*z^3/64$

hgfred([-3-1/2,-3],[],z);
%i*hermite(7,%i/sqrt(z))*z^(7/2)/128$

hgfred([-2,-4],[],z);
2*gen_laguerre(2,2,-1/z)*z^2$

/* Test that 2F0([a,b],...) = 2F0([b,a],...) */
hgfred([-4,-2],[],z);
2*gen_laguerre(2,2,-1/z)*z^2$

/* Test 2F1 polynomials */

hgfred([-2,1],[1/2],z);
8*jacobi_p(2,-1/2,-3/2,1-2*z)/3$
