/*************** -*- Mode: MACSYMA; Package: MAXIMA -*-  ******************/
/***************************************************************************
***                                                                    *****
***     Copyright (c) 1984 by William Schelter,University of Texas     *****
***     All rights reserved                                            *****
***************************************************************************/


(kill(all),
DECLARE(L,SCALAR,[M1,M2,M3],NONSCALAR));
DONE$
EXPAND((1-L*M1) . (1-L*M2) . (1-L*M3));
-(L*M3)+(L*M2) . (L*M3)-(L*M2)+(L*M1) . (L*M3)-(L*M1) . (L*M2) . (L*M3)+(L*M1) . (L*M2)-L*M1+1;
EV(%,DOTSCRULES);
-L*M3+L^2*(M2 . M3)-L*M2+L^2*(M1 . M3)-L^3*(M1 . M2 . M3)+L^2*(M1 . M2)-L*M1+1$
RAT(%,L);
-(M1 . M2 . M3)*L^3+(M2 . M3+M1 . M3+M1 . M2)*L^2+(-M3-M2-M1)*L+1$
RAT(X^2);
X^2$
DIFF(F(%),X);
'DIFF(F(X^2),X,1)$
((X-2*Y)^4/(X^2-4*Y^2)^2+1)*(Y+A)*(2*Y+X)/(4*Y^2+X^2);
(Y+A)*(2*Y+X)*((X-2*Y)^4/(X^2-4*Y^2)^2+1)/(4*Y^2+X^2)$
RAT(%,Y,A,X);
(2*A+2*Y)/(X+2*Y)$
(X+3)^20;
(X+3)^20$
RAT(%);
X^20+60*X^19+1710*X^18+30780*X^17+392445*X^16+3767472*X^15+28256040*X^14
    +169536240*X^13+826489170*X^12+3305956680*X^11+10909657044*X^10
    +29753610120*X^9+66945622770*X^8+123591918960*X^7+185387878440*X^6
    +222465454128*X^5+208561363245*X^4+147219785820*X^3+73609892910*X^2
    +23245229340*X+3486784401$
DIFF(%,X);
20*X^19+1140*X^18+30780*X^17+523260*X^16+6279120*X^15+56512080*X^14
       +395584560*X^13+2203971120*X^12+9917870040*X^11+36365523480*X^10
       +109096570440*X^9+267782491080*X^8+535564982160*X^7+865143432720*X^6
       +1112327270640*X^5+1112327270640*X^4+834245452980*X^3+441659357460*X^2
       +147219785820*X+23245229340$
FACTOR(%);
20*(X+3)^19$
RATWEIGHT(A,1,B,1);
[A,1,B,1]$
EXP:RAT(A+B+1);
B+A+1$
%^2;
B^2+(2*A+2)*B+A^2+2*A+1$
EV(EXP^2,RATWTLVL:1);
2*B+2*A+1;
POLY:1.0E-20*X^2-5.5*X+5.2E+20;
1.0E-20*X^2-5.5*X+5.2E+20$
ERRCATCH(EV(%,X = 1.0E+20));
[7.0E19];
/*  []$ floating point on lispm or tops20 can't do this */
EV(HORNER(POLY,X),KEEPFLOAT);
(1.0E-20*X-5.5)*X+5.2E+20$
EV(%,X = 1.0E+20);
7.0E19;
DIVIDE(X+Y,X-Y,X);
[1,2*Y]$
DIVIDE(X+Y,X-Y);
[-1,2*X]$
CONTENT(2*X*Y+4*X^2*Y^2,Y);
[2*X,2*X*Y^2+Y]$
RESULTANT(A*Y+X^2+1,Y^2+X*Y+B,X);
Y^4+A*Y^3+(2*B+1)*Y^2+B^2$
BEZOUT(A*Y+X^2+1,Y^2+X*Y+B,X);
MATRIX([Y^2+B,-A*Y^2-Y],[Y,Y^2+B])$
EXPAND(DETERMINANT(%));
Y^4+A*Y^3+2*B*Y^2+Y^2+B^2$
%-EXPAND(RESULTANT(A*Y+X^2+1,Y^2+X*Y+B,X));
0$
FACTOR(POLY_DISCRIMINANT((X-A)*(X-B)*(X-C),X));
(B-A)^2*(C-A)^2*(C-B)^2$
EXP:(4*X^3+10*X-11)/(X^5+5);
(4*X^3+10*X-11)/(X^5+5)$
EV(MOD(%),MODULUS:3);
(X^2+X-1)/(X^4+X^3+X^2+X+1)$
RATDIFF(EXP,X);
-(8*X^7+40*X^5-55*X^4-60*X^2-50)/(X^10+10*X^5+25)$
10*(1+%I)/(3^(1/3)+%I);
10*(%I+1)/(%I+3^(1/3))$
EV(RATDISREP(RAT(%)),ALGEBRAIC);
(4*3^(2/3)-2*3^(1/3)-4)*%I+2*3^(2/3)+4*3^(1/3)-2$
TELLRAT(A^2+A+1);
[A^2+A+1]$
A/(SQRT(2)+SQRT(3))+1/(A*SQRT(2)-1);
1/(SQRT(2)*A-1)+A/(SQRT(3)+SQRT(2))$
EV(RATDISREP(RAT(%)),ALGEBRAIC);
((7*SQRT(3)-10*SQRT(2)+2)*A-2*SQRT(2)-1)/7$
TELLRAT(Y^2 = X^2);
[Y^2-X^2,A^2+A+1]$
TAYLOR(1+X,[X,0,3]);
1+X$
1/%;
1-X+X^2-X^3$
TAYLOR(1+X+Y+Z,[X,0,3],[Y,1,2],[Z,2,1]);
4+(Z-2)+(Y-1)+X$
1/%;
1/4-(Z-2)/16+(-1/16+(Z-2)/32)*(Y-1)+(1/64-3*(Z-2)/256)*(Y-1)^2
 +(-1/16+(Z-2)/32+(1/32-3*(Z-2)/128)*(Y-1)+(-3/256+3*(Z-2)/256)*(Y-1)^2)*X
 +(1/64-3*(Z-2)/256+(-3/256+3*(Z-2)/256)*(Y-1)+(3/512-15*(Z-2)/2048)*(Y-1)^2)
  *X^2
 +(-1/256+(Z-2)/256+(1/256-5*(Z-2)/1024)*(Y-1)
		   +(-5/2048+15*(Z-2)/4096)*(Y-1)^2)
  *X^3$
TAYLOR(1+X+Y+Z,[X,0,3],[Y,0,3],[Z,0,3]);
1+Z+Y+X$
1/%;
1-Z+Z^2-Z^3+(-1+2*Z-3*Z^2+4*Z^3)*Y+(1-3*Z+6*Z^2-10*Z^3)*Y^2
	   +(-1+4*Z-10*Z^2+20*Z^3)*Y^3
 +(-1+2*Z-3*Z^2+4*Z^3+(2-6*Z+12*Z^2-20*Z^3)*Y+(-3+12*Z-30*Z^2+60*Z^3)*Y^2
		     +(4-20*Z+60*Z^2-140*Z^3)*Y^3)
  *X
 +(1-3*Z+6*Z^2-10*Z^3+(-3+12*Z-30*Z^2+60*Z^3)*Y+(6-30*Z+90*Z^2-210*Z^3)*Y^2
		     +(-10+60*Z-210*Z^2+560*Z^3)*Y^3)
  *X^2
 +(-1+4*Z-10*Z^2+20*Z^3+(4-20*Z+60*Z^2-140*Z^3)*Y
		       +(-10+60*Z-210*Z^2+560*Z^3)*Y^2
		       +(20-140*Z+560*Z^2-1680*Z^3)*Y^3)
  *X^3$
EV(SUM(I^2+2^I,I,0,N),SIMPSUM);
2^(N+1)+(2*N^3+3*N^2+N)/6-1$
EV(SUM(3^-I,I,1,INF),SIMPSUM);
1/2$
EV(SUM(I^2,I,1,4)*SUM(1/I^2,I,1,INF),SIMPSUM);
5*%PI^2$
SUM(I^2,I,1,5);
55$
PRODUCT(X+I*(I+1)/2,I,1,4);
(X+1)*(X+3)*(X+6)*(X+10)$
LIMIT(X*LOG(X),X,0,PLUS);
0$
LIMIT((1+X)^(1/X),X,0);
%E$
LIMIT(%E^X/X,X,INF);
INF$
LIMIT(SIN(1/X),X,0);
IND$
NUSUM(N*N!,N,0,N);
(N+1)!-1$
NUSUM(N^4*4^N/BINOMIAL(2*N,N),N,0,N);
2*(N+1)*(63*N^4+112*N^3+18*N^2-(22*N)+3)*4^N/(693*BINOMIAL(2*N,N))-2/(3*11*7)$
UNSUM(%,N);
N^4*4^N/BINOMIAL(2*N,N)$
UNSUM(PRODUCT(I^2,I,1,N),N);
('PRODUCT(I^2,I,1,N-1))*(N-1)*(N+1)$
NUSUM(%,N,1,N);
'PRODUCT(I^2,I,1,N)-1$
FUNCSOLVE((N+1)*F(N)-(N+3)*F(N+1)/(N+1) = (N-1)/(N+2),F(N));
F(N) = N/((N+1)*(N+2))$
(untellrat(a),'done);
done$
RESIDUE(S/(S^2+A^2),S,A*%I);
1/2$
RESIDUE(SIN(A*X)/X**4,X,0);
-A^3/6$
TAYLOR(SQRT(1+A*X+SIN(X)),X,0,3);
1+(A+1)*X/2-(A^2+2*A+1)*X^2/8+(3*A^3+9*A^2+9*A-1)*X^3/48$
%^2;
1+(A+1)*X-X^3/6$
TAYLOR(SQRT(1+X),X,0,5);
1+X/2-X^2/8+X^3/16-5*X^4/128+7*X^5/256$
%^2;
1+X$
PRODUCT((X^I+1)^2.5,I,1,INF)/(X^2+1);
('PRODUCT((X^I+1)^2.5,I,1,INF))/(X^2+1)$
EV(TAYLOR(%,X,0,3),KEEPFLOAT);
1+2.5*X+3.375*X^2+6.5625*X^3$
TAYLOR(1/LOG(1+X),X,0,3);
1/X+1/2-X/12+X^2/24-19*X^3/720$
TAYLOR(COS(X)-SEC(X),X,0,5);
-X^2-X^4/6$
TAYLOR((COS(X)-SEC(X))^3,X,0,5);
+0$
TAYLOR((COS(X)-SEC(X))^-3,X,0,5);
-1/X^6+1/(2*X^4)+11/(120*X^2)-347/15120-6767*X^2/604800-15377*X^4/7983360$
TAYLOR(SQRT(1-K^2*SIN(X)^2),X,0,6);
1-K^2*X^2/2-(3*K^4-4*K^2)*X^4/24-(45*K^6-60*K^4+16*K^2)*X^6/720$
TAYLOR((1+X)^N,X,0,4);
1+N*X+(N^2-N)*X^2/2+(N^3-3*N^2+2*N)*X^3/6+(N^4-6*N^3+11*N^2-6*N)*X^4/24$
TAYLOR(SIN(X+Y),X,0,3,Y,0,3);
Y-Y^3/6+(1-Y^2/2)*X+(-Y/2+Y^3/12)*X^2+(-1/6+Y^2/12)*X^3$
TAYLOR(SIN(X+Y),[X,Y],0,3);
X+Y-(X^3+3*Y*X^2+3*Y^2*X+Y^3)/6$
TAYLOR(1/SIN(X+Y),X,0,3,Y,0,3);
1/Y+Y/6+(-1/Y^2+1/6)*X+(+1/Y^3)*X^2+(+(-1/Y^4))*X^3$
END;
END$
